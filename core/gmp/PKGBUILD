# Port: math/gmp

pkgname=gmp
pkgver=5.1.3
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i386' 'amd64')
url="http://gmplib.org/"
depends=('freebsd-world')
license=('LGPL3')
options=('libtoolfix')
install=gmp.install
source=(ftp://ftp.gmplib.org/pub/gmp-${pkgver}/gmp-${pkgver}.tar.xz{,.sig})
md5sums=('e5fe367801ff067b923d1e6a126448aa'
         'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e  "s/\(athlon64\-\*\-\*\)/amd64-*-* | \1/;s/\(x86_64)\)/amd64 | \1/" \
    configure

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ "${CARCH}" == "amd64" ]; then
    conf_args=(ABI=64)

  else
    conf_args=(ABI=32)
  fi

  ./configure --build=${CHOST} \
    --prefix=/usr --enable-cxx \
    ${conf_args[@]} \
    MAKEINFO="makeinfo --no-split"

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
}
