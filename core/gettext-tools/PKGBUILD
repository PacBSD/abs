# $Id: PKGBUILD 174268 2013-01-03 13:58:09Z allan $
# Maintainer:
# Contributor Wolfgang Bumiller <blub@speed.at>
# Port devel/gettext-tools

pkgname=gettext-tools
_pkgname=gettext
pkgver=0.19.8.1
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
groups=('base-devel')
depends=('libc' 'libiconv' 'gettext-runtime')
options=('!docs' 'iconv')
patchdir=("${srcdir}/${_pkgname}-${pkgver}/${pkgname}")
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${_pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871') # Daiki Ueno
sha256sums=('ff942af0e438ced4a8b0ea4b0b6e0d6d657157c5e2364de57baa279c1c125c43'
            'SKIP')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"

# Exclude examples and gnulib-tests.
	sed -i '' -E '/^SUBDIRS =/s/(examples|gnulib-tests)//g' \
		Makefile.in
	sed -i '' -e \
		'/^install-data-am:/s/install-examplesbuildauxDATA//' \
		Makefile.in m4/Makefile.in
# Do not install html copies of manpages.
	sed -i '' \
		-e '/^all-local:/s/html-local//' \
		-e '/^install-data-local:/s/install-html//' \
		-e '/^installdirs-local:/s/installdirs-html//' \
		man/Makefile.in

# Do not patch system float.h.
	sed -i '' -e 's/FLOAT_H=float\.h//' \
		configure
# Use libintl installed by gettext-runtime.
	sed -i '' -e '/^LDADD_no =/s,\.\./intl/libgnuintl.la,,' \
		tests/Makefile.in
# Use libasprintf installed by gettext-runtime.
	sed -i '' -e 's,[^ ]*libasprintf.la,-lasprintf,' \
		tests/lang-c++


}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
  CFLAGS+=" -L/usr/local/lib"
	./configure --prefix=/usr \
		--disable-csharp --disable-java --disable-openmp \
		--with-included-glib --with-included-libcroco \
		--with-included-libunistring --with-included-libxml \
		--without-emacs --without-git ac_cv_lib_rt_sched_yield=no \
		--disable-curses ICONV_LIBS="-liconv"
  gmake
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
  gmake check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755 ${pkgdir}/usr/lib/gettext

  rm ${pkgdir}/usr/lib/charset.alias
}
