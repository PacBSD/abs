# $Id: PKGBUILD 174267 2013-01-03 13:58:07Z allan $
# Maintainer:

pkgname=gawk
pkgver=4.1.3
pkgrel=9
pkgdesc="GNU version of awk"
arch=('i386' 'amd64')
url="http://www.gnu.org/directory/GNU/gawk.html"
license=('GPL')
groups=('base-devel')
depends=('mpfr' 'gmp' 'gettext-runtime')
provides=('awk')
install=gawk.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('55d37f4069502677f25d1340df8eec97'
         'SKIP')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}

	sed -i '' -e 's/ -export-dynamic/ -Wl,--export-dynamic/' \
		configure
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	AWK=/usr/bin/awk ./configure --prefix=/usr \
	--libexecdir=/usr/libexec --with-libiconv-prefix=/usr/local \
	--without-libsigsegv

	make 
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	# one test failure calling mktime. we can ignore this
	make check || true
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	rm -f "${pkgdir}/usr/bin/awk"
}
