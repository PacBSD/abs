# Port: devel/isl

pkgname=isl
pkgver=0.17.1
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('i386' 'amd64')
url="http://freecode.com/projects/isl"
depends=('gmp' 'libc')
license=('MIT')
options=('libtoolfix')
source=(http://isl.gforge.inria.fr/$pkgname-$pkgver.tar.bz2)
sha256sums=('d6307bf9a59514087abac3cbaab3d99393a0abb519354f7e7834a8c842310daa')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '' -e 's@pkgconfig_libdir=.*@pkgconfig_libdir=${prefix}/libdata/pkgconfig@' configure
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/licenses/isl
  install -m644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}
