#
# This package is mostly there to build llvm parts which need it,
# and to provide c++filt

pkgname=gnu-binutils
pkgver=2.25
pkgrel=12
pkgdesc="Provides additional binutils like the gold linker."
arch=('i686' 'amd64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
#checkdepends=('dejagnu' 'bc')
depends=('gcc-libs')
options=('staticlibs' '!distcc' '!ccache')
install=binutils.install
provides=('c++filt')
patchdir=("binutils-${pkgver}")
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig})
md5sums=('d9f3303f802a5b6b0bb73a335ab89d66'
         'SKIP')

prepare() {
  cd "${srcdir}/binutils-${pkgver}"

  find . -name Makefile.in \
    | xargs sed -i '' -e '/SUBDIRS =/s/ doc//'
}

build() {
  cd ${srcdir}

  mkdir binutils-build && cd binutils-build

  ${srcdir}/binutils-${pkgver}/configure --prefix=/usr/local \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://bugs.archbsd.net/ \
    --enable-gold --enable-plugins \
    --with-pic --enable-shared \
    --disable-werror --enable-multilib \
    --disable-nls

  cd ${srcdir}/binutils-build
  # check the host environment and makes sure all the necessary tools are available
  gmake configure-host
  gmake tooldir=/usr/local
}

check() {
  cd ${srcdir}/binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  gmake LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/binutils-build
  gmake prefix=${pkgdir}/usr/local tooldir=${pkgdir}/usr/local install

  # Add some useful headers
  install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}/usr/local/include
  install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}/usr/local/include

  # Install PIC libiberty.a
  install -m644 libiberty/pic/libiberty.a ${pkgdir}/usr/local/lib

  #Install Plugin.api
  install -m644 ${srcdir}/binutils-${pkgver}/include/plugin-api.h ${pkgdir}/usr/local/include

  # No shared linking to these files outside binutils
  rm ${pkgdir}/usr/local/lib/lib{bfd,opcodes}.so

  rm -rf "${pkgdir}/usr/local/share/info"
  # screw those
  rm -rf "${pkgdir}/usr/local/share/man"
}
