# Maintainer: Anthony Donnelly <amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=curl
pkgver=7.45.0
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('i686' 'amd64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca_root_nss')
source=("http://curl.haxx.se/download/$pkgname-$pkgver.tar.gz"{,.asc}
        curlbuild.h)
sha1sums=('e44fc290844c1e3c9b7f6ef9926d4366d0600514'
          'SKIP'
          '67da0e46ce02287ad1f4f93e73160835cbb54dc7')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --sysconfdir=/etc \
      --disable-dependency-tracking \
      --disable-ldap \
      --disable-ldaps \
      --enable-ipv6 \
      --enable-manual \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --without-libidn \
      --with-random=/dev/urandom \
      --with-ca-bundle=/usr/share/certs/ca-root-nss.crt \
      --without-libssh2 --without-librtmp \
      OPENSSL_CFLAGS="-DNO_IDEA" \
      ac_cv_func_SSLv2_client_method=no

  gmake
}

package() {
  cd "$pkgname-$pkgver"

  gmake DESTDIR="$pkgdir" install

  local ptrsize=$(cpp <<<'__SIZEOF_POINTER__' | sed '/^#/d')
  case $ptrsize in
    8) _curlbuild=curlbuild-64.h ;;
    4) _curlbuild=curlbuild-32.h ;;
    *) error "unknown pointer size for architecture: %s bytes" "$ptrsize"
      exit 1
      ;;
  esac

  # license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # devel
  install -dm755 "$pkgdir/usr/share/aclocal/"
  install -m644 docs/libcurl/libcurl.m4 "$pkgdir/usr/share/aclocal/libcurl.m4"

  mv "$pkgdir/usr/include/curl/curlbuild.h" "$pkgdir/usr/include/curl/$_curlbuild"
  install -m644 "$srcdir/curlbuild.h" "$pkgdir/usr/include/curl/curlbuild.h"
}
