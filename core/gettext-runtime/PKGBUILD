# Maintainer:
# Contributor Wolfgang Bumiller <blub@speed.at>
# Port: devel/gettext-runtime

pkgname=gettext-runtime
_pkgname=gettext
pkgver=0.19.8.1
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
groups=('base-devel')
depends=('libc' 'libiconv')
replaces=('gettext')
provides=('gettext')
conflicts=('gettext')
options=('!docs' 'iconv' 'libtoolfix')
patchdir=("${srcdir}/${_pkgname}-${pkgver}/${pkgname}")
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${_pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871') # Daiki Ueno
sha256sums=('ff942af0e438ced4a8b0ea4b0b6e0d6d657157c5e2364de57baa279c1c125c43'
            'SKIP')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"

# Do not install csharp and java documentation.
	sed -i '' -E '/^SUBDIRS =/s/(intl-csharp|intl-java)//g' \
		Makefile.in
# Do not install html copies of manpages.
	sed -i '' \
		-e '/^all-local:/s/html-local//' \
		-e '/^install-data-local:/s/install-html//' \
		-e '/^installdirs-local:/s/installdirs-html//' \
		libasprintf/Makefile.in \
		man/Makefile.in

}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"

	./configure --prefix=/usr \
		--disable-csharp --disable-java --with-included-gettext \
		--with-libiconv-prefix=/usr/local ac_cv_lib_rt_sched_yield=no

  gmake
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
  gmake check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755 ${pkgdir}/usr/lib/gettext

  ln -s libintl.so.8 ${pkgdir}/usr/lib/libintl.so.9

}
