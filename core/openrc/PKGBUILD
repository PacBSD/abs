_src_uri="http://dev.gentoo.org/~williamh/dist"
_net_uri="http://dev.gentoo.org/~robbat2/distfiles"
_net="netifrc"
_nver=0.3.1

pkgbase=openrc
pkgname=(openrc tmpfiles)
pkgver=0.17
pkgrel=8
arch=('i386' 'amd64')
url='http://www.gentoo.org/proj/en/base/openrc/'
license=('BSD')
source=("${_src_uri}/${pkgname}-${pkgver}.tar.bz2"
	"${_net_uri}/${_net}-${_nver}.tar.bz2"
	{init,conf}.d-sshd
	{init,conf}.d-modules
	{init,conf}.d-ntp-client
	{init,conf}.d-ldconfig
	init.d-jail
	init.d-cron
	init.d-pflog
        init.d-devfs
	init.d-{zfs,zvol}
)
md5sums=('de0737609c5f803470b186b6506994a2'
         '542ed7d16d6fc1116aa95ad8bc841632'
         'a0cdcd1d1f57e29614e41eb5e7588d4b'
         'b35e9f3829f4cfca07168fcba98749c7'
         '2ba1445f7bd40fb0bbda075754952423'
         '0e7438caba45a532c4470a507451158e'
         'e453487fc7293a1ab4f45415e14784cf'
         '5527c30b1f101bfe27b061d2a8d4dacb'
         '8e4321df09e6b6bba7eec313226c3eb8'
         '7302ddb853270d46f31b2d96e105c1b8'
         '551c79a75661f0239bcea1da3d3011cb'
         '0971170976cae102427e8cf0492a2e06'
         '4050d02947497d93fe65b1b2026f91af'
         'ca5aecd2119e30803815ba55b02bf7e2'
         '526dfc5a64618a75a2d85c0b92777aa9'
         '45464ade52b069e26b2d01eaee403b0f')

_makeargs=(BRANDING='Pac BSD' OS=FreeBSD MKNET=no)
_net_args=( "${_base_args[@]}")

prepare() {
  cd "$srcdir"

  find "$srcdir"/$pkgname-$pkgver/init.d -type f | xargs sed -i '' -e 's|sbin/runscript|sbin/openrc-run|g'
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  
#  find "$srcdir"/$pkgname-$pkgver -name *.sh | xargs sed -i '' -e 's|/bin/sh|/usr/bin/bash|g'
  gmake "${_makeargs}"

  cd "${srcdir}/${_net}-${_nver}"
  sed -i '' -e 's|sbin/runscript|sbin/openrc-run|g' init.d/net.lo.in
  gmake "${_net_args[@]}" OS=FreeBSD

}

package_openrc() {
  pkgdesc='Dependency based init system that works with sysvinit.'
  depend=('bash' 'tmpfiles')
  provides=('init')
  conflicts=('init')
  backup=('etc/rc.conf')

  cd "$srcdir/$pkgbase-$pkgver"
  gmake DESTDIR="$pkgdir" "${_makeargs[@]}" install

  rm -f ${pkgdir}/usr/share/man/man8/rc.8
  rm -f ${pkgdir}/usr/share/man/man8/service.8

  install -dm755 "${pkgdir}/etc/init.d"
  install -dm755 "${pkgdir}/etc/conf.d"

  # Services with both init.d and conf.d files
  for i in sshd modules ntp-client ldconfig
  do
	install -m755 "${srcdir}/init.d-$i" "${pkgdir}/etc/init.d/$i"
	install -m644 "${srcdir}/conf.d-$i" "${pkgdir}/etc/conf.d/$i"
  done

  # For which there is no conf.d
  for i in cron pflog devfs zfs zvol jail
  do
  	install -m755 "${srcdir}/init.d-$i" "${pkgdir}/etc/init.d/$i"
  done
  for i in ldconfig devd devfs modules; do
        ln -sf /etc/init.d/${i} ${pkgdir}/etc/runlevels/boot/${i}
  done

  #urandom seed fix
  install -d -m755 "${pkgdir}/var/lib/misc"

  install -d -m755 ${pkgdir}/var/log

  for i in messages security auth.log maillog lpd-errs xferlog cron debug.log ppp.log; do
	touch ${pkgdir}/var/log/${i}
  done

# netifrc
  cd "${srcdir}/${_net}-${_nver}"
  gmake DESTDIR="${pkgdir}" "${_net_args[@]}" install
  install -m644 "${srcdir}/${_net}-${_nver}/doc/net.example" "${pkgdir}/etc/conf.d/net"

  ln -sf "/etc/init.d/net.lo" "${pkgdir}/etc/runlevels/boot/net.lo0"

  rm ${pkgdir}/libexec/rc/sh/tmpfiles.sh

  for f in `find -E ${pkgdir}/etc/conf.d -type f`; do
    backup+=("etc/conf.d/${f##*/}")
  done

}
package_tmpfiles() {
  pkgdesc='Reimplementation of the systemd tmpfiles.d code'
  arch=('any')

  install -dm755  -o root -g wheel ${pkgdir}/libexec/rc/sh
  install -m755 ${srcdir}/${pkgbase}-${pkgver}/sh/tmpfiles.sh ${pkgdir}/libexec/rc/sh/tmpfiles.sh
}
