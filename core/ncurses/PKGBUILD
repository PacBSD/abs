# $Id: PKGBUILD 150103 2012-02-12 13:47:59Z allan $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=ncurses
pkgver=6.0
pkgrel=10
pkgdesc="System V Release 4.0 curses emulation library"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
depends=('libc' 'gcc-libs')
source=(ftp://invisible-island.net/ncurses/ncurses-${pkgver/_/-}.tar.gz{,.asc})
md5sums=('ee13d052e1ead260d7c28071f46eefb1'
         'SKIP')

prepare() {
  mkdir ncurses{,w}-build
  unset ac_cv_build
  unset ac_cv_host
}

build() {
  cd ncursesw-build
  case $CARCH in
    i386)
      build="--host=${CARCH}-pacbsd-freebsd11.0"
      ;;
    amd64)
      build="--host=x86_64-pacbsd-freebsd11.0"
      ;;
    esac
     
  ../$pkgname-${pkgver/_/-}/configure --prefix=/usr/local --mandir=/usr/share/man \
     --with-shared --with-normal --without-debug --without-ada --with-pkg-config=yes "${build}"\
     --with-pkg-config-libdir=/usr/libdata/pkgconfig --enable-widec --enable-pc-files --with-cxx-binding --with-cxx-shared
  # add --enable-ext-colors and --enable-ext-mouse with next soname bump
  gmake

  # libraries for external binary support
  cd ${srcdir}/ncurses-build
  [[ $CARCH = "amd64" ]] && CONFIGFLAG="--with-chtype=long"
  ../$pkgname-${pkgver/_/-}/configure --prefix=/usr/local "${build}" \
    --with-shared --with-normal --without-debug --without-ada $CONFIGFLAG --with-pkg-config=yes \
    --with-cxx-binding --with-cxx-shared --with-pkg-config-libdir=/usr/libdata/pkgconfig
  gmake
}

package() {
  cd ncursesw-build
  gmake DESTDIR="$pkgdir" install

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    echo "INPUT(-l${lib}w)" > "$pkgdir"/usr/local/lib/lib${lib}.so
  done

  for lib in ncurses ncurses++ form panel menu; do
    ln -s ${lib}w.pc "$pkgdir"/usr/libdata/pkgconfig/${lib}.pc
  done

  # some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" > "$pkgdir"/usr/local/lib/libcursesw.so
  ln -s libncurses.so "$pkgdir"/usr/local/lib/libcurses.so

  # install license, rip it from the readme
  cd "$srcdir"/$pkgname-${pkgver/_/-}
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  grep -B 100 '$Id' README > "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  rm -r "${pkgdir}/usr/share/man"
}

