# Port: security/sudo

pkgname=sudo
pkgver=1.8.17
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('i386' 'amd64')
url="http://www.sudo.ws/sudo/"
license=('custom')
backup=('etc/sudoers' 'etc/pam.d/sudo')
depends=('gettext-runtime' 'libiconv')
source=(http://www.sudo.ws/sudo/dist/$pkgname-$pkgver.tar.gz{,.sig}
        sudo.pam)
validpgpkeys=('CCB24BE9E9481B15D34159535A89DFA27EE470C4')
sha256sums=('62b12c4fa9a3ad4f20f6e7576bc6405b2ec8d76222ea44a1c94830c68cccec8c'
            'SKIP'
            '7325f2c17a8a7c3efcb5a03d2d328bacc2459077286543eaf8a80f7dd49dd53c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  export LDFLAGS="${LDFLAGS} -L/usr/lib -lintl -lgcc -lssp_nonshared"

}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export ac_cv_build="${CHOST}"

  sed -i '' \
      -e 's,\$(srcdir)/sudoers2ldif \$(DESTDIR)\$(docdir),$(srcdir)/sudoers2ldif $(DESTDIR)$(bindir),' plugins/sudoers/Makefile.in

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-logfac=local2 \
    --with-pam \
    --with-env-editor \
    --with-long-otp-prompt \
    --with-logincap \
    --with-tty-tickets \
    --with-ignore-dot \
    --with-all-insults \
    --libexecdir=/usr/libexec
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  # gettext fu?
  #make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/etc/pam.d/"
  install -m644 "$srcdir/sudo.pam" "$pkgdir/etc/pam.d/sudo"

  install -dm755 "$pkgdir/usr/share/licenses/sudo/"
  install -m644 doc/LICENSE "$pkgdir/usr/share/licenses/sudo/LICENSE"
}

# vim:set ts=2 sw=2 et:
