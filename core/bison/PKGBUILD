# $Id: PKGBUILD 173327 2012-12-19 01:16:33Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=bison
pkgver=2.7
pkgrel=5
pkgdesc="The GNU general-purpose parser generator"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=('gettext' 'gm4')
options=('!emptydirs')
groups=('base-devel')
install=bison.install
source=(ftp://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('234cdfac99257cf99ac4a03c898f37b9'
         '853204af1f9a5602d84989b4852e317e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  rm -f doc/bison.info*
  sed -i '' -e 's/MANS = yacc.1/MANS =/' doc/Makefile.in

  export M4=/usr/bin/gm4
  ./configure --prefix=/usr --datadir=/usr/share \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --disable-yacc
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -f "${pkgdir}/usr/lib/charset.alias"
}
