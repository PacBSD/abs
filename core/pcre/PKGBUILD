# $Id: PKGBUILD 172477 2012-12-03 21:23:28Z allan $
# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>
# Port: devel/pcre
pkgname=pcre
pkgver=8.38
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i386' 'amd64')
url="http://www.pcre.org/"
license=('BSD')
options=('libtoolfix')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8') # Philip Hazel
source=("ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/$pkgname-$pkgver.tar.bz2"{,.sig})
md5sums=('00aabbfe56d5a48b270f999b508c5ad2'
         'SKIP')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  # the ports pkg adds this flag for amd64/ia64
  if [[ "${CARCH}" == "amd64" ]]; then
    CFLAGS="${CFLAGS} -fPIC"
  fi

  # ports comment on this:
  # work around for a autoconf assumption, to fix c++ linking with clang
  sed -i '' -e 's|GREP "\\-L"|GREP "conftest\\."|g' configure

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --enable-pcre8 --enable-pcre16 --enable-pcre32 --enable-jit \
    --enable-utf --enable-unicode-properties
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
# disable the stack test
  sed -i '' -e 's/do2=yes/do2=no/' RunTest
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
