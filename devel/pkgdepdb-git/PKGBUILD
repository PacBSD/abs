# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>
pkgname=pkgdepdb-git
pkgver=0.1.9
pkgrel=1
_pkgver=0.1.9
pkgdesc="maintain library dependencies of packages in a database"
license=(BSD)
arch=(i386 amd64)
url=http://github.com/Blub/pkgdepdb
depends=(libarchive pacman gcc-libs)
makedepends=('git' 'libtool' 'python' 'python2')
checkdepends=('check' 'python')
# THREAD=yes + clang have issues
options=(!fbsd10fix)
conflicts=('pkgdepdb')
source=("git://github.com/Blub/${pkgname%-*}"
        pkgdepdb.conf
        absd-fetchdb.sh)

_db_small_url='http://users.pacbsd.org/~repo/repo-report/current/dep.db.gz'
_db_full_url='http://users.pacbsd.org/~repo/repo-report/current/full.db.gz'

build() {
	cd "$srcdir/${pkgname%-*}"
	export CXXFLAGS="${CXXFLAGS} -pthread"
	export LDFLAGS="${LDFLAGS} -pthread"

	sed -i '' -e 's/\-Werror//g' Makefile*
	make \
	    SYSCONFDIR=/etc \
	    ALPM=yes \
	    REGEX=yes \
	    THREADS=yes \
	    WITH_LIBRARY=yes
	sed -e "s@%%SMALL_URL%%@${_db_small_url}@" \
	    -e "s@%%FULL_URL%%@${_db_full_url}@" \
	    "${srcdir}/absd-fetchdb.sh" > "${srcdir}/pacbsd-fetchdb"
}

check() {
	cd "$srcdir/${pkgname%-*}"
	export CXXFLAGS="${CXXFLAGS} -pthread"
	export LDFLAGS="${LDFLAGS} -pthread"
	make \
	    ALPM=yes \
	    REGEX=yes \
	    THREADS=yes \
	    WITH_LIBRARY=yes \
	    check
}

package() {
	cd "${pkgname%-*}"
	make DESTDIR="${pkgdir}" \
		SYSCONFDIR=/etc \
		PREFIX=/usr \
		WITH_LIBRARY=yes \
		install setup.py

	install -dm755 "${pkgdir}/usr/bin"
	install -m755 "${srcdir}/pacbsd-fetchdb" "${pkgdir}/usr/bin/pacbsd-fetchdb"

	install -dm755 "${pkgdir}/etc"
	install -m644 "${srcdir}/pkgdepdb.conf" "${pkgdir}/etc/pkgdepdb.conf"

	msg "installing python2 bindings"
	python2 setup.py install --root="$pkgdir/" --optimize=1
	msg "installing python3 bindings"
	python3 setup.py install --root="$pkgdir/" --optimize=1
}
sha1sums=('SKIP'
          'a64e3027b83468c1f67e69c150a95134c28a3b17'
          '1b315acf9d176b02647a9cd33345ae605ac0a8ee')

