pkgname=linux-perl
_pkgname=perl
pkgver=5.10.1
pkgrel=2
pkgdesc='A highly capable, feature-rich programming language'
url='http://www.perl.org'
license=('GPL')
arch=('any')
options=('!strip' '!fbsd10fix')
groups=('linux-base')
depends=('linux-coreutils' 'linux-db' 'linux-gdbm' 'linux-glibc' 'linux-bash')
source=("http://mirror.lzjtu.edu.cn/archlinux/core/os/i686//${_pkgname}-${pkgver}-5-i686.pkg.tar.gz")
md5sums=('5f53c54360fe10b3635f92394a4a10ba')

prepare() {
	rm -f "${srcdir}"/*.pkg.tar.gz
}

build() {
    find ${srcdir}/bin ${srcdir}/sbin/ ${srcdir}/usr/bin \
         ${srcdir}/usr/sbin \
           -type f -print0 | xargs -0 file \
          | grep ELF | cut -d : -f 1 \
          | xargs brandelf -t linux
}

package() {
    cd ${srcdir}

    find * -type d -exec mkdir -p "${pkgdir}/compat/linux/{}" \;
    find * ! -type d | cpio -pm -R root:wheel ${pkgdir}/compat/linux

}
