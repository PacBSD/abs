# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: Jaroslaw Swierczynski <swiergotΘaur·archlinux·org>

pkgname=skype
pkgver=2.0.0.72
pkgrel=1
arch=('i386' 'amd64')
pkgdesc="P2P software for high-quality voice communication (OSS-compatible version)"
url="http://www.skype.com/"
license=('custom')
# Don't ask me why, but skype is dinamically linked to libasound.so.2,
# so this package really depends on alsa-lib, even if it doesn't use it.
depends=('linux-base' 'linux-fontconfig' 'linux-xorg-libs' 'gcc-libs' 'linux-alsa-lib' 'xdg-utils')
#depends=('fontconfig' 'libxinerama' 'libxi' 'libxcursor' 'libsm' 'libxrandr' 'gcc-libs' 'libxv' 'libxss' 'alsa-lib' 'oss' 'xdg-utils')
provides=("skype=${pkgver}")
#conflicts=('skype' 'skype-staticqt')
install=skype.install
# todo: find a real mirror
source=("http://kmkeen.com/tmp/skype_static-$pkgver-oss.tar.bz2" PERMISSION)
md5sums=('ff41dd31906484863884ee0a7da441c1'
         '26e1772379d4d4df9471b6ed660a6d97')

package() {
  lindir="$pkgdir/compat/linux/"
  cd "$srcdir/skype_static-$pkgver-oss"
 
  install -dm755 "$lindir/usr/share/skype"/{avatars,lang,sounds}
  install -m 644 avatars/* "$lindir/usr/share/skype/avatars"
  install -m 644 lang/* "$lindir/usr/share/skype/lang"
  install -m 644 sounds/* "$lindir/usr/share/skype/sounds"
  
  install -dm755 "$lindir/usr/bin"
  install -m 755 skype "$lindir/usr/bin/skype"

  install -dm755 "$pkgdir/etc/dbus-1/system.d"
  install -m 644 skype.conf "$pkgdir/etc/dbus-1/system.d/skype.conf"
  
  install -dm755 "$lindir/usr/share/icons/hicolor/"{16x16,32x32,48x48}
  install -m 644 icons/SkypeBlue_16x16.png "$lindir/usr/share/icons/hicolor/16x16/skype.png"
  install -m 644 icons/SkypeBlue_32x32.png "$lindir/usr/share/icons/hicolor/32x32/skype.png"
  install -m 644 icons/SkypeBlue_48x48.png "$lindir/usr/share/icons/hicolor/48x48/skype.png"
  
  install -dm755 "$lindir/usr/share/pixmaps"
  install -m 644 icons/SkypeBlue_48x48.png "$lindir/usr/share/pixmaps/skype.png"
 
  install -dm755 "$pkgdir/usr/share/applications"
  install -m 644 skype.desktop "$pkgdir/usr/share/applications/skype.desktop"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m 644 "$srcdir/PERMISSION" "$pkgdir/usr/share/licenses/$pkgname/PERMISSION"

  install -dm755 "$pkgdir/usr/bin"
  cd "$pkgdir/usr/bin"
  ln -s /compat/linux/usr/bin/skype .
}
