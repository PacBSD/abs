# $Id: PKGBUILD 91987 2013-05-30 18:00:35Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgbasename=libxcursor
pkgname=lib32-$_pkgbasename
pkgver=1.1.14
pkgrel=2
pkgdesc="X cursor management library (32-bit)"
arch=('amd64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes' 'lib32-libxrender' $_pkgbasename)
makedepends=('xorg-util-macros' lib32-devel)
options=('!libtool')
source=(${url}/releases/individual/lib/libXcursor-${pkgver}.tar.bz2)
sha256sums=('9bc6acb21ca14da51bda5bc912c8955bc6e5e433f0ab00c5e8bef842596c33df')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/libXcursor-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  gmake
}

package() {
  cd "${srcdir}/libXcursor-${pkgver}"

  gmake DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
