# $Id: PKGBUILD 93026 2013-06-22 11:20:03Z bluewind $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=glib2
pkgname=lib32-$_pkgbasename
pkgver=2.40.0
pkgrel=3
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (32-bit)"
url="http://www.gtk.org/"
arch=('i386' 'amd64')
license=('LGPL')
depends=('lib32-pcre' 'lib32-libffi' 'lib32-dbus' 'lib32-icu>=51.1' 'lib32-gettext' $_pkgbasename 'lib32-libxml2')
makedepends=('gsed' 'python2')
patchdir=("glib-$pkgver")
options=('!docs' 'libtoolfix')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz
        revert-warn-glib-compile-schemas.patch)

prepare() {
  cd "$srcdir/glib-$pkgver"

  sed -i '' -e '46i \
    #include <sys/socket.h>' gio/tests/gdbus-peer.c

  sed -i '' -e 's|inotify_support=yes|inotify_support=no| ; s|#define HAVE_SYS_INOTIFY_H 1||' configure

  patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch

}

build() {
  cd "$srcdir/glib-$pkgver"

  export CC="$CC -m32"
  export CXX="$CXX -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  export LDFLAGS="-m32 ${LDFLAGS}"

  sed -i '' -e 's|-liconv||g' configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libdir=/usr/lib32 --disable-fam \
      --sysconfdir=/etc \
      --with-libiconv=native \
      --disable-gtk-doc --with-html-dir=/usr/share/doc \
      --disable-man --without-xml-catalog \
      --disable-dtrace \
      --with-pcre=system \
      --target=$CHOST \
      PTHREAD_CFLAGS="" \
      PTHREAD_LIBS="-pthread" \
      LDFLAGS="-L/usr/lib32 -lintl" ac_cv_header_sys_inotify_h=

  # glib-genmarshal will need the local library:
  export LD_32_LIBRARY_PATH="${LD_32_LIBRARY_PATH}:${srcdir}/glib-$pkgver/glib/.libs"
  gmake
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{share,include}}

  ## hmm doesn't get installed
  install -dm755 ${pkgdir}/usr/lib32/glib-2.0/include
  install -m644 glib/glibconfig.h ${pkgdir}/usr/lib32/glib-2.0/include/

  cd "${pkgdir}"/usr/bin
  mv gio-querymodules gio-querymodules-32
  rm -f gdbus glib* gobject-query gsettings gtester*
  rm -rf "$pkgdir"/usr/lib32/gdbus-2.0
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules-32 -delete

  rm -f "${pkgdir}/usr/lib32/charset.alias"
}

sha1sums=('44e1442ed4d1bf3fa89138965deb35afc1335a65'
          '0e5c7456ec9a79d22f9a06488cca63a3e838fc02')
