# $Id: PKGBUILD 63998 2012-02-08 18:53:40Z pschmitz $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=libsndfile
pkgname=lib32-$_pkgbasename
pkgver=1.0.25
pkgrel=4
pkgdesc="A C library for reading and writing files containing sampled sound (32-bit)"
arch=('i386' 'amd64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
depends=('lib32-flac' 'lib32-libvorbis' $_pkgbasename)
makedepends=('lib32-alsa-lib' 'lib32-devel')
options=('!pathfix' 'pathfix32')
source=(http://www.mega-nerd.com/libsndfile/files/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('e2b7bb637e01022c7d20f95f9c3990a2')
sha1sums=('e95d9fca57f7ddace9f197071cbcfb92fa16748e')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  export GETCONF="getconf -v POSIX_V7_ILP32_OFFBIG"
  export GETCONF_DIR="/usr/lib32/getconf"

  ./configure --prefix=/usr --disable-sqlite --libdir=/usr/lib32
  make -C src
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make -C src DESTDIR="${pkgdir}" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA

  rm -rf "$pkgdir/usr/include"
}
