# $Id: PKGBUILD 76262 2012-09-15 09:41:41Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

_pkgbasename=sdl
pkgname=lib32-$_pkgbasename
pkgver=1.2.15
pkgrel=7
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (32-bit)"
arch=('i386' 'amd64')
url="http://www.libsdl.org"
license=('LGPL')
depends=('lib32-libxext' 'lib32-libxrender' 'lib32-libx11' $_pkgbasename)
makedepends=('lib32-alsa-lib' 'lib32-mesa' 'lib32-libpulse' lib32-devel)
optdepends=('lib32-alsa-lib: ALSA audio driver'
            'lib32-libpulse: PulseAudio audio driver')
options=('!libtool')
source=(http://www.libsdl.org/release/SDL-${pkgver}.tar.gz
        patch-sdl-config.in
        patch-sdl.pc.in
        patch-src_cdrom_freebsd_SDL_syscdrom.c
        patch-src_joystick_bsd_SDL_sysjoystick.c
)

md5sums=('9d96df8417572a2afb781a7c4c811a85'
         'fa24d6160600e587fb67c01eb6c91e28'
         '349f8349b3ed30b875db12079960fc3e'
         '95daaf7b18a2b197c66e92f8ce81d8f6'
         '6e3aba616d04afd4901e3a97be8430da')

prepare() {
  cd SDL-$pkgver
  for i in patch-sdl-config.in patch-sdl.pc.in \
           patch-src_cdrom_freebsd_SDL_syscdrom.c \
           patch-src_joystick_bsd_SDL_sysjoystick.c
  do
    patch -p0 -i "${srcdir}/$i"
  done
}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd SDL-$pkgver
  ac_cv_header_libusbhid_h=no \
  ./configure --prefix=/usr --disable-alsa --disable-alsatest \
              --with-x --disable-video-directfb \
              --enable-video-opengl \
              --enable-oss \
              --libdir=/usr/lib32 --disable-nasm --disable-rpath
  gmake
}

package() {
  cd SDL-$pkgver
  gmake DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/{include,share}

  cd "$pkgdir/usr/bin"
  mv sdl-config sdl-config-32
}
