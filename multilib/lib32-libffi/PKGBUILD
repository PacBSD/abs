# $Id: PKGBUILD 87137 2013-03-27 22:21:38Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libffi
pkgver=3.0.13
pkgrel=1
pkgdesc="A portable, high level programming interface to various calling conventions (32-bit)"
arch=('x86_64')
license=('MIT')
url="http://sourceware.org/libffi/"
depends=('freebsd-lib32')
checkdepends=('dejagnu')
options=('!libtool')
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
sha1sums=('f5230890dc0be42fb5c58fbf793da253155de106')

build() {
  cd "${srcdir}/libffi-${pkgver}"

  export CC="gcc -m32"
  export CPP=cpp
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
    --disable-static

  make
}

check() {
  cd "${srcdir}/libffi-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/libffi-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -r "${pkgdir}"/usr/share/{info,man}
}
