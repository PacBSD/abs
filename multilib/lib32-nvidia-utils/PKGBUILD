# $Id: PKGBUILD 93241 2013-06-29 10:14:01Z foutrelis $
# Maintainer: Thomas Baechler <thomas@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>

_pkgbasename=nvidia-utils
pkgbase=lib32-$_pkgbasename
pkgname=('lib32-nvidia-utils' 'lib32-nvidia-libgl') # 'lib32-opencl-nvidia')
pkgver=340.32
pkgrel=2
arch=('i386' 'amd64')
url="http://www.nvidia.com/"
license=('custom')
options=('!strip')

_arch='x86'
_pkg="NVIDIA-FreeBSD-${_arch}-${pkgver}"
source=("ftp://download.nvidia.com/XFree86/FreeBSD-${_arch}/${pkgver}/${_pkg}.tar.gz")
sha1sums=('a7d06ad50caeaa96c9eb3e0be118b8307519a6f8')

prepare() {
    cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"
# Don't try compiling the kernel module:
    sed -i '' -e 's/SUBDIR=.*src/SUBDIR=/' Makefile
}

build() {
    cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"
    unset CFLAGS LDFLAGS CPPFLAGS CXXFLAGS

    make -j1
}

package_lib32-opencl-nvidia() {
    pkgdesc="OpenCL implemention for NVIDIA (32-bit)"
    depends=('lib32-libcl' 'lib32-zlib')
    optdepends=('opencl-headers: headers necessary for OpenCL development')
    cd "${srcdir}/${_pkg}/obj"

    # OpenCL
    install -dm755 "${pkgdir}/usr/lib32"
    install -m755 "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so.${pkgver}"
    ln -s "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so.1"
    ln -s "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so"

    install -m755 "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so.${pkgver}" 
    ln -s "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so.1"
    ln -s "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so"
}

package_lib32-nvidia-libgl() {
    pkgdesc="NVIDIA drivers libraries symlinks (32-bit)"
    depends=('lib32-nvidia-utils')
    replaces=('lib32-nvidia-utils<=313.26-1')
    conflicts=('lib32-libgl')
    provides=('lib32-libgl')
    cd "${srcdir}/${_pkg}"

    mkdir -p "${pkgdir}/usr/lib32"
    ln -s "/usr/lib32/nvidia/libGL.so.${pkgver}" "${pkgdir}/usr/lib32/libGL.so.${pkgver}"
    ln -s "libGL.so.${pkgver}" "${pkgdir}/usr/lib32/libGL.so.1"
    ln -s "libGL.so.${pkgver}" "${pkgdir}/usr/lib32/libGL.so"
}

package_lib32-nvidia-utils() {
    pkgdesc="NVIDIA drivers utilities (32-bit)"
    depends=('freebsd-lib32' 'lib32-gcc-libs' 'lib32-libxext' 'lib32-libx11')
    cd "${srcdir}/${_pkg}/obj"

    install -dm755 "${pkgdir}/usr/lib32/"{nvidia,vdpau}
    # OpenGL library
    install -m755 libGL.so.1 "${pkgdir}/usr/lib32/nvidia/libGL.so.${pkgver}"
    # OpenGL core library
    install -m755 libnvidia-glcore.so.1 "${pkgdir}/usr/lib32/libnvidia-glcore.so.${pkgver}"
    # VDPAU
    install -m755 libvdpau_nvidia.so.1 "${pkgdir}/usr/lib32/vdpau/libvdpau_nvidia.so.${pkgver}"
    # nvidia-tls library
    install -m755 libnvidia-tls.so.1 "${pkgdir}/usr/lib32/libnvidia-tls.so.${pkgver}"

    install -m755 libnvidia-cfg.so.1 "${pkgdir}/usr/lib32/libnvidia-cfg.so.${pkgver}"
    #install -m755 libnvidia-ml.so.1 "${pkgdir}/usr/lib32/libnvidia-ml.so.${pkgver}"

    # create soname links
    for _lib in $(find "${pkgdir}" -name '*.so*'); do
        _soname="$(dirname ${_lib})/$(LC_ALL=C readelf -d "$_lib" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
        if [ ! -e "${_soname}" ]; then
            ln -s "$(basename ${_lib})" "${_soname}"
            ln -s "$(basename ${_soname})" "${_soname/.[0-9]*/}"
        fi
    done

    rm -rf "${pkgdir}"/usr/{include,share,bin}
    mkdir -p "${pkgdir}/usr/share/licenses"
    ln -s $_pkgbasename "${pkgdir}/usr/share/licenses/${pkgname}"
}
