# $Id: PKGBUILD 90770 2013-05-13 18:47:37Z lcarlier $
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Maintainer: Biru Ionut <ionut@archlinux.ro>

_pkgbasename=pango
pkgname=lib32-$_pkgbasename
pkgver=1.36.6
pkgrel=2
pkgdesc="A library for layout and rendering of text (32-bit)"
arch=('i386' 'amd64')
license=('LGPL')
makedepends=("lib32-devel" 'lib32-fontconfig' 'lib32-mesa-libgl' 'lib32-cairo' 'lib32-libxft' 'lib32-harfbuzz' $_pkgbasename)
options=('!libtool' '!emptydirs')
install=pango.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver:0:4}/${_pkgbasename}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('4c53c752823723875078b91340f32136aadb99e91c0f6483f024f978a02c8624')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gsed -i 's#"pango.modules"#"pango.modules-32"#' pango/modules.c
  gsed -i '/SUBDIRS = /s@ tests@@' Makefile.in
}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
      --localstatedir=/var --with-included-modules=basic-fc \
      --with-dynamic-modules=arabic-fc,arabic-lang,basic-fc,basic-win32,basic-x,basic-atsui,hangul-fc,hebrew-fc,indic-fc,indic-lang,khmer-fc,syriac-fc,tibetan-fc \
      --disable-introspection

  export LD_32_LIBRARY_PATH="$srcdir/${_pkgbasename}-${pkgver}/pango/.libs:${LD_32_LIBRARY_PATH}"
  gmake
}

package() {
  depends=('lib32-cairo' 'lib32-libxft' 'lib32-harfbuzz' $_pkgbasename)
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir"/etc
  rm -rf "$pkgdir"/usr/{bin/pango-view,share,include}
  mv "$pkgdir"/usr/bin/pango-querymodules "$pkgdir"/usr/bin/pango-querymodules-32
}
