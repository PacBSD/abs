# $Id: PKGBUILD 110354 2014-04-28 13:14:57Z lcarlier $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

_pkgbasename=lcms2
pkgname=lib32-lcms2
pkgver=2.6
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2 (32-bit)"
arch=('i386' 'amd64')
license=('MIT')
depends=('lib32-libtiff' ${_pkgbasename})
url="http://www.littlecms.com"
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/lcms/${_pkgbasename}-${pkgver}.tar.gz)
sha1sums=('b0ecee5cb8391338e6c281d1c11dcae2bc22a5d2')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake
}

package() {
  cd ${_pkgbasename}-${pkgver}

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
