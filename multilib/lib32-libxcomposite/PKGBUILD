# $Id: PKGBUILD 82049 2013-01-06 10:57:49Z bluewind $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxcomposite
pkgname=lib32-$_pkgbasename
pkgver=0.4.4
pkgrel=2
pkgdesc="X11 Composite extension library (32-bit)"
arch=('amd64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes' $_pkgbasename)
makedepends=('xorg-util-macros' lib32-devel)
options=(!libtool)
source=(${url}/releases/individual/lib/libXcomposite-${pkgver}.tar.bz2)
sha256sums=('ede250cd207d8bee4a338265c3007d7a68d5aca791b6ac41af18e9a2aeb34178')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/libXcomposite-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  gmake
}

package() {
  cd "${srcdir}/libXcomposite-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
