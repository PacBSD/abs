# $Id: PKGBUILD 89997 2013-05-06 19:35:42Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

_pkgbasename=libpng
pkgname=lib32-$_pkgbasename
pkgver=1.6.12
_apngver=1.6.12
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files (32-bit)"
arch=('i386' 'amd64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=($_pkgbasename)
makedepends=(lib32-devel)
options=('libtoolfix' '!libtool')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz"
        patch-ac
        patch-makefile.freebsd)
md5sums=('ee752da8aa75076500e4cf31318e636e'
         'f959e8b1cb9876ce946a2701e8aca519'
         '8bafc2a81bf6c588727cec782aaefe85'
         'cf54ad12ef74d5d4bf01d61a40cccb06')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i ../libpng-$_apngver-apng.patch

  patch -p0 -i "${srcdir}/patch-ac"
  patch -p0 -i "${srcdir}/patch-makefile.freebsd"

  ./configure --prefix=/usr --libdir=/usr/lib32 --program-suffix=-32
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  cd contrib/pngminus
  gmake PNGLIB="-L${pkgdir}/usr/lib32 -lpng" -f makefile.std png2pnm pnm2png

  rm -rf "${pkgdir}"/usr/{include,share}

  rm "$pkgdir/usr/bin/libpng-config"
  ln -s "libpng${_libversion}-config-32" "$pkgdir/usr/bin/libpng-config-32"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
