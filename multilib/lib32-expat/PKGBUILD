_pkgbasename=expat
pkgname=lib32-${_pkgbasename}
pkgver=2.1.0
pkgrel=6
pkgdesc="An XML Parser library written in C (32 bit)"
arch=('amd64')
url="http://expat.sourceforge.net/"
license=('custom')
makedepends=('lib32-gcc-libs')
depends=('freebsd-lib32' "${_pkgbasename}")
options=('!libtool' '!pathfix' 'pathfix32')
source=(http://downloads.sourceforge.net/sourceforge/expat/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('dd7dab7a5fea97d2a6a43f511449b7cd')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  export CC='clang -m32'
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --mandir=/usr/share/man 
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share/man}
}
