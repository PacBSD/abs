# $Id: PKGBUILD 95008 2013-08-03 09:04:03Z lcarlier $
# Maintainer: Florian Pritz <flo@xinu.at>

_pkgbasename=glew
pkgname=lib32-$_pkgbasename
pkgver=1.9.0
pkgrel=3
pkgdesc="A cross-platform C/C++ extension loading library (32 bit)"
arch=('i386' 'amd64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('lib32-libxmu' 'lib32-libxi' 'lib32-glu' "$_pkgbasename>=$pkgver")
makedepends=('lib32-devel')
port_patches=(
	patch-Makefile
	patch-config_Makefile.freebsd
)
source=(http://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tgz
	glxew_export_fix.diff
	${port_patches[@]})
sha1sums=('9291f5c5afefd482c7f3e91ffb3cd4716c6c9ffe'
          '72761588d27797566e101fceebfbb412e1a4cc93'
          '303f19cdfc88a2386e5c958a8e8969951e2c8806'
          'cabad07de2dec017d75be8633032cb163e21b6e5')

prepare() {
	cd ${srcdir}/${_pkgbasename}-${pkgver}

	patch -Np1 -i ${srcdir}/glxew_export_fix.diff
	for i in ${port_patches[@]}; do
		msg "Applying patch ${i}"
		patch -p0 -i "${srcdir}/${i}"
	done
}

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
	export CC="clang -m32"
	export CXX="clang++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  gsed -i 's|CC = cc|CC = clang -m32|' config/Makefile.freebsd

  gmake 
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake GLEW_DEST="${pkgdir}/usr" install
	install -dm755 "${pkgdir}"/usr/lib32

	mv "${pkgdir}"/usr/lib/* "${pkgdir}"/usr/lib32/
  chmod 0755 "${pkgdir}/usr/lib32/libGLEW.so.${pkgver}"
	chmod 0755 "${pkgdir}/usr/lib32/libGLEW.so.1"

  rm -rf "${pkgdir}"/usr/{include,bin,lib}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
