# $Id: PKGBUILD 87074 2013-03-26 17:44:36Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Xavius <xavierelf@gmail.com>

pkgname=lib32-portaudio
pkgver=19_20111121
pkgrel=3
pkgdesc='A free, cross-platform, open source, audio I/O library. (32 bit)'
arch=('i386' 'amd64')
url="http://www.portaudio.com/"
license=('custom')
depends=('lib32-alsa-lib' 'lib32-jack' "${pkgname#*-}")
makedepends=('lib32-devel')
options=('!libtool')
source=("http://www.portaudio.com/archives/pa_stable_v${pkgver}.tgz")
sha256sums=('9c26d1330d506496789edafe55b0386f20d83c4aa2c0e3f81fbeb0f114ab1b99')

build() {
  cd "${srcdir}"/${pkgname#*-}

# Multilib
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

# Build
  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake
}

package () {
  cd "${srcdir}"/${pkgname#*-}

# Install
  gmake DESTDIR="${pkgdir}" install

# Multilib
  rm -rf "${pkgdir}"/usr/include

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
