# $Id: PKGBUILD 92101 2013-05-31 14:48:14Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgbasename=libxinerama
pkgname=lib32-$_pkgbasename
pkgver=1.1.3
pkgrel=1
pkgdesc="X11 Xinerama extension library (32-bit)"
arch=('amd64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxext' $_pkgbasename)
makedepends=('xorg-util-macros' lib32-devel)
options=(!libtool)
source=(${url}/releases/individual/lib/libXinerama-${pkgver}.tar.bz2)
sha1sums=('6a3956d73f7457ef0b4db12806d99c6918e328e3')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/libXinerama-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake
}

package() {
  cd "${srcdir}/libXinerama-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
