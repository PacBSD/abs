# $Id: PKGBUILD 81098 2012-12-11 21:58:53Z heftig $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

## UNINSTALL: portaudio, lib32-portaudio, and ffmpeg before building this

_pkgbasename=openal
pkgname=lib32-$_pkgbasename
pkgver=1.15.1
pkgrel=2
pkgdesc="OpenAL audio library for use with opengl (32-bit)"
arch=('i386' 'amd64')
url="http://www.openal.org/"
license=('LGPL')
depends=('freebsd-lib32' $_pkgbasename)
makedepends=('lib32-alsa-lib' 'lib32-sdl' 'lib32-libpulse' 'pkgconf' 'cmake' lib32-devel)
options=('!libtool')
patchdir=("${_pkgbasename}-soft-${pkgver}")
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-${pkgver}.tar.bz2)
md5sums=('ea83dec3b9655a27d28e7bc7cae9cd71')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${srcdir}/${_pkgbasename}-soft-${pkgver}/build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -D LIB_SUFFIX=32 ..
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-soft-${pkgver}/build
  make DESTDIR=${pkgdir}/ install 

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
