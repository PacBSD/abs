pkgname=lib32-libdevq
_pkgbasename=libdevq
pkgver=0.0.2
pkgrel=3
pkgdesc="Generic Device Query and Monitor interface"
arch=('i686' 'amd64')
url="http://www.FreeBSD.org"
license=('BSD')
depends=('freebsd-lib32' ${_pkgbasename})
makedepends=('lib32-devel')
options=('!pathfix' 'pathfix32')
source=(http://distcache.FreeBSD.org/ports-distfiles/${_pkgbasename}-${pkgver}.tar.xz)
sha1sums=('a7a06d4cc3e8de2e8015fb3f78cd596ede6e87ca')

build() {
  cd ${_pkgbasename}-${pkgver}

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --libdir=/usr/lib32

  make
}

package() {
  cd "${_pkgbasename}-${pkgver}"

  make DESTDIR=${pkgdir} install
  cp ${pkgdir}/usr/libdata32/pkgconfig/libdevq-1.0.pc ${pkgdir}/usr/libdata32/pkgconfig/libdevq.pc 
  rm -r ${pkgdir}/usr/include
}
