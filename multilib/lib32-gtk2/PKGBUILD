# $Id: PKGBUILD 93625 2013-07-07 16:14:57Z bluewind $
# Maintainer:  Ionut Biru <ibiru@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

#
# NOTE:
#   -j unsafe - run makepkg -e after the error if you don't want
#   to -j1 thoug the whole thing
#

_pkgbasename=gtk2
pkgname=lib32-$_pkgbasename
pkgver=2.24.24
pkgrel=1
pkgdesc="The GTK+ Toolkit (v2) (32-bit)"
arch=('i386' 'amd64')
url="http://www.gtk.org/"
install=gtk2.install
depends=(lib32-{'atk>=1.30.0','pango>=1.28.0','cairo>=1.10.0','gdk-pixbuf2>=2.28.2-4'}
         lib32-lib{xcursor,'xrandr>=1.3','xi>=1.3',xinerama,xcomposite,xdamage}
         $_pkgbasename)
makedepends=('pkgconf' lib32-devel)
options=('!libtool' '!docs' 'libtoolfix')
license=('LGPL')
_port_patches=(
	patch-Makefile.in
	patch-gtk_gtkbuilderparser.c
	patch-gtk_updateiconcache.c
)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-$pkgver.tar.xz
        xid-collision-debug.patch
        gdk-thread-hack.diff
        "${_port_patches[@]}"
)
sha256sums=('12ceb2e198c82bfb93eb36348b6e9293c8fdcd60786763d04cfec7ebe7ed3d6d'
            '88a5f572a78f3f2095e8ad3fca5003103f71b25c8071fd0261ade8c431aaa248'
            'ba22671269e3d20dc3c0013ee1ac36bbb1cde890e4597ee4a60badc101b7dced'
            '3a090e37a543b71ee7fd37fffbbaeadd212dd0ad9f84ad4f8788488c14dfa2d9'
            '7a2089acc9553f48c73a18d31bd302b82a65b06d7a0a61f72077961ddb4b33ee'
            '5e082f9e0109a1a63e713d1997d277251a3f7760efedccd4c9253e5e9aaae8a9')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"

  patch -Np1 -i ../xid-collision-debug.patch
  for i in "${_port_patches[@]}"; do
    patch -p0 -i "${srcdir}/$i"
  done
#  patch -p0 -i ${srcdir}/gdk-thread-hack.diff

  # so the tests can run:
  gsed -i 's/shlibpath_var=.*/shlibpath_var=LD_32_LIBRARY_PATH/g' configure

}

build() {
  export CC="clang -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  export LDFLAGS="-L/usr/lib32 ${LDFLAGS}"
  export LIBS="$LIBS -lintl"

  cd "${srcdir}/gtk+-${pkgver}"

  CXX=/bin/false ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libdir=/usr/lib32 \
      --with-xinput=yes \
      --disable-cups

  # we'll have to be persistent about this -lintl thing
  gsed -i '/^LIBS = /s@$@ -lintl@' gtk/Makefile

  ## it wants to run some of its executables...
  #export LD_32_LIBRARY_PATH="$srcdir/gtk+-${pkgver}/gdk/.libs:${LD_32_LIBRARY_PATH}"
  #export LD_32_LIBRARY_PATH="$srcdir/gtk+-${pkgver}/gtk/.libs:${LD_32_LIBRARY_PATH}"

  gmake
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}"/usr/bin
  mv gtk-query-immodules-2.0 gtk-query-immodules-2.0-32
  rm -f gtk-builder-convert gtk-demo gtk-update-icon-cache
}
