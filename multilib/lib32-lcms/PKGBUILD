# $Id: PKGBUILD 63984 2012-02-08 18:50:52Z pschmitz $
# Maintainer: Tobias Kieslich <neri@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

_pkgbasename=lcms
pkgname=lib32-$_pkgbasename
pkgver=1.19
pkgrel=5
pkgdesc="Lightweight color management development library/engine (32-bit)"
arch=('amd64')
license=('custom')
depends=('lib32-libtiff>=3.9.4' $_pkgbasename)
makedepends=(lib32-devel)
url="http://www.littlecms.com"
options=('!libtool')
patchdir=("${_pkgbasename}-${pkgver}")
source=(http://downloads.sourceforge.net/sourceforge/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('8af94611baf20d9646c7c2c285859818')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${srcdir}/${_pkgbasename}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  gmake DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
