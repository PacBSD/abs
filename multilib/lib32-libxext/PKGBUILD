# $Id: PKGBUILD 92134 2013-06-01 10:22:23Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxext
pkgname=lib32-$_pkgbasename
pkgver=1.3.3
pkgrel=1
pkgdesc="X11 miscellaneous extensions library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11' $_pkgbasename)
makedepends=('xorg-util-macros' 'lib32-devel')
options=('!libtool')
source=(${url}/releases/individual/lib/libXext-${pkgver}.tar.bz2)
sha1sums=('43abab84101159563e68d9923353cc0b3af44f07')

build() {
  cd "${srcdir}/libXext-${pkgver}"

  export CC="clang -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32 \
              --disable-specs --without-xmlto
  gmake
}

package() {
  cd "${srcdir}/libXext-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
