pkgname=apache
pkgver=2.4.17
pkgrel=2
pkgdesc='A high performance Unix-based HTTP server'
arch=('i386' 'amd64')
url='http://www.apache.org/dist/httpd'
license=('APACHE')
backup=(etc/conf.d/httpd
        etc/httpd/conf/httpd.conf
        etc/httpd/conf/extra/httpd-{autoindex,dav,default,info,languages}.conf
        etc/httpd/conf/extra/httpd-{manual,mpm,multilang-errordoc}.conf
        etc/httpd/conf/extra/httpd-{ssl,userdir,vhosts}.conf
        etc/logrotate.d/httpd)
depends=('apr-util' 'pcre')
makedepends=('libxml2' 'lua')
optdepends=(
    'lua: for mod_lua module'
    'libxml2: for mod_proxy_html, mod_xml2enc modules' 
    'lynx: apachectl status'
)

source=(http://archive.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2{,.asc}
        apache.conf.d
        httpd.logrotate
        arch.layout
        httpd
        freebsd-init-apache)
md5sums=('cf4dfee11132cde836022f196611a8b7'
         'SKIP'
         '08b3c875f6260644f2f52b4056d656b0'
         '6382331e9700ed9e8cc78ea51887b537'
         '34c0b64e2bb3566b9223b6e135cd0115'
         '2218a71b7a10000c814a97c4fd5f72d6'
         '371e37c3723ec89310ee33f5174a1eae')

prepare() {
  cd "${srcdir}/httpd-${pkgver}"
  gsed -i -e 's/srclib//' Makefile.in

  sed -i '' -e 's| cp -rp| cp -r|g' Makefile.in
}

build() {
  cd httpd-${pkgver}

  # set default user
  gsed -e 's#User daemon#User www#' \
      -e 's#Group daemon#Group www#' \
      -i docs/conf/httpd.conf.in

  cat "${srcdir}/arch.layout" >> config.layout
	
  ./configure --sbindir=/usr/bin \
      --enable-layout=Arch \
      --enable-mpms-shared=all \
      --enable-modules=all \
      --enable-mods-shared=all \
      --enable-so \
      --enable-suexec \
      --with-suexec-caller=www \
      --with-suexec-docroot=/srv/http \
      --with-suexec-logfile=/var/log/httpd/suexec.log \
      --with-suexec-bin=/usr/bin/suexec \
      --with-suexec-uidmin=80 --with-suexec-gidmin=80 \
      --enable-ldap --enable-authnz-ldap --enable-authnz-fcgi \
      --enable-cache --enable-disk-cache --enable-mem-cache --enable-file-cache \
      --enable-ssl --with-ssl \
      --enable-deflate --enable-cgi --enable-cgid \
      --enable-proxy --enable-proxy-connect \
      --enable-proxy-http --enable-proxy-ftp \
      --enable-dbd --enable-imagemap --enable-ident --enable-cern-meta \
      --enable-lua --enable-xml2enc \
      --with-apr=/usr/bin/apr-1-config \
      --with-apr-util=/usr/bin/apu-1-config \
      --with-pcre=/usr

  make
}

package() {
  cd httpd-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/etc/logrotate.d"
  install -m644 "${srcdir}/httpd.logrotate" "${pkgdir}/etc/logrotate.d/httpd"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}"/etc/{rc.d,init.d,conf.d}
  install -m755 "${srcdir}/freebsd-init-apache" "${pkgdir}/etc/rc.d/httpd"
  install -m755 "${srcdir}/httpd" "${pkgdir}/etc/init.d/httpd"
  install -m644 "${srcdir}/apache.conf.d" "${pkgdir}/etc/conf.d/httpd"

  # symlinks for /etc/httpd
  # do we really need these symlinks?
  #ln -fs /var/log/httpd "${pkgdir}/etc/httpd/logs"
  ln -fs /usr/lib/httpd/modules "${pkgdir}/etc/httpd/modules"
  #ln -fs /usr/lib/httpd/build "${pkgdir}/etc/httpd/build"

  # set sane defaults
  gsed -e 's#/usr/libexec/httpd/modules/#modules/#' \
      -e 's|#\(LoadModule negotiation_module \)|\1|' \
      -e 's|#\(LoadModule include_module \)|\1|' \
      -e 's|#\(LoadModule userdir_module \)|\1|' \
      -e 's|#\(LoadModule slotmem_shm_module \)|\1|' \
      -e 's|#\(Include conf/extra/httpd-multilang-errordoc.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-autoindex.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-languages.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-userdir.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-default.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-mpm.conf\)|\1|' \
      -i "${pkgdir}/etc/httpd/conf/httpd.conf"

  # cleanup
  rm -r "${pkgdir}/usr/share/httpd/manual"
  rm -r "${pkgdir}/etc/httpd/conf/original"
  rm -r "${pkgdir}/srv/"
  rm -r "${pkgdir}/var/run"
}

