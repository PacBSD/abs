# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=cups-filters
pkgver=1.0.75
pkgrel=1
pkgdesc="OpenPrinting CUPS Filters"
arch=('i686' 'amd64')
url="http://www.linuxfoundation.org/collaborate/workgroups/openprinting"
license=('GPL')
depends=('lcms2' 'poppler' 'qpdf')
makedepends=('ghostscript' 'ttf-dejavu' 'python2') # ttf-dejavu for make check
optdepends=('ghostscript: for non-PostScript printers to print with CUPS to convert PostScript to raster images'
	    'foomatic-db: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-engine: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-nonfree: drivers use Ghostscript to convert PostScript to a printable form directly')
backup=(etc/fonts/conf.d/99pdftoopvp.conf
        etc/cups/cups-browsed.conf)
source=(http://www.openprinting.org/download/cups-filters/$pkgname-$pkgver.tar.gz
	cups_browsed.in)
provides=('foomatic-filters')
replaces=('foomatic-filters')
conflicts=('foomatic-filters')
md5sums=('240d05fb24d64893b4e20d5da7ad7f46'
         '2ac63e77ef0c6feed8a81a69ffe0b567')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '' -e 's:-std=c++0x:-std=c++11:' Makefile.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ZLIB_CFLAGS="-I/usr/include"  ZLIB_LIBS="-lz" \
  LIBQPDF_CFLAGS="`pkg-config --cflags libqpdf`" \
  LIBQPDF_LIBS="`pkg-config --libs libqpdf`" \
  ./configure --prefix=/usr  \
    --libexecdir=/usr/libexec \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-avahi \
    --with-browseremoteprotocols=DNSSD,CUPS \
    --with-test-font-path=/usr/share/fonts/TTF/DejaVuSans.ttf

  sed -i '' -e 's/^\(DLOPEN_LIBS =\).*$/\1/' Makefile
  gmake -j1
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake -j1 -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir/" install

  install -dm755 "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/cups_browsed.in" "${pkgdir}/etc/rc.d/cups_browsed"
}
