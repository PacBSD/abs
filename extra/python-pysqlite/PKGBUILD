# $Id: PKGBUILD 169030 2012-10-17 10:21:00Z allan $
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=python-pysqlite
pkgname=python2-pysqlite
pkgver=2.6.3
pkgrel=4
pkgdesc="A Python DB-API 2.0 interface for the SQLite embedded relational database engine"
license=('custom')
arch=('i386' 'amd64')
url="http://code.google.com/p/pysqlite/"
depends=('python2' 'sqlite')
conflicts=('python-pysqlite<=2.6.3-2')
replaces=('python-pysqlite<=2.6.3-2')
source=(https://pypi.python.org/packages/source/p/pysqlite/pysqlite-${pkgver}.tar.gz \
        setup.cfg)
md5sums=('7ff1cedee74646b50117acff87aa1cfa'
         '86dd356c65afd14a22f2f8f64a26441e')

check() {
    cd "${srcdir}"/pysqlite-${pkgver}/build/lib.*/
    python2 -c "from pysqlite2 import test; test.test()"
}

build() {
	cd "${srcdir}/pysqlite-${pkgver}"
	cp "${srcdir}/setup.cfg" .
	python2 setup.py build
}

package_python2-pysqlite() {
	cd "${srcdir}/pysqlite-${pkgver}"
	python2 setup.py install --root="${pkgdir}"
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	rm -r "${pkgdir}/usr/pysqlite2-doc"
}
