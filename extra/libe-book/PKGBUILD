# $Id: PKGBUILD 235950 2015-04-09 18:55:30Z heftig $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=libe-book
pkgver=0.1.2
pkgrel=2
pkgdesc="Library for import of reflowable e-book formats."
arch=('i386' 'amd64')
url="http://sourceforge.net/projects/libebook/"
license=('LGPL2.1' 'MPL')
depends=('librevenge' 'libxml2')
makedepends=('gperf' 'boost' 'cppunit' 'doxygen')
source=(https://sourceforge.net/projects/${pkgname//-/}/files//${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha1sums=('950ee08e7a41585636aebb7774fdc6f05817a18c')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-werror
  gmake
}

check() {
  cd ${pkgname}-${pkgver}
  gmake check
}

package() {
  cd ${pkgname}-${pkgver}
  gmake DESTDIR=$pkgdir install
}

