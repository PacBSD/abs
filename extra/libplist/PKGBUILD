# $Id: PKGBUILD 183073 2013-04-17 10:22:07Z tomegun $
# Maintainer : Tom Gundersen <teg@jklm.no>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libplist
pkgver=1.10
pkgrel=3
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('glib2' 'libxml2' 'python2')
makedepends=('cmake' 'swig')
source=(http://www.libimobiledevice.org/downloads/$pkgname-$pkgver.tar.bz2
	'patch-src__Dictionary.cpp')

prepare() {
	cd ${srcdir}/$pkgname-$pkgver
	patch -p0 -i ${srcdir}/patch-src__Dictionary.cpp
}
build() {
  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
md5sums=('fe642d0c8602d70c408994555c330dd1'
         '02b70e7e54d42932a6e55e844e357e2b')
