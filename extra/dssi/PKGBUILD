# $Id: PKGBUILD 205865 2014-02-12 07:12:16Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Pajaro

pkgname=dssi
pkgver=1.1.1
pkgrel=1
pkgdesc="An API for audio processing plugins & softsynths with UIs"
arch=('i386' 'amd64')
url="http://dssi.sourceforge.net/"
license=('LGPL' 'BSD' 'custom')
depends=('liblo>=0.28' 'jack')
makedepends=('ladspa' 'qt4')
optdepends=('qt4: example plugins')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'license.txt')
md5sums=('619ab73c883b02dc37ddb37001591f8b'
         '258ca4d0ca28c5c4d4675147621fac18')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '' -e \
		's|-ldl||' jack-dssi-host/Makefile.in examples/Makefile.in
  sed -i '' -e \
		'/alsa >=/s|$$PKG_CONFIG|${FALSE}|' configure

}
build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$srcdir/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
