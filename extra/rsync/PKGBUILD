# Contributor: Wolfgang Bumiller <blub at speed dot at>

pkgname=rsync
pkgver=3.1.1
pkgrel=3
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i386' 'amd64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('perl' 'gnu-libiconv')
backup=('etc/rsyncd.conf')
source=("http://rsync.samba.org/ftp/rsync/$pkgname-$pkgver.tar.gz"
        'rsyncd.conf' 'rsyncd.in')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	sed -i '' -e 's|CFLAGS|CONFIGUREDCFLAGS|' \
	          -e 's|perl.*||' \
	          Makefile.in

	sed -i '' -e 's|malloc.h|stdlib.h|g' rsync.h zlib/zutil.h

	./configure --prefix=/usr \
	            --disable-debug --enable-ipv6 \
	            --with-rsyncd-conf=/etc/rsyncd.conf \
	            --with-included-popt
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make test
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -dm755 "${pkgdir}/etc/rc.d"

	install -m755 ../rsyncd.in      "$pkgdir/etc/rc.d/rsyncd"

	install -m644 ../rsyncd.conf "$pkgdir/etc/rsyncd.conf"
	install -dm755 "$pkgdir/usr/lib/rsync"
	install -m755 support/rrsync "$pkgdir/usr/lib/rsync/rrsync"
}
md5sums=('43bd6676f0b404326eee2d63be3cdcfe'
         '14bc303db10da09425e165661a47d7c3'
         'aaa6dcc00ebcb7a03d712a9498e63629')

