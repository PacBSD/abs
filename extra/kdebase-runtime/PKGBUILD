# $Id: PKGBUILD 229774 2015-01-24 10:41:10Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=14.12.2
pkgrel=2
pkgdesc="Plugins and applications necessary for the running of KDE applications"
arch=('i686' 'amd64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('GPL' 'LGPL')
depends=("kdelibs" 'kactivities' 'libkactivities4' 'smbclient' 'libcanberra'
         'oxygen-icons' 'xorg-xauth' 'libwebp')
makedepends=('pkg-config' 'cmake' 'automoc4' 'kdepimlibs' 'openslp' 'doxygen'
             'boost')
optdepends=('kdepimlibs: needed by DrKonqi to send crash reports to KDE.org'
            'gdb: needed by DrKonqi to generate backtrace'
            'htdig: to build the search index in the KHelpCenter'
            'rarian: needed by KHelpCenter')
provides=('khelpcenter' 'notification-daemon')
patchdir=(kde-runtime-${pkgver})
install="${pkgname}.install"
source=("http://download.kde.org/stable/applications/${pkgver}/src/kde-runtime-${pkgver}.tar.xz")
sha1sums=('96811840cb04a879109739f3df4f8075fb876ea7')

prepare() {
  mkdir build

  cd kde-runtime-${pkgver}
}

build() {
  cd build
  cmake ../kde-runtime-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_QNtrack=OFF \
    -DSAMBA_INCLUDE_DIR=/usr/include/samba-4.0 \
    -DWITH_NepomukCore=OFF
  gmake
}

package() {
  cd build
  gmake DESTDIR="$pkgdir" install

  rm "${pkgdir}"/usr/share/icons/hicolor/index.theme

  ln -sf /usr/lib/kde4/libexec/kdesu "${pkgdir}/usr/bin/"

  # FS#36668
  chown :nobody "${pkgdir}"/usr/lib/kde4/libexec/kdesud
  chmod g+s "${pkgdir}"/usr/lib/kde4/libexec/kdesud
}
