# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgnomecanvas
pkgver=2.30.3
pkgrel=5
pkgdesc="The GNOME Canvas library"
arch=('i386' 'amd64')
license=('LGPL')
depends=('libglade>=2.6.4' 'libart-lgpl>=2.3.21')
makedepends=('pkgconf' 'intltool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('859b78e08489fce4d5c15c676fec1cd79782f115f516e8ad8bed6abcb8dedd40')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static \
      --enable-glade
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
