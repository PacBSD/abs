# $Id: PKGBUILD 228206 2014-12-31 02:15:20Z fyan $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=python-characteristic
pkgname=('python-characteristic' 'python2-characteristic')
pkgver=14.3.0
pkgrel=4
pkgdesc="Service identity verification for pyOpenSSL"
arch=('i386' 'amd64')
license=('MIT')
url="http://pypi.python.org/pypi/characteristic"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/c/characteristic/characteristic-${pkgver}.tar.gz")
md5sums=('b249368dd021fde1c06b4802867c0913')

prepare() {
   cp -a characteristic-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd characteristic-${pkgver}
   python3 setup.py build

   # Build python 2 module
   cd ../characteristic-${pkgver}-python2
   python2 setup.py build
}

package_python-characteristic() {
   depends=('python')

   cd characteristic-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
   install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-characteristic() {
   depends=('python2')

   cd characteristic-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
   install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
