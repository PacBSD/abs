# $Id: PKGBUILD 197854 2013-10-30 11:16:38Z allan $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=alsaplayer
pkgver=0.99.81
pkgrel=13
pkgdesc="A heavily multi-threaded PCM player that tries to excercise the ALSA library and driver quite a bit"
arch=('i386' 'amd64')
url="http://alsaplayer.sourceforge.net/"
license=('GPL')
depends=('gcc-libs' 'desktop-file-utils')
makedepends=('gtk2' 'libid3tag' 'libvorbis' 'libmad' 'libmikmod' 'jack' 'mesa' 'audiofile' 'doxygen')
optdepends=('gtk2: for the gtk2 GUI interface' \
            'libgl: for the OpenGL visual plugins' \
            'jack: for JACK audio server output support' \
            'audiofile: for support of various audio formats like AIFF, WAVE, .snd/.au' \
            'libid3tag: for flac support' \
            'flac: for flac support' \
            'libmad: for MPEG support' \
            'libsndfile: for sndfile support' \
            'libvorbis: for ogg vorbis support' \
            'libmikmod: for mod, s3m, it and xm formats supports')
install=alsaplayer.install
source=(http://alsaplayer.sourceforge.net/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('25817ebd1e5c36da31a88363315a1bf7a945ddfd')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  find . -name "*.c" | xargs sed -i '' -e '/alloca.h/d'

  sed -i ''  -e '/LIBS/s/-ldl//g; /LIBRT/s/-lrt//g;' configure
  sed -i '' -e 's|-ldl||g' app/Makefile.in

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	  -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
	  ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

  chmod +x install-sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-audiofile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
}
