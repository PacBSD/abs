# $Id: PKGBUILD 184423 2013-05-06 19:39:15Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=python-pygame
pkgname=python2-pygame
pkgver=1.9.1
pkgrel=8
pkgdesc="Python game library"
arch=('i686' 'x86_64')
url="http://www.pygame.org/"
license=('LGPL')
depends=('sdl_image' 'python2')
optdepends=('sdl_mixer: sound support' 'portmidi: midi-sound support' 'sdl_ttf: truetype font support')
conflicts=('python-pygame')
source=(http://pygame.org/ftp/pygame-${pkgver}release.tar.gz \
        config.patch
        pygame-v4l.patch
        patch-camera.h
        patch-setup.py)
sha1sums=('a45aeb0623e36ae7a1707b5f41ee6274f72ca4fa'
          '285815e28705d5a2aea53c9d952d35fddf10dd13'
          '7e693fb2ef5ef636f9965ba1a4eb854f8b0b5070'
          'SKIP'
          'SKIP')

prepare() {
  cd pygame-${pkgver}release
  find . -type f -exec sed -i 's#/usr/bin/env python#/usr/bin/env python2#' {} +
  patch -p0 -i "${srcdir}/config.patch"
  patch -p1 -i "${srcdir}/pygame-v4l.patch"
  patch -p0 -i "${srcdir}/patch-camera.h"
  patch -p0 -i "${srcdir}/patch-setup.py"
}

package_python2-pygame() {
  cd pygame-${pkgver}release
  python2 config.py -auto
  python2 setup.py install --root="${pkgdir}" --prefix=/usr

# Copying the examples and tests
  cp -R examples lib/* "${pkgdir}/usr/lib/python2.7/site-packages/pygame"
  cp -R test/* "${pkgdir}/usr/lib/python2.7/site-packages/pygame/tests"

# Fixing permissions
  chmod 644 "${pkgdir}"/usr/include/python2.7/pygame/*
}
