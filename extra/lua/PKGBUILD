# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=lua
pkgver=5.3.1
pkgrel=1
pkgdesc='A powerful light-weight programming language designed for extending applications'
arch=('i686' 'amd64')
url='http://www.lua.org/'
license=('MIT')
options=('!makeflags' '!emptydirs')
source=("http://www.lua.org/ftp/$pkgname-$pkgver.tar.gz"
        'liblua.so.patch' 'lua.pc' 'LICENSE')
md5sums=('797adacada8d85761c079390ff1d9961'
         '85fe4e5f780376457eba3bc23d2cb2d3'
         'e7ba6c2b695b0b84a5ea0cbff5fc9067'
         '0e2bd67b909b9ff673da844ca3480df2')

prepare() {
  cd lua-$pkgver
  patch -p1 -i ../liblua.so.patch

  sed "s/%VER%/${pkgver%.*}/g;s/%REL%/$pkgver/g" ../lua.pc > lua.pc
  sed -i '' -e 's|CC= gcc|CC?= gcc|g' src/Makefile
}

build() {
  cd $pkgname-$pkgver

  [[ $CARCH == x86_64 ]] && export CFLAGS="$CFLAGS -fPIC"
  sed -i '' -e 's|-ldl ||g' src/Makefile

  export LUA_SONAME="liblua-5.2.so.1"
  make MYCFLAGS="$CFLAGS -fPIC -DLUA_COMPAT_5_2 -DLUA_COMPAT_5_1" MYLDFLAGS="$LDFLAGS" freebsd
}

package() {
  cd $pkgname-$pkgver
  gmake \
    TO_LIB="liblua.a liblua.so liblua.so.5.3 liblua.so.$pkgver" \
    INSTALL_TOP="$pkgdir/usr" \
    INSTALL_MAN="$pkgdir/usr/share/man/man1" \
    install

  install -dm755 "$pkgdir/usr/libdata/pkgconfig"
  install -m644 lua.pc "$pkgdir/usr/libdata/pkgconfig/lua.pc"

  # Install the documentation
  install -d "$pkgdir/usr/share/doc/lua"
  install -m644 doc/*.{gif,png,css,html} "$pkgdir/usr//share/doc/lua"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=4 sw=4 et:
