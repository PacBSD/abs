# Port: lang/lua53

pkgname=lua
pkgver=5.3.3
pkgrel=1
pkgdesc='A powerful light-weight programming language designed for extending applications'
arch=('i386' 'amd64')
url='http://www.lua.org/'
license=('MIT')
options=('!makeflags' '!emptydirs')
source=("http://www.lua.org/ftp/$pkgname-$pkgver.tar.gz"
        'lua.pc' 'LICENSE' liblua.so.patch)
sha256sums=('5113c06884f7de453ce57702abaac1d618307f33f6789fa870e87a59d772aca2'
            'ca9252633e782b8f85d6a94ea4f6babd4fe30bd759085b373160b1878e36ff78'
            'e8b12ba279d7eb06abcab8aad6f43eb289451113b832d09865c22fdeb6faad29'
            '2cc83c77423a2dda3696766b2d1ccee2796e052ab04d5178905f41ed9241a3d8')

prepare() {
  cd lua-$pkgver

  patch -p1 -i ../liblua.so.patch
  sed "s/%VER%/${pkgver%.*}/g;s/%REL%/$pkgver/g" ../lua.pc > lua.pc
  sed -i '' -e 's|CC= gcc|CC?= gcc|g' src/Makefile
}

build() {
  cd $pkgname-$pkgver

  [[ $CARCH == amd64 ]] && export CFLAGS="$CFLAGS -fPIC"
  sed -i '' -e 's|-ldl ||g' src/Makefile

  export LUA_SONAME="liblua-5.3.so.1"
  make MYCFLAGS="$CFLAGS -fPIC -DLUA_COMPAT_5_2 -DLUA_COMPAT_5_1" MYLDFLAGS="$LDFLAGS" freebsd
}

package() {
  cd $pkgname-$pkgver
  gmake \
    TO_LIB="liblua.a liblua.so liblua.so.5.3 liblua.so.$pkgver" \
    INSTALL_TOP="$pkgdir/usr" \
    INSTALL_MAN="$pkgdir/usr/share/man/man1" \
    install

  install -dm755 "$pkgdir/usr/libdata/pkgconfig"
  install -m644 lua.pc "$pkgdir/usr/libdata/pkgconfig/lua.pc"

  # Install the documentation
  install -d "$pkgdir/usr/share/doc/lua"
  install -m644 doc/*.{gif,png,css,html} "$pkgdir/usr//share/doc/lua"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=4 sw=4 et:
