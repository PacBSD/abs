# $Id: PKGBUILD 174762 2013-01-07 00:33:58Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ccache
pkgver=3.1.9
pkgrel=2
pkgdesc="A compiler cache"
arch=('i686' 'x86_64')
url="http://ccache.samba.org/"
license=('GPL3')
depends=('zlib')
source=("http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('65f48376a91d3651d6527ca568858be8')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -dm755 ${pkgdir}/usr/bin
  install -m755 ccache ${pkgdir}/usr/bin/ccache
  install -dm755 ${pkgdir}/usr/share/man/man1
  install -m644 ccache.1 ${pkgdir}/usr/share/man/man1/ccache.1

  install -dm755 ${pkgdir}/usr/lib/ccache/bin
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/cc 
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/gcc
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/g++
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/cpp
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/c++
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-cc
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-gcc
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-g++
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-cpp
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-c++
}
