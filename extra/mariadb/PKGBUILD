# $Id: PKGBUILD 180833 2013-03-26 15:59:12Z bpiotrowski $
# Maintainer: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgbase=mariadb
pkgname=('libmariadbclient' 'mariadb-clients' 'mariadb' 'mytop')
pkgver=10.0.21
pkgrel=3
arch=('i386' 'amd64')
license=('GPL')
url='http://mariadb.org/'
makedepends=('cmake' 'gnu-binutils' 'gcc-fortran' 'pcre')
patchdir=("${pkgbase}-${pkgver}")
source=(http://mariadb.kisiek.net/mariadb-$pkgver/source/mariadb-$pkgver.tar.gz
	http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/dev-db/mysql-init-scripts/files/mysql-{5.1.53-conf.d,5.1.67-init.d}
	mariadb-post.sh
	mariadb-tmpfile.conf
	mysql-server.in)
sha256sums=('4b9a32e15ceadefdb1057a02eb3e0addf702b75aef631a3c9194b832ecfa3545'
            '927667f76c917e211d1ff58768956fbc6b4dcdaf32315f624b6993ce90ec55f4'
            'daf65c1329cd2765201d06e2169fd14d0c65620898198b058f91ec58d36aad73'
            '368f9fd2454d80eb32abb8f29f703d1cf9553353fb9e1ae4529c4b851cb8c5dd'
            '2af318c52ae0fe5428e8a9245d1b0fc3bc5ce153842d1563329ceb1edfa83ddd'
            'fbe0a3e1746985cd09ca1ad20b7e72d344d183a7f969cca390aa650d38019742')

build() {

  cd $srcdir
  if [ ! -d build ]; then
    mkdir build
  fi

  if [ ! -d ${srcdir}/bin ]; then
    mkdir ${srcdir}/bin
    ln -s /usr/bin/f95 ${srcdir}/bin/f77
    export PATH=${PATH}:${srcdir}/bin
  fi
  cd build

 cmake ../$pkgbase-$pkgver \
    -DCMAKE_AR=/usr/local/bin/ar \
    -DCMAKE_RANLIB=/usr/local/bin/ranlib \
    -DBUILD_CONFIG=mysql_release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc/mysql \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/var/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_INFODIR=share/mysql/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=lib/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_JEMALLOC=ON \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITH_TOKUDB_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITHOUT_PBXT_STORAGE_ENGINE=1 \
    -DCMAKE_C_FLAGS="-fPIC $CFLAGS -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC $CXXFLAGS -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
    -DWITH_MYSQLD_LDFLAGS="-pie ${LDFLAGS},-z,now"


  gmake -j1
}

package_libmariadbclient() {
  pkgdesc='MariaDB client libraries'
  conflicts=('libmysqlclient')
  depends=('pcre' 'gcc-libs')
  provides=("libmysqlclient=$pkgver")
  options=('staticlibs')
  cd "$srcdir"/build
  for dir in include libmysql libmysqld libservices; do
    make -C $dir DESTDIR="$pkgdir" install
  done

  install -d "$pkgdir"/usr/bin
  install -m755 scripts/mysql_config "$pkgdir"/usr/bin/
  install -d "$pkgdir"/usr/share/man/man1
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 "$srcdir"/$pkgbase-$pkgver/man/$man.1 "$pkgdir"/usr/share/man/man1/$man.1
  done

}

package_mariadb-clients() {
  pkgdesc='MariaDB client tools'
  depends=('libmariadbclient' 'gcc-libs')
  conflicts=('mysql-clients')
  provides=("mysql-clients=$pkgver")
  
  cd "$srcdir"/build
  make -C client DESTDIR="$pkgdir" install

  # install man pages
  install -d "$pkgdir"/usr/share/man/man1
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 "$srcdir"/$pkgbase-$pkgver/man/$man.1 "$pkgdir"/usr/share/man/man1/$man.1
  done

  # provided by mariadb
  rm "$pkgdir"/usr/bin/{mysql_{plugin,upgrade},mysqlbinlog,mysqltest}
}

package_mytop() {
  pkgdesc='Top clone for MariaDB'
  depends=('perl-dbd-mysql' 'perl-term-readkey')

  cd build
	install -dm755 "$pkgdir"/usr/bin
  install -m755 scripts/mytop "$pkgdir"/usr/bin/mytop
}

package_mariadb() {
  pkgdesc='Fast SQL database server, drop-in replacement for MySQL'
  backup=('etc/mysql/my.cnf')
  install=mariadb.install
  depends=('mariadb-clients' 'libxml2' 'pcre')
  conflicts=('mysql' 'mytop')
  provides=("mysql=$pkgver" 'mytop')
  optdepends=('perl-dbd-mysql: mytop dependency'
              'perl-term-readkey: mytop dependency')
  options=('emptydirs')

  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
  
  install -dm755 "$pkgdir"/etc/mysql/
	install -dm755 "${pkgdir}"/etc/{rc.d,init.d,conf.d}
	install -m775 "${srcdir}/mysql-server.in" ${pkgdir}/etc/rc.d/mysql
	install -m755 "${srcdir}/mysql-5.1.67-init.d" ${pkgdir}/etc/init.d/mysql
	install -m644 "${srcdir}/mysql-5.1.53-conf.d" ${pkgdir}/etc/conf.d/mysql
	chmod +x ${pkgdir}/etc/rc.d/mysql

  install -m644 "$pkgdir"/usr/share/mysql/my-medium.cnf "$pkgdir"/etc/mysql/my.cnf
  install -m755 "$srcdir"/mariadb-post.sh "$pkgdir"/usr/bin/mysqld-post

  cd "$pkgdir"
  rm usr/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}

  rm usr/lib/libmysql*
  rm -r ${pkgdir}/usr/include/
  rm ${pkgdir}/usr/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1

  # provided by mariadb-clients
  rm usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm usr/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

  # provided by mytop
  rm usr/bin/mytop

  # not needed
  rm -r usr/{data,mysql-test,sql-bench}
  rm usr/share/man/man1/mysql-test-run.pl.1

  #provided in base
  rm -f ${pkgdir}/usr/bin/perror
  rm -f ${pkgdir}/usr/share/man/man1/perror.1
}

