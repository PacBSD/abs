# Original creators:
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>

# Adaption to non-icedtea openjdk7:
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=('jre7-openjdk-headless' 'jre7-openjdk' 'jdk7-openjdk'
         'openjdk7-src' 'openjdk7-doc')
pkgbase=java7-openjdk
_java_ver=7
# well they say it's u25
_patched_updatever=u43
_updatever=u40
_builddate=26_aug_2013
_buildnumber=b43
_milestone=fcs

pkgver=${_java_ver}.95.00
pkgrel=14
arch=('i386' 'amd64')
url="http://icedtea.classpath.org"
license=('custom')
options=('!emptydirs' '!makeflags')
#makedepends=('jdk7-openjdk' 'libxp' 'libxslt'
#             'alsa-lib' 'apache-ant>=1.8.1' 'giflib' 'libpng>=1.5.7' 'gtk2'
#             'java-rhino' 'libpulse>=0.9.11' 'zip' 'unzip' 'cpio' 'fastjar' 'wget')
makedepends=('libxp' 'libxslt' 'giflib' 'gtk2' 'libpng>=1.5.7' 'wget' 'libcups' 'zip' 'unzip'
                'alsa-lib' 'libxtst' 'bootstrap-openjdk' 'dos2unix' 'cups')

_url=http://download.java.net/openjdk/jdk7u40/promoted/b43
_port_patches=(
	patch-bsd
	patch-icedtea-web
	patch-src-os-bsd-vm-os_bsd.cpp
	patch-src-os-bsd-vm-vmError_bsd.cpp
	patch-src-share-classes-sun-print-PSPrinterJob.java
	patch-src-solaris-bin-java_md_solinux.c
	patch-src-solaris-classes-sun-awt-X11-XWM.java
	patch-src-solaris-classes-sun-nio-fs-BsdFileSystemProvider.java
	patch-src-solaris-classes-sun-nio-fs-DefaultFileSystemProvider.java
	patch-src-solaris-classes-sun-print-UnixPrintServiceLookup.java
	patch-src-solaris-native-java-net-bsd_close.c
)
patchfiles=(
	"http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u45-b30.xz"
	"http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u51-b30.xz"
	"http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u55-b31.xz"
	"http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u60-b30.xz"
	"http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u65-b31.xz"
	"http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u71-b14.xz"
	"http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u76-b31.xz"
	"http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u80-b32.xz"
        "http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u85-b02.xz"
        "http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u91-b02.xz"
        "http://distcache.FreeBSD.org/local-distfiles/glewis/openjdk7/patch-7u95-b00.xz"
)
source=(${_url}/openjdk-${_java_ver}${_updatever}-${_milestone}-src-${_buildnumber}-${_builddate}.zip
	http://archive.apache.org/dist/ant/binaries/apache-ant-1.9.4-bin.zip
        jdk7-openjdk.profile
        jdk7-openjdk.profile.csh
        jre7-openjdk.profile
        jre7-openjdk.profile.csh
	"${patchfiles[@]}"
        "${_port_patches[@]}"
        cacerts
)
sha256sums=('00f8ea561876cbcad174ba3ca7a9ae10b50df46a782ac64c2ac57ac357b50596'
            '973d97d656e65561e2753823de6cf6db35570aaf93eeec6e7cb3d68e1ec4d0e8'
            'bd55299e08e6f8e61ba5baad0990170891801a29a1e8137e502b5e8a10fc37ea'
            '3f28f8bfc6dd105a07f747d7135c77a77de433e2b8647dd7520a900135203fbd'
            'b7c045b08ad55a9f79390c104fa846d0e7dbb49fccffb2fab2a3824b6b19c9c8'
            '89d99d8ac269ca66e2e279aff652d5aac938a35faec93cd8cff8f048052bd3ce'
            'f0ab28db8819a72b82236d98c6780a0fa063cf4d2f45f26105545559e630a1b3'
            'af1143952a0f67535c2bdf586b7089af1d525f544974e989fe7b273a925b8359'
            '30d4ae0e98e639dca060f7dc8289fe8af345e93258c8642cad1378725330afa9'
            '1132c6c32f515b735b7b3f78670ec81c3161b7afe8a10f21f68f851c2e776a4c'
            'acde33caa1691a6a1bcb4cab71ea01ce73df616d02d023bda053e3f728850d72'
            '2fa4622c55d55e27679ef99ce5d528210d66982abdfae53bf7fa9850e160db27'
            'a9ddaf7ee121b29bff9cd0300e7939640f6933223d81a484fa2db575cbb7f3de'
            '095e2f074125a3804710b7276fd96b743befb0472c3a3b5fd3a695511a10ff23'
            '87699747b28ae80cb168f1e2af3dcff65cc0ef9a74decebcf4ee1868b117f971'
            'b18c3843d656340fcca0e9b0eb0410a598dbce9c53c84fc6fde53d189bf44829'
            'bb522469cedcd31ff15fde745ab5717bd012c52ce2e25d1180b3338b90f755a3'
            '2008c2c899dc5b6ca22f05ddfaaa48e90997f4abed9135be7e2c5fb15ac210e7'
            '13ac827e1bac77d41e5cdf9814b6634d225229b6fa515892fa4710a900693d2f'
            '2eba00c3e22504d7d065de235d5930cd267fe249375091e10449aa8b8160b901'
            '19bb314eaf86bc00643e258c8beba5a09b353cbca497e0f460ee5f28a6b2baec'
            'e90a3735968101ed1e2cf95deefca63a73084df57ce3733baeea062d0524f530'
            'a2f08346ad44e0fc3b34ff0740c2e9938b9663e6a9c7064dda7040b2fdc59833'
            '78b97c3f6e004138998494ef74fc8dbe8c92a83da95d88583411295cc76b4361'
            '73bcb7a1758daa49cdc80d395ee5edb3af7cb0be0dc24c0124ad93f8688a2d0b'
            '4968cbb98ad070d14dd89515d4ed654bbadc50efeaf71b80ba80c9eb5daa31b4'
            '839d8ff24d2f6de56dc833f27a4cce9b7f02de789cc508acfe26775b5180f9ad'
            '6f73f8aec847d23e2665d3ac9c90bc48cae4329c5b42f38d366bfe3b4adc648a'
            '7054cb97cc065c6d3287d6469278f0148b5c82ffa0c924ee2f4464184d0b6b09')

_jvmdir=/usr/lib/jvm/java-7-openjdk

[ "$CARCH" = "amd64" ] && _JARCH=amd64
[ "$CARCH" = "i386"   ] && _JARCH=i586

_security_icons=(
        security-icon-bw16.png
		security-icon-bw24.png
		security-icon-bw32.png
		security-icon-bw48.png
		security-icon-interim16.png
		security-icon-interim24.png
		security-icon-interim32.png
		security-icon-interim48.png
		security-icon-yellow16.png
		security-icon-yellow24.png
		security-icon-yellow32.png
		security-icon-yellow48.png)

prepare() {
  cd openjdk

  for x in "${patchfiles[@]}"
  do
    y="${x##*/}"
    msg "Applying patch ${x##*/}"
    patch -p0 -i "${srcdir}/${y/.xz/}"
  done

  for i in "${_port_patches[@]}"
  do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done


# include linux filesystem provider classes
  msg "filesystem provider sed"
  sed -i '' -e 's@FILES_java =.*@& sun/nio/fs/LinuxFileSystemProvider.java@' \
      jdk/make/java/nio/FILES_java.gmk

#  mkdir -p jdk/src/share/classes/sun/awt/resources
#  for icon in "${_security_icons[@]}"
#  do
#    mv jdk/src/solaris/classes/sun/awt/X11/${icon} \
#       jdk/src/share/classes/sun/awt/resources/
#  done

sed -i '' -e 's| -liconv||' \
	Makefile \
	jdk/make/sun/splashscreen/Makefile \
	jdk/make/java/npt/Makefile \
	jdk/make/java/instrument/Makefile

dos2unix jdk/src/share/classes/com/sun/org/apache/xml/internal/security/resource/xmlsecurity_en.properties
}

build() {
  cd "${srcdir}/openjdk"

  export LANG="C" LC_ALL="C"
  
  export ALT_BOOTDIR="/usr/share/bootstrap-openjdk"
  PATH="/usr/local/bin:${PATH}"
  unset JAVA_HOME
  unset LD_LIBRARY_PATH
  unset CLASSPATH
  export OPENJDK_OSARCH=bsd-${_JARCH}
  export ALT_PARALLEL_COMPILE_JOBS=`sysctl -n kern.smp.cpus`
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"
  unset MAKEFLAGS
  export ANT_HOME="${srcdir}/apache-ant-1.9.4"
  export JAVACMD="${ALT_BOOTDIR}/bin/java"
  export ALT_CUPS_HEADERS_PATH=/usr/local/include
  export ALT_X11_PATH=/usr X11DIR=/usr
  export ALT_PKG_PATH=/usr
  export ALT_PACKAGE_PATH=/usr
  export ALT_FREETYPE_HEADERS_PATH=/usr/include
  export ALT_FREETYPE_LIB_PATH=/usr/lib
  export PTHREAD_LIBS="-lpthread"
  export LOCALDIR=/usr
  export COMPILER_WARNINGS_FATAL="false"
  export ZIP_DEBUGINFO_FILES=0
  export SKIP_COMPARE_IMAGES="true"
  export SKIP_FASTDEBUG_BUILD="true"
  export SKIP_DEBUG_BUILD="true"
  export ZIP_DEBUGINFO_FILES=0
  export ENABLE_FULL_DEBUG_SYMBOLS=0

  gmake CC=${CC} CXX=${CXX} HOST_CC=${CC} all
}

package_jre7-openjdk-headless() {
  pkgdesc="Free Java environment - Minimal Java runtime - needed for executing non GUI Java programs"
  #depends=('libjpeg-turbo' 'lcms2' 'nss' 'ca-certificates-java')
  depends=('libjpeg-turbo' 'lcms2')
  optdepends=('libcups: needed for Java Mauve support - libmawt.so'
              'fontconfig: needed for Java Mauve support - libmawt.so')
  provides=('java-runtime-headless=7')
  conflicts=('openjdk6' 'openjdk7')
  backup=(etc/profile.d/jre.sh
          etc/profile.d/jre.csh
          etc/java-7-openjdk/calendars.properties
          etc/java-7-openjdk/content-types.properties
          etc/java-7-openjdk/cursors/cursors.properties
          etc/java-7-openjdk/flavormap.properties
          etc/java-7-openjdk/fontconfig.bfc
          etc/java-7-openjdk/jvm.cfg
          etc/java-7-openjdk/logging.properties
          etc/java-7-openjdk/management/jmxremote.access
          etc/java-7-openjdk/management/jmxremote.password
          etc/java-7-openjdk/management/management.properties
          etc/java-7-openjdk/management/snmp.acl
          etc/java-7-openjdk/net.properties
          etc/java-7-openjdk/psfont.properties.ja
          etc/java-7-openjdk/psfontj2d.properties
          etc/java-7-openjdk/security/java.policy
          etc/java-7-openjdk/security/java.security
          etc/java-7-openjdk/sound.properties)
#          etc/java-7-openjdk/fontconfig.properties
#          etc/java-7-openjdk/security/nss.cfg
#          etc/java-7-openjdk/tz.properties)
  #install=jre7-openjdk-headless.install

  cd "${srcdir}/openjdk/build/bsd-${_JARCH}/j2re-image"

  install -d -m755 ${pkgdir}/${_jvmdir}/jre/
  cp -RP bin lib ${pkgdir}/${_jvmdir}/jre

  # Install man pages
  pushd man
  install -m755 -d ${pkgdir}/usr/share/man/{,ja/}man1/
  install -m644 man1/*.1 ${pkgdir}/usr/share/man/man1
  install -m644 ja_JP.UTF-8/man1/*.1 ${pkgdir}/usr/share/man/ja/man1
  popd

  # more files that belong to the desktop package
    # "/usr/lib/jvm/java-7-openjdk/jre/lib/${_JARCH}/libpulse-java.so"
  mkdir ${srcdir}/tmp-desktop-jre
  for file in \
    "/usr/lib/jvm/java-7-openjdk/jre/bin/policytool" \
    "/usr/lib/jvm/java-7-openjdk/jre/lib/${CARCH}/libjsoundalsa.so" \
    "/usr/lib/jvm/java-7-openjdk/jre/lib/${CARCH}/libsplashscreen.so" \
    "/usr/lib/jvm/java-7-openjdk/jre/lib/${CARCH}/xawt/libmawt.so" \
    "/usr/share/man/ja/man1/policytool.1" \
    "/usr/share/man/man1/policytool.1"; do
     dirname=`dirname $file`
     install -dm755 ${srcdir}/tmp-desktop-jre/$dirname || /bin/true
     # mv file from fakeinstall to pkgdir
     mv ${pkgdir}/$file ${srcdir}/tmp-desktop-jre$file
  done
  rmdir ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/${CARCH}/xawt

  # Link binaries into /usr/bin
  pushd ${pkgdir}/${_jvmdir}/jre/bin
  install -m755 -d ${pkgdir}/usr/bin/
  for file in *; do
    ln -sf ${_jvmdir}/jre/bin/${file} ${pkgdir}/usr/bin
  done
  popd

  ## Link JKS keystore from ca-certificates-java
  #rm -f ${pkgdir}/${_jvmdir}/jre/lib/security/cacerts
  #ln -sf /etc/ssl/certs/java/cacerts "${pkgdir}/${_jvmdir}/jre/lib/security/cacerts"
# install the cacerts file
  install -dm755 ${pkgdir}/${_jvmdir}/jre/lib/security
  install "${srcdir}/cacerts" ${pkgdir}/${_jvmdir}/jre/lib/security/cacerts

  # Set some variables
  install -m755 -d ${pkgdir}/etc/profile.d/
  install -m755 ${srcdir}/jre7-openjdk.profile ${pkgdir}/etc/profile.d/jre.sh
  install -m755 ${srcdir}/jre7-openjdk.profile.csh ${pkgdir}/etc/profile.d/jre.csh

  # Install license
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgbase}/
  install -m644 ASSEMBLY_EXCEPTION LICENSE THIRD_PARTY_README \
                 ${pkgdir}/usr/share/licenses/${pkgbase}

  # Put some more files under backup control
  install -m755 -d ${pkgdir}/etc/java-7-openjdk/
  install -m644 ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/*.properties* ${pkgdir}/etc/java-7-openjdk/
  # install dummy links to make them found by JAVA
  cd ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/
  for file in `ls ${pkgdir}/etc/java-7-openjdk/*.properties*`; do
    ln -vsf /etc/java-7-openjdk/`basename $file` .
  done
  # some more
  install -m755 -d ${pkgdir}/etc/java-7-openjdk/{cursors,management,security}
  install -m644 ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/images/cursors/cursors.properties ${pkgdir}/etc/java-7-openjdk/cursors/
  pushd ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/images/cursors/
    ln -vsf /etc/java-7-openjdk/cursors/cursors.properties .
  popd
  mv ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/management/jmxremote.password.template ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/management/jmxremote.password
  mv ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/management/snmp.acl.template ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/management/snmp.acl
  install -m644 ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/management/{management.properties,jmxremote.access,jmxremote.password,snmp.acl} ${pkgdir}/etc/java-7-openjdk/management/
  pushd ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/management
    ln -vsf /etc/java-7-openjdk/management/{management.properties,jmxremote.access,jmxremote.password,snmp.acl} .
  popd
#nss.cfg for the following 2:
  install -m644 ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/security/{java.policy,java.security} ${pkgdir}/etc/java-7-openjdk/security/
  pushd ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/security
    ln -vsf /etc/java-7-openjdk/security/{java.policy,java.security} .
  popd
  install -m644 ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/fontconfig.bfc ${pkgdir}/etc/java-7-openjdk/
  install -m644 ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/${CARCH}/jvm.cfg ${pkgdir}/etc/java-7-openjdk/
  pushd ${pkgdir}/usr/lib/jvm/java-7-openjdk/jre/lib/${CARCH}/
     ln -vsf /etc/java-7-openjdk/jvm.cfg .
  popd
}
 
package_jre7-openjdk() {
  pkgdesc="Free Java environment - Full Java runtime environment - needed for executing Java GUI and Webstart programs"
  depends=('jre7-openjdk-headless' 'xdg-utils' 'hicolor-icon-theme')
  optdepends=('icedtea-web-java7: web browser plugin + Java Web Start'
             'alsa-lib: for basic sound support'
             'giflib: for gif format support'
             'libpulse: for advanced sound support'
             'gtk2: for the Gtk+ look and feel - desktop usage'
             'libxtst: linked in xawt/libmawt.so - desktop usage')
  install=jre7-openjdk.install
  provides=('java-runtime=7')
  conflicts=('openjdk6' 'openjdk7')

  mv ${srcdir}/tmp-desktop-jre/* ${pkgdir}
  # Link binaries into /usr/bin
  pushd ${pkgdir}/${_jvmdir}/jre/bin
  install -m755 -d ${pkgdir}/usr/bin/
  for file in *; do
    ln -sf ${_jvmdir}/jre/bin/${file} ${pkgdir}/usr/bin
  done
  popd

  cd "${srcdir}/openjdk/build/bsd-${_JARCH}/"

  # Install icons and menu entries
  for s in 16 24 32 48 ; do
    install -m755 -d ${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/
    install -m644 ../../../openjdk/jdk/src/solaris/classes/sun/awt/X11/java-icon${s}.png \
                  ${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/java.png
  done

#  # Install desktop files.
#  install -m755 -d ${pkgdir}/usr/share/applications
#  install -m644 ${srcdir}/icedtea-${_icedtea_ver}/policytool.desktop ${pkgdir}/usr/share/applications
}

package_jdk7-openjdk() {
  pkgdesc="Free Java environment - SDK"
  depends=('jre7-openjdk')
  provides=('java-environment=7')
  conflicts=('java-environment')
  replaces=('openjdk6' 'openjdk7')
  backup=(etc/profile.d/jdk.sh etc/profile.d/jdk.csh)

  cd "${srcdir}/openjdk/build/bsd-${_JARCH}/j2sdk-image"

  # Main files
  install -m755 -d ${pkgdir}/${_jvmdir}/

  cp -RP include lib ${pkgdir}/${_jvmdir}

  # 'bin' files
  pushd bin
  install -m755 -d ${pkgdir}/${_jvmdir}/bin/ \
                   ${pkgdir}/usr/bin/ \
                   ${pkgdir}/usr/share/man/{,ja/}man1/

  # 'java-rmi.cgi' will be handled separately as it should not be in the PATH and has no man page
  for b in $(ls | grep -v java-rmi.cgi); do
    if [ -e ../jre/bin/${b} ]; then
      # Provide a link of the jre binary in the jdk/bin/ directory
      ln -s ../jre/bin/${b} ${pkgdir}/${_jvmdir}/bin/${b}
    else
      # Copy binary to jdk/bin/
      install -m755 ${b} ${pkgdir}/${_jvmdir}/bin/${b}
      # Copy man page
      install -m644 ../man/man1/${b}.1 ${pkgdir}/usr/share/man/man1/${b}.1
      install -m644 ../man/ja/man1/${b}.1 ${pkgdir}/usr/share/man/ja/man1/${b}.1
      # Link from /bin/
      ln -s ${_jvmdir}/bin/${b} ${pkgdir}/usr/bin/${b}
    fi
  done
  popd

#  # Install desktop files.
#  install -m755 -d ${pkgdir}/usr/share/applications
#  install -m644 ${srcdir}/icedtea-${_icedtea_ver}/jconsole.desktop ${pkgdir}/usr/share/applications

  # Handling 'java-rmi.cgi' separately
  install -m755 -d ${pkgdir}/${_jvmdir}/bin
  install -m755 bin/java-rmi.cgi ${pkgdir}/${_jvmdir}/bin/java-rmi.cgi

  # Set some variables
  install -m755 -d ${pkgdir}/etc/profile.d/
  install -m755 ${srcdir}/${pkgname}.profile ${pkgdir}/etc/profile.d/jdk.sh
  install -m755 ${srcdir}/${pkgname}.profile.csh ${pkgdir}/etc/profile.d/jdk.csh
}

package_openjdk7-src() {
  pkgdesc="Free Java environment - sources"
  replaces=('openjdk6-src')

  cd "${srcdir}/openjdk/build/bsd-${_JARCH}/j2sdk-image"
  install -dm755 "${pkgdir}/${_jvmdir}"
  install src.zip "${pkgdir}/${_jvmdir}/src.zip"
}

package_openjdk7-doc() {
  pkgdesc="Free Java environment - documentation"

  cd "${srcdir}/openjdk/build/bsd-${_JARCH}"
  install -m755 -d ${pkgdir}/usr/share/doc/openjdk7-doc
  for i in docs/*; do
    cp -RP $i "${pkgdir}/usr/share/doc/openjdk7-doc/"
  done
}
