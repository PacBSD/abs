# $Id: PKGBUILD 171126 2012-11-16 19:24:09Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xmms
pkgver=1.2.11
pkgrel=9
pkgdesc="The X MultiMedia System - a multimedia player"
arch=('i386' 'amd64')
url="http://legacy.xmms2.org/"
license=('GPL')
depends=('gtk' 'libxxf86vm' 'libsm' 'desktop-file-utils')
makedepends=('mesa' 'libmikmod' 'libvorbis' 'alsa-lib' 'libgl')
optdepends=('libmikmod: for the MikMod input plugin' \
            'libvorbis: for the Ogg Vorbis input plugin' \
            'alsa-lib: for the alsa output plugin' \
            'libgl: for the OpenGL Spectrum analyser visualization plugin')
options=('!libtool')
install=xmms.install
source=(http://legacy.xmms2.org/${pkgname}-${pkgver}.tar.bz2 \
	xmms-menu.patch xmms-1.2.10-crossfade-0.3.9.patch xmms-fix.diff \
        xmms-1.2.11-CVE-2007-0653.0654.patch xmms-1.2.11-fix-http-title-mpg123.patch \
        xmms-1.2.10-recode-id3.patch xmms-1.2.11-fix-mikmod-driver.patch \
        xmms-1.2.10-fonts.patch xmms.16.png xmms.32.png xmms.48.png
	extra-patch-Visualization_Makefile.am
	patch-Output__OSS__mixer.c
	patch-Output__esd__mixer.c
	patch-aclocal.m4
	patch-am64
	patch-configure.in
	patch-gcc41
	patch-ltmain.sh
	patch-oss
	patch-po-Makefile.in.in
	patch-xmms-config.in
	patch-xmms_util.c
	xmms.desktop.in)
sha1sums=('9d1eae4baab25fd35c9ddfb49321ca60222f672d'
          '27fb6e21bd6db923a425a8df8bd07a2a93867ddb'
          'ff5c172752c8bee708d9a9ecd53864cc95a4bcc5'
          'c227284955f2cc16f027dade670155c0f0fbc4ff'
          '4ef47c34b27b6c868d4301b7a078b257c8604471'
          '0c060385561ea2a4b1c7ad3bd35d3646ae9554f5'
          '2a3b1bf17054b6bd8b1e5937de5c0ad49af8dc92'
          '155e11b344af1ba594b4e339640bcca37c8909af'
          '31abd37c9c57bd13b2088eb11a1e54e5aa312bf3'
          '0089d11aa3bce67f3496592d68ee65837406e220'
          'f24071ae484d9b55e61dba076801fa8e7976ff2f'
          '485c67cb8c8d4678bb3b0b280d2e233b14153b22'
          '7aa8e9144e9a444526c6089c901de8fc627fcbd5'
          '425115f426a2b5779bfa0d28af9473f2d781c4fe'
          'ddaaf13e66cb6b8ab504c0f4bb7e0b6b8d5e6392'
          '3ce6e7e00377d08a5b8a9421419fdf2d55025f43'
          '4dd8a5120e8f5a13d1b4171c4f8e981735bc023d'
          '19be042088f186bfcf1f74383b02e745f48fcbcb'
          '7245047a55bb1164a03ee85485f4457267fe75cb'
          '50fa9699f04ccd5ce5907f722ad3719dfda47530'
          'abbfe80ffaf5b6c9752c17525afaff200589e763'
          '24342e4bd32265fc81f54c5dd72b509dd456fa57'
          'fe5ff8147c81190470e72a8c3e4c4daef04c24c1'
          '5c7c77baa4720f4fbe98b793cc0f00fc5797329d'
          '9ab35d5640c34bcbe8b68f8f761cfd7e499d2e76')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Fix xmms.desktop
  patch -p0 < ../xmms-menu.patch
# Patch for hang with xmms-crossfade 
  patch -p1 < ../xmms-1.2.10-crossfade-0.3.9.patch
# fix xmms not able to be shown double size,at least needed for x86_64
  patch -p1 < ../xmms-fix.diff
# Several patches from Mandriva
  patch -p1 < ../xmms-1.2.11-fix-http-title-mpg123.patch
  patch -p0 < ../xmms-1.2.10-recode-id3.patch
  patch -p1 < ../xmms-1.2.10-fonts.patch
  patch -p1 < ../xmms-1.2.11-CVE-2007-0653.0654.patch
  patch -p1 < ../xmms-1.2.11-fix-mikmod-driver.patch

#  port patches
   for i in extra-patch-Visualization_Makefile.am patch-Output__OSS__mixer.c \
	patch-Output__esd__mixer.c patch-aclocal.m4 \
	patch-am64 patch-configure.in patch-ltmain.sh \
	patch-oss patch-po-Makefile.in.in patch-xmms-config.in \
	patch-xmms_util.c; do
		patch -p0 -i ${srcdir}/$i
  done
  
#  export CFLAGS="${CFLAGS} -fPIC"

  if [ "$CARCH" == "i386" ]; then
    SIMDOPT="--enable-simd"
  else
    SIMDOPT="--disable-simd"
  fi
  ./configure --prefix=/usr --mandir=/usr/share/man $SIMDOPT
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  
  install -m644 xmms/xmms.desktop "${pkgdir}/usr/share/applications/xmms.desktop"
  install -m644 xmms/xmms_mini.xpm "${pkgdir}/usr/share/pixmaps/xmms_mini.xpm"
  install -m644 ../xmms.16.png "${pkgdir}/usr/share/pixmaps/xmms16.png"
  install -m644 ../xmms.32.png "${pkgdir}/usr/share/pixmaps/xmms32.png"
  install -m644 ../xmms.48.png "${pkgdir}/usr/share/pixmaps/xmms48.png"
  ln -s /usr/share/pixmaps/xmms48.png "${pkgdir}/usr/share/pixmaps/xmms.png"

  # don't want wmxmms
  rm -rf "${pkgdir}/usr/bin/wmxmms" "${pkgdir}/usr/share/xmms"
  rm -f "${pkgdir}"/usr/share/man/man1/{gnomexmms.1,wmxmms.1}
}
