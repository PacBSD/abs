# $Id: PKGBUILD 198559 2013-10-30 15:36:13Z allan $
# Maintainer:
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-netload-plugin
pkgver=1.2.0
pkgrel=1
pkgdesc="A netload plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://xfce-goodies.berlios.de/"
license=('GPL')
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
install=xfce4-netload-plugin.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('c824f09e2d3860011b44d8272d661c458237786828027f3d9ad6a1d4f0096845')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	
	sed -i '' -e 's, || __FreeBSD_kernel__,,' \
		panel-plugin/net.c \
		panel-plugin/net.h \
		panel-plugin/os.h
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec \
    --localstatedir=/var \
    --disable-debug
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
