# $Id: PKGBUILD 185050 2013-05-10 17:29:50Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=soprano
pkgver=2.9.3
pkgrel=8
pkgdesc='A library which provides a highly usable object-oriented C++/Qt4 framework for RDF data'
arch=('amd64')
url='http://soprano.sourceforge.net/'
license=('GPL' 'LGPL')
depends=('qt4' 'redland' 'libiodbc' 'virtuoso-base')
makedepends=('cmake' 'java-environment' 'doxygen')
optdepends=('jre7-openjdk-headless')

source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('714a729809943efd4b536ed15cd66aca')

prepare() {
  sed -i '' -e '/-DLINK_LIBRARIES=dl/d' \
		${pkgname}-${pkgver}/cmake/modules/FindRedland.cmake
}

build() {
  mkdir build
  cd build
  # we need the rpath
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSOPRANO_DISABLE_CLUCENE_INDEX=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
