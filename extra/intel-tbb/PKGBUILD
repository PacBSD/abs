# $Id: PKGBUILD 188979 2013-06-27 17:08:09Z eric $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=intel-tbb
pkgver=4.3_20150611
pkgrel=1
pkgdesc='High level abstract threading library'
arch=('amd64')
url='http://www.threadingbuildingblocks.org/'
license=('GPL')
depends=('gcc-libs')
makedepends=('gmake' 'gcc')
options=(!clang)
patchdir="tbb${pkgver/\./}oss"
source=("http://threadingbuildingblocks.org/sites/default/files/software_releases/source/tbb${pkgver/\./}oss_src.tgz"
        'version_info_FreeBSD.sh')
sha1sums=('5457cd15ad13625442283e67844199a79e88a3a4'
          'af0856dd6e744d8bcd5459dd92655e84e63e4bf2')

prepare() {
  cd tbb${pkgver/\./}oss

  cp ../version_info_FreeBSD.sh build/

  if [ "${CC}" != "gcc" ]; then
    cp build/FreeBSD.gcc.inc build/FreeBSD.${CC}.inc
  fi

  sed -i '' -e "s|gcc|${CC}|g" -e "s|g++|${CXX}|g" \
    build/FreeBSD.${CC}.inc

  find . -type f | xargs sed -i '' -e 's/-fno-schedule-insns2//g'
}

build() {
  cd tbb${pkgver/\./}oss

  gmake tbb_os=FreeBSD arch=x86_64
}

package() {
  cd tbb${pkgver/\./}oss

  install -d "${pkgdir}"/usr/lib
  ${BSD_INSTALL_LIB} build/FreeBSD_*/*.so* "${pkgdir}"/usr/lib

  install -d "${pkgdir}"/usr/include
  cp -R include/tbb "${pkgdir}"/usr/include
}
