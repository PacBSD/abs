# $Id: PKGBUILD 151165 2012-02-25 06:45:35Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xeyes
pkgver=1.1.1
pkgrel=3
pkgdesc="Follow the mouse/SHAPE extension X demo"
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxt' 'libxext' 'libxmu' 'libxrender')
makedepends=('xorg-util-macros')
source=(http://xorg.freedesktop.org/archive/individual/app/xeyes-${pkgver}.tar.bz2)
sha1sums=('efe6116d31a7f69e4fb6038613e52b0960b9b61c')

build() {
  cd "${srcdir}/xeyes-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xeyes-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
