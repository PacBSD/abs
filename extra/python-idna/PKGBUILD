# $Id: PKGBUILD 239754 2015-05-26 04:32:41Z fyan $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=python-idna
pkgname=('python-idna' 'python2-idna')
pkgver=2.0
pkgrel=2
pkgdesc="Internationalized Domain Names in Applications (IDNA)"
arch=('amd64')
license=('BSD')
url="https://github.com/kjd/idna"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/i/idna/idna-${pkgver}.tar.gz")
md5sums=('bd17a9d15e755375f48a62c13b25b801')

prepare() {
   cp -a idna-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd idna-${pkgver}
   python setup.py build

   # Build python 2 module
   cd ../idna-${pkgver}-python2
   python2 setup.py build
}

check() {
   # Check python3 module
   cd "${srcdir}"/idna-${pkgver}
   python setup.py test

   # Check python2 module
   cd "${srcdir}"/idna-${pkgver}-python2
   python2 setup.py test
}

package_python-idna() {
   depends=('python')

   cd idna-${pkgver}
   python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
   install -m644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

package_python2-idna() {
   depends=('python2')

   cd idna-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
   install -m644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}
