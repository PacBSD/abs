# $Id: PKGBUILD 170804 2012-11-12 02:00:36Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Lukas Sabota <punkrockguy318@cocmast.net> (Timidity Patch)

pkgname=sdl_mixer
pkgver=1.2.12
pkgrel=3
pkgdesc="A simple multi-channel audio mixer"
arch=('i386' 'amd64')
url="http://www.libsdl.org/projects/SDL_mixer/"
license=('custom')
depends=('sdl>=1.2.12' 'libvorbis' 'libmikmod' 'smpeg')
makedepends=('fluidsynth')
optdepends=('fluidsynth: MIDI software synth, replaces built-in timidity')
options=('!libtool')
source=(http://www.libsdl.org/projects/SDL_mixer/release/SDL_mixer-$pkgver.tar.gz
        mikmod1.patch mikmod2.patch fluidsynth-volume.patch double-free-crash.patch
        patch-SDL_mixer.h patch-mixer.c)
md5sums=('e03ff73d77a55e3572ad0217131dc4a1'
         '95a6d58686fe3017d58c39e3e1dd40f7'
         'd823994c3961f2ff48644478f27a27d1'
         'eed8b61defde36ac2077ef96d868ea3e'
         '4b4835c63297f016a198cacb7dd68ec1'
         '0f13d134a59406988f24caab904798c4'
         '21bea731646b2b0cc6d9b754533c6e9c')

prepare() {
  cd "$srcdir/SDL_mixer-$pkgver"

  patch -p0 -i "${srcdir}/patch-SDL_mixer.h"
  patch -p0 -i "${srcdir}/patch-mixer.c"

  find . -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

}

build() {
  cd "$srcdir/SDL_mixer-$pkgver"

  patch -Np1 -i ../mikmod1.patch
  patch -Np1 -i ../mikmod2.patch
  patch -Np1 -i ../fluidsynth-volume.patch
  patch -Np1 -i ../double-free-crash.patch

  gsed -e "/CONFIG_FILE_ETC/s|/etc/timidity.cfg|/etc/timidity++/timidity.cfg|" \
      -e "/DEFAULT_PATH/s|/etc/timidity|/etc/timidity++|" \
      -e "/DEFAULT_PATH2/s|/usr/local/lib/timidity|/usr/lib/timidity|" \
      -i timidity/config.h

  ./configure --prefix=/usr --disable-static
  gmake
}

package() {
  cd "$srcdir/SDL_mixer-$pkgver"
  gmake DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
