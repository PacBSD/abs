# $Id: PKGBUILD 137679 2011-09-10 11:58:36Z pierre $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=giblib
pkgver=1.2.4
pkgrel=13
pkgdesc="A library that feh uses as a wrapper to imlib2"
arch=('i386' 'amd64')
url="http://freshmeat.net/projects/giblib/"
license=('MIT')
depends=('imlib2' 'libxext' 'freetype2')
options=('libtoolfix')
source=("http://linuxbrit.co.uk/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('c810ef5389baf24882a1caca2954385e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" docsdir=/usr/share/doc/giblib install

  # Install custom license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
