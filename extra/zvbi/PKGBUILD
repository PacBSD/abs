# $Id: PKGBUILD 187081 2013-06-03 11:17:17Z allan $
# Maintainer: 
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=zvbi
pkgver=0.2.35
pkgrel=3
pkgdesc="VBI capture and decoding library"
url="http://zapping.sourceforge.net/cgi-bin/view/ZVBI/WebHome"
arch=('i386' 'amd64')
depends=('libpng' 'libx11' 'gettext-runtime')
license=('GPL')
options=('libtoolfix')
source=("http://downloads.sourceforge.net/sourceforge/zapping/${pkgname}-${pkgver}.tar.bz2")
md5sums=('95e53eb208c65ba6667fd4341455fa27')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i '' -e 's|#cpu (\([^)]*\))|defined(__\1__)|g' \
		src/misc.h \
		src/exp-gfx.c
	sed -i '' -e 's|, ts|, \&ts|' test/date.c
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --disable-static \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
