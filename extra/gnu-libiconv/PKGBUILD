_pkgname=libiconv
pkgname=gnu-libiconv
pkgver=1.14
pkgrel=10
pkgdesc='Provides GNU libiconv.so and libcharset.so'
arch=('i386' 'amd64')
url='http://www.gnu.org/software/libiconv/'
license=('LGPL')
port_patches=(
	patch-lib-iconv.c
	patch-lib_Makefile.in
	patch-libcharset-config.charset
	patch-src-Makefile.in
)
source=("http://ftp.gnu.org/pub/gnu/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
	${port_patches[@]})
sha256sums=('72b24ded17d687193c3366d0ebe7cde1e6b18f0df8c55438ac95be39e8a30613'
            '1960fa7aaba7267f484288673b984a29c230367be6865881b202b21c2e440580'
            '9a3d191dce5f9847c7d74982fa845a6bd734e86adbb43f8cda86e4a1093dec6a'
            'a970b378289df0452419bcc27905f292ee722242a66dc902030d3c0ccfae83c0'
            '1fa4706d7fce529133c4ecfc829f0846c482821987d06d6c500782b7449459c6')
options=(!libtool)

prepare() {
	cd $srcdir/${_pkgname}-${pkgver}

	for i in ${port_patches[@]}; do
		msg "Applying patch ${i}"
		patch -p0 -i "${srcdir}/${i}"
	done

	sed -i '' -e 's:ei_ksc5601:ei_euc_kr:g' lib/aliases.h
	sed -i ''  -e 's:gawk:awk:g' configure \
		libcharset/configure \
		preload/configure

}

build() {
	cd $srcdir/${_pkgname}-${pkgver}
	./configure --prefix=/usr/local \
		am_cv_func_iconv="yes" \
		am_cv_proto_iconv_arg1="const"

	gmake
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

}
# vim:set ts=2 sw=2 et:
