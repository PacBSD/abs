# Port: devel/sdl12
pkgname=sdl
pkgver=1.2.15
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard"
arch=('i386' 'amd64')
url="http://www.libsdl.org"
license=('LGPL')
depends=('libxext' 'libxrender' 'libx11')
makedepends=('nasm' 'mesa' 'glu')
patchdir=("SDL-$pkgver")
options=('staticlibs')
source=(http://www.libsdl.org/release/SDL-${pkgver}.tar.gz
        SDL-1.2.10-GrabNotViewable.patch
        SDL-1.2.15-SDL_EnableUNICODE_drops_keyboard_events.patch
        SDL-1.2.15-ignore_insane_joystick_axis.patch
        SDL-1.2.15-no-default-backing-store.patch
        SDL-1.2.15-x11-Bypass-SetGammaRamp-when-changing-gamma.patch
        sdl-1.2.14-fix-mouse-clicking.patch
        sdl-1.2.14-disable-mmx.patch
        libsdl-1.2.15-resizing.patch
        X11_KeyToUnicode.patch)
md5sums=('9d96df8417572a2afb781a7c4c811a85'
         '37ad001a4d2ff924a5fab356b49f8a78'
         '8d5a61a0adf1a302695a30141f402712'
         'e382e5b7449e95969c5ba4084b0aab39'
         '1b0578c76453813eb4eaf0125ce242db'
         '321377e38e2db7f1baf43c392e165dd5'
         '04d8c179f125e04bcd4c9d60e013c2d7'
         'e5c16b7611f62c7cb25f0534eff68852'
         '3dd50347d8856160a9cbd7f80383a1f8'
         'edea272dff4698470877c016d818918d')

prepare() {
  cd SDL-$pkgver
  patch -Np1 -i ../SDL-1.2.10-GrabNotViewable.patch
  patch -Np1 -i ../SDL-1.2.15-SDL_EnableUNICODE_drops_keyboard_events.patch
  patch -Np1 -i ../SDL-1.2.15-ignore_insane_joystick_axis.patch
  patch -Np1 -i ../SDL-1.2.15-no-default-backing-store.patch

  # https://bugs.freedesktop.org/show_bug.cgi?id=27222
  patch -Np1 -i ../SDL-1.2.15-x11-Bypass-SetGammaRamp-when-changing-gamma.patch

  patch -Np1 -i ../sdl-1.2.14-fix-mouse-clicking.patch
  patch -Np1 -i ../sdl-1.2.14-disable-mmx.patch
  patch -Np1 -i ../libsdl-1.2.15-resizing.patch
  patch -Np1 -i ../X11_KeyToUnicode.patch
}

build() {
  cd SDL-$pkgver

  ac_cv_header_libusbhid_h=no \
  ./configure --prefix=/usr --disable-alsa --disable-alsatest \
              --with-x --disable-video-directfb \
              --enable-video-opengl --disable-nasm \
              --enable-oss --disable-rpath --disable-static

  gmake
}

package() {
  cd SDL-$pkgver
  gmake DESTDIR="$pkgdir" install

	# Remove -Wl and -rpath, and duplicate -pthread from sdl.pc
  sed -i '' -e 's|-Wl,-rpath,${libdir} ||g' \
    -e 's|-lSDL  -pthread|-lSDL|g' ${pkgdir}/usr/libdata/pkgconfig/sdl.pc
}
