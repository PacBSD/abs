# $Id: PKGBUILD 172901 2012-12-06 14:08:40Z stephane $
# Maintainer:
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=talloc
pkgver=2.1.1
pkgrel=1
pkgdesc="A hierarchical pool based memory allocator with destructors"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://talloc.samba.org/"
depends=('libc')
makedepends=('python2' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=("http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('5dffb86414218a91864ed4453ba9be07')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   sed -i -e 's#python#python2#g' buildtools/bin/waf
   ./configure --prefix=/usr \
     --sysconfdir=/etc/samba \
     --localstatedir=/var \
     --enable-talloc-compat1
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install
}
