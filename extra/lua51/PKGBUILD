# Port lang/lua51
pkgname=lua51
pkgver=5.1.5
pkgrel=2
pkgdesc='A powerful light-weight programming language designed for extending applications'
arch=('i386' 'amd64')
url='http://www.lua.org/'
license=('MIT')
options=('!makeflags' '!emptydirs')
depends=('readline')
patchdir=("lua-$pkgver")
source=("http://www.lua.org/ftp/lua-$pkgver.tar.gz"
        'lua-arch.patch' 'LICENSE'
        'lua-5.1-cflags.diff')
md5sums=('2e115fe26e435e33b0d5c022e4490567'
         'fa25feb70ef9fec975b1c20da5098b3c'
         '0e2bd67b909b9ff673da844ca3480df2'
         '249582bf1fd861ccf492d2c35a9fe732')

build() {
  cd lua-$pkgver
  LDFLAGS+=" -lm -pthread"

  patch -p1 -i "$srcdir"/lua-arch.patch
  patch -p1 -i "$srcdir"/lua-5.1-cflags.diff
  [[ $CARCH == amd64 ]] && export CFLAGS="$CFLAGS -fPIC"
  sed -i '' -e 's|-ldl | |g' src/Makefile
  sed -i '' -e 's:llua:llua5.1:' -e 's:/include:/include/lua5.1:' etc/lua.pc
  sed -r -i '' -e '/^LUA_(SO|A|T)=/ s/lua/lua5.1/' \
               -e '/^LUAC_T=/ s/luac/luac5.1/' src/Makefile
  export LUA_SONAME="liblua-5.1.so.1"
  gmake MYCFLAGS="${CFLAGS} ${CPPFLAGS} -DLUA_USE_LINUX" MYLDFLAGS="$LDFLAGS" CC="${CC}" freebsd
}

package() {
  cd lua-$pkgver
  gmake \
    TO_BIN="lua5.1 luac5.1" \
    TO_LIB="liblua5.1.a liblua5.1.so liblua5.1.so.5.1 liblua5.1.so.$pkgver" \
    INSTALL_TOP="$pkgdir/usr" \
    INSTALL_INC="$pkgdir/usr/include/lua5.1" \
    INSTALL_MAN="$pkgdir/usr/share/man/man1" \
    install

  install -dm755 "$pkgdir/usr/libdata/pkgconfig"
  install -m644 etc/lua.pc "$pkgdir/usr/libdata/pkgconfig/lua5.1.pc"
  # Install the documentation
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -m644 doc/*.{gif,png,css,html} "$pkgdir/usr/share/doc/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  ln -s liblua5.1.so "$pkgdir/usr/lib/liblua.so.5.1"
  ln -s liblua5.1.so "$pkgdir/usr/lib/liblua.so.$pkgver"
  cd "$pkgdir/usr/share/man/man1"
  mv lua.1 lua5.1.1
  mv luac.1 luac5.1.1
}

# vim:set ts=4 sw=4 et:
