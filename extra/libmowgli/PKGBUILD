# $Id: PKGBUILD 198675 2013-11-01 21:30:25Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Brad Gordon <brad@rpgcyco.net>

pkgname=libmowgli
pkgver=2.0.0
pkgrel=2
pkgdesc='Performance and usability-oriented extensions to C'
url='http://www.atheme.org/project/mowgli'
license=('custom')
arch=('i386' 'amd64')
depends=('openssl')
makedepends=('git')
source=("${pkgname}-${pkgver}::git+https://github.com/atheme/libmowgli-2.git#tag=${pkgname}-${pkgver}")
sha1sums=('SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
