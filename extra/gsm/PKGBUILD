# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=gsm
pkgver=1.0.13
pkgrel=11
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('i386' 'amd64')
url="http://www.quut.com/gsm/"
license=('custom')
patchdir=(${pkgname}-${pkgver%.*}-pl${pkgver##*.}/)
source=("http://www.quut.com/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gsm.patch')
md5sums=('c1ba392ce61dc4aff1c29ea4e92f6df4'
         '014954f5b79b3a29bc0b76f758ec7a6e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}-pl${pkgver##*.}/"

  patch -Np0 -i "${srcdir}/${pkgname}.patch"

  # flags for shared lib
  CFLAGS="${CFLAGS} -fPIC"
  gmake CCFLAGS="-c ${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}-pl${pkgver##*.}/"

  # Prepare directories
  install -m755 -d "${pkgdir}"/usr/{bin,lib,include/gsm,share/licenses/${pkgname},share/man/man{1,3}}

  gmake -j1 INSTALL_ROOT="${pkgdir}/usr" \
    GSM_INSTALL_INC="${pkgdir}/usr/include/gsm" \
    GSM_INSTALL_MAN="${pkgdir}/usr/share/man/man3" \
    TOAST_INSTALL_MAN="${pkgdir}/usr/share/man/man1" \
    install

  # Install license
  install -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
