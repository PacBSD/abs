# $Id: PKGBUILD 173707 2012-12-21 16:29:38Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2
pkgver=2.6
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=(i686 amd64)
license=('GPL')
url="http://freetype.sourceforge.net"
options=('libtoolfix')
install=libtool-unfixed.install
# harfbuzz<->freetype cycle
# freetype can be built first
depends=('zlib' 'libpng')
if ! ((BREAK_DEPENDENCY_CYCLES)); then
  depends+=('harfbuzz')
fi
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2{,.sig}
        freetype-2.2.1-enable-valid.patch
        freetype-2.5.1-enable-spr.patch
        freetype-2.5.1-enable-sph.patch
        env_FT2_SUBPIXEL_HINTING.diff)
sha1sums=('cd2b144205dd2c61693e2d861069367aa3dde1bc'
          'SKIP'
          'f279d922a873d62a8af50bfc873051839d194dca'
          '13ee8d558593db991ad29fa090b461f914536104'
          'c31fa3d342ead56f3acfa1f267b474a7686d0014'
          '78322bee05f013cd76487439ddac0cae50596db4')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')

prepare() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-spr.patch"

  # https://bugs.archlinux.org/task/35274
  patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-sph.patch"
  # Provide a way to enable the above patch at runtime.
  # Hopefully just a temporary measure until fontconfig picks up
  # the necessary configurables.
  patch -Np1 -i "${srcdir}/env_FT2_SUBPIXEL_HINTING.diff"
}

build() {
  cd "${srcdir}/freetype-${pkgver}"
  ./configure --prefix=/usr --disable-static
  gmake -j1
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  gmake -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  gmake -j1 DESTDIR="${pkgdir}" install

  install -dm755 "$pkgdir"/usr/lib/unfixed
  ln -s /usr/lib/libfreetype.so.6 "$pkgdir"/usr/lib/unfixed/libfreetype.so.17
  install -dm755 "$pkgdir"/usr/lib/ldconfig
  echo '/usr/lib/unfixed' > "$pkgdir"/usr/lib/ldconfig/freetype-unfixed
}
