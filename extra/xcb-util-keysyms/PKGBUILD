# Maintainer: Anthony Donnelly <Amzo@Archbsd.net>

pkgname=xcb-util-keysyms
pkgver=0.4.0
pkgrel=2
pkgdesc="Utility libraries for XC Binding - Standard X key constants and conversion to/from keycodes"
arch=('i386' 'amd64')
url="http://xcb.freedesktop.org"
license=('custom')
depends=('libxcb>=1.7')
makedepends=('xorg-util-macros')
source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2"
        'LICENSE')
sha256sums=('0ef8490ff1dede52b7de533158547f8b454b241aa3e4dcca369507f66f216dd9'
            'ded299aa179dcf0d885bf89274a4db77a530e03f9f5e7cf1c3c4ef1d60e914b9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-malloc0returnsnull
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
