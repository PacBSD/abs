# $Id: PKGBUILD 213058 2014-05-18 14:54:10Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-pyasn1-modules
pkgname=('python-pyasn1-modules' 'python2-pyasn1-modules')
pkgver=0.0.8
pkgrel=1
pkgdesc="A collection of ASN.1-based protocols modules"
arch=('amd64')
license=('BSD')
url="http://pypi.python.org/pypi/pyasn1-modules"
makedepends=('python-pyasn1' 'python2-pyasn1')
source=("http://pypi.python.org/packages/source/p/pyasn1-modules/pyasn1-modules-${pkgver}.tar.gz")
md5sums=('178129bc7fbf07215c25230c5f053f81')

prepare() {
   cp -a pyasn1-modules-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd pyasn1-modules-${pkgver}
   python3 setup.py build

   # Build python 2 module
   cd ../pyasn1-modules-${pkgver}-python2
   python2 setup.py build
}

package_python-pyasn1-modules() {
   depends=('python' 'python-pyopenssl' 'python-pyasn1')

   cd pyasn1-modules-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

   install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
   install -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}


package_python2-pyasn1-modules() {
   depends=('python2' 'python2-pyopenssl' 'python2-pyasn1')

   cd pyasn1-modules-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
   install -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
   
