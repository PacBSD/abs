# $Id: PKGBUILD 188100 2013-06-09 13:12:36Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Martin Sandsmark <martin.sandsmark@kde.org>

pkgname=vlc
pkgver=2.2.1
pkgrel=2
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player"
arch=('amd64')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL2')
depends=('a52dec' 'libdvbpsi' 'libxpm' 'libdca' 'qt4' 'libproxy' 
         'sdl_image' 'libdvdnav' 'libtiger' 'lua' 'libmatroska' 
         'zvbi' 'taglib' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp' 
         'libshout' 'libmad' 'libmpeg2' 'xcb-util-keysyms')
makedepends=('live-media' 'libnotify' 'libbluray' 'flac' 'kdelibs'
             'libdc1394' 'libcaca' 'libssh2'
             'librsvg' 'portaudio' 'libgme' 
             'twolame' 'aalib' 'libmtp' 'libdvdcss' 'gnome-vfs' 
             'libtar' 'vcdimager' 'opus' 'mesa' 'libgl')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'libdvdcss: for decoding encrypted DVDs'
            'libdc1394: for IEEE 1394 plugin'
            'kdelibs: KDE Solid hardware integration'
            'libva-vdpau-driver: vdpau back-end for nvidia'
            'libbluray: for Blu-Ray support'
            'flac: for Free Lossless Audio Codec plugin'
            'portaudio: for portaudio support'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'libcaca: for colored ASCII art video output'
            'libgme: for libgme plugin'
            'librsvg: for SVG plugin'
            'gnome-vfs: for GNOME Virtual File System support'
            'vcdimager: navigate VCD with libvcdinfo'
            'aalib: for ASCII art plugin'
            'libmtp: for MTP devices support'
            'smbclient: for SMB access plugin'
            'libcdio: for audio CD playback support'
            'ttf-freefont: for subtitle font '
            'ttf-dejavu: for subtitle font'
            'opus: for opus support')
conflicts=('vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!emptydirs')
install=vlc.install
source=("http://download.videolan.org/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('42273945758b521c408fabc7fd6d9946')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c
  sed -i -e 's:truetype/ttf-dejavu:TTF:g' modules/visualization/projectm.cpp
	sed -i ''  -e 's|/usr/local|/usr|g' \
         -e '\|LIBS|s|-lrt||' \
         configure

  sed -i '' -e 's|O4|O3|g' configure

  cp src/android/error.c src/posix
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rm -rf modules/access/v4l2/linux

  CFLAGS="${CFLAGS} -I/usr/include/samba-4.0" CPPFLAGS+="-I/usr/include/samba-4.0" \
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
      --enable-vlc \
      --enable-avcodec \
      --enable-avformat \
      --enable-oss \
      --enable-postproc \
      --disable-sftp \
      --enable-libgcrypt \
      --disable-alsa \
      --disable-dc1394 \
      --disable-directfb \
      --disable-projectm \
      --disable-telx \
      --disable-tremor \
      --disable-udev \
      --disable-update-check \
      --disable-glx \
      RCC=/usr/bin/rcc-qt4 \
      NCURSES_CFLAGS="-I/usr/include" NCURSES_LIBS="-lncursesw" \
      LDFLAGS="${LDFLAGS} -pthread -lc" \
      LIBS="-lc" CPPFLAGS="${CPPFLAGS} -pthread" \
      ac_cv_search_pthread_rwlock_init=-pthread
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for res in 16 32 48 128; do
    install -m644 "${srcdir}/vlc-${pkgver}/share/icons/${res}x${res}/vlc.png" \
        "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
}
