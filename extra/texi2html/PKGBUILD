# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=texi2html
pkgver=5.0
pkgrel=5
pkgdesc="Converts texinfo documents to HTML"
arch=('any')
url="http://www.nongnu.org/texi2html/"
license=('GPL')
depends=('perl')
install=texi2html.install
source=("http://download.savannah.gnu.org/releases/texi2html/$pkgname-$pkgver.tar.bz2"
        patch-texi2html.pl
)
md5sums=('f15ac876fcdc8be865b16535f480aa54'
         'ce6f4ed67b14ee6a70af2133b49d2434')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p0 -i "$srcdir"/patch-texi2html.pl

  sed -i '' \
      -e 's@\(texinfodata = \).*$@\1$(datadir)/doc/texi2html@' \
      -e 's@\(texinfohtmldir = \).*$@\1$(texinfodata)@' \
      doc/Makefile.in

  sed -i '' -e 's/^translations.pl/_&/' Makefile.in
  sed -i '' -e '/^install-data/s|i18n|&/en.thl|' Makefile.in
  chmod +x install-sh

  ./configure --prefix=/usr \
              --disable-multiplatform
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir"  install
}
