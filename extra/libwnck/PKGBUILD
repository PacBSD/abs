# Port: x11-toolkits/libwnck
pkgname=libwnck
pkgver=2.30.7
pkgrel=1
pkgdesc="Window Navigator Construction Kit"
arch=('i386' 'amd64')
license=('LGPL')
depends=('gtk2' 'startup-notification' 'libxres')
makedepends=('libxt' 'intltool' 'gobject-introspection')
url="http://www.gnome.org/"
options=('libtoolfix')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('8be84d5333f0ade0e61af9f4cbf7685cb7cce2aa0ee32f53bac32107a81fe194')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var \
        --with-html-dir=/usr/share/doc \
        --enable-startup-notification LIBS="-lintl"

    gmake
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    gmake DESTDIR="${pkgdir}" install

    #The binaries are shipped in libwnck3
    rm -f "${pkgdir}/usr/bin/wnckprop"
    rm -f "${pkgdir}/usr/bin/wnck-urgency-monitor"
    rmdir "${pkgdir}/usr/bin"

}
