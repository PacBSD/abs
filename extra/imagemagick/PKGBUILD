# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=imagemagick
pkgname=('imagemagick' 'imagemagick-doc')
pkgver=6.9.2.0
pkgrel=1
arch=('amd64')
url="http://www.imagemagick.org/"
license=('custom')
makedepends=('libltdl' 'lcms2' 'libxt' 'fontconfig' 'libxext' 'ghostscript'
             'openexr' 'libwmf' 'librsvg' 'libxml2' 'liblqr'
             'libwebp' 'bash')
#source=(http://www.imagemagick.org/download/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz{,.asc}
source=(ftp://ftp.sunet.se/pub/multimedia/graphics/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz{,.asc}
        perlmagick.rpath.patch)
sha1sums=('1db8234c996b5dd1f230f8e043fad27851709b8d'
          'SKIP'
          'e143cf9d530fabf3b58023899b5cc544ba93daec')

prepare() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  gsed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf --force --install
  patch -p0 -i "${srcdir}/perlmagick.rpath.patch"
}

build() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}

  # they use bashisms in configure.ac
  bash ./configure --prefix=/usr --sysconfdir=/etc --with-modules \
    --enable-hdri --with-wmf --with-openexr --with-xml --with-lcms2 \
    --with-webp --with-gslib --with-gs-font-dir=/usr/share/fonts/Type1 \
    --with-perl --with-perl-options="MAKE=gmake INSTALLDIRS=vendor" --with-lqr --with-rsvg \
    --disable-opencl --without-gvc --without-djvu --without-autotrace \
    --without-jbig --without-fpx --without-dps --without-fftw $EXTRAOPTS \
    MAKE=gmake ac_cv_path_SED=/usr/bin/gsed PTHREAD_LIBS="-lpthread"

  gmake
}

check() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  #gmake check
}

package_imagemagick() {
  pkgdesc="An image viewing/manipulation program"
  depends=('perl' 'libltdl' 'lcms2' 'libxt' 'fontconfig' 'libxext' 'liblqr')
  optdepends=('imagemagick-doc: for additional information'
              'ghostscript: for Ghostscript support' 
              'openexr: for OpenEXR support' 
              'libwmf: for WMF support' 
              'librsvg: for SVG support' 
              'libxml2: for XML support' 
              'libpng: for PNG support' 
              'libwebp: for WEBP support')
  backup=("etc/ImageMagick-${pkgver%%.*}/coder.xml"
          "etc/ImageMagick-${pkgver%%.*}/colors.xml"
          "etc/ImageMagick-${pkgver%%.*}/delegates.xml"
          "etc/ImageMagick-${pkgver%%.*}/log.xml"
          "etc/ImageMagick-${pkgver%%.*}/magic.xml"
          "etc/ImageMagick-${pkgver%%.*}/mime.xml"
          "etc/ImageMagick-${pkgver%%.*}/policy.xml"
          "etc/ImageMagick-${pkgver%%.*}/quantization-table.xml"
          "etc/ImageMagick-${pkgver%%.*}/thresholds.xml"
          "etc/ImageMagick-${pkgver%%.*}/type.xml"
          "etc/ImageMagick-${pkgver%%.*}/type-dejavu.xml"
          "etc/ImageMagick-${pkgver%%.*}/type-ghostscript.xml"
          "etc/ImageMagick-${pkgver%%.*}/type-windows.xml")
  options=('!docs' 'libtool' '!emptydirs')

  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  gmake -j1 DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/imagemagick/"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/imagemagick/LICENSE"
  install -dm755 "${pkgdir}/usr/share/licenses/imagemagick/"
  install -m644 NOTICE "${pkgdir}/usr/share/licenses/imagemagick/NOTICE"

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;

  #Cleaning
  rm -f "${pkgdir}"/usr/lib/*.la
}

package_imagemagick-doc() {
  pkgdesc="The ImageMagick documentation (utilities manuals and libraries API)"

  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  gmake -j1 DESTDIR="${pkgdir}" install-data-html
  install -dm755 "${pkgdir}/usr/share/licenses/imagemagick-doc/"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/imagemagick-doc/LICENSE"
  install -dm755 "${pkgdir}/usr/share/licenses/imagemagick-doc/"
  install -m644 NOTICE "${pkgdir}/usr/share/licenses/imagemagick-doc/NOTICE"
}
