# $Id: PKGBUILD 199975 2013-11-20 12:18:31Z allan $
# Maintainer:
# Contributor: Kritoke <kritoke@gamebox.net>

pkgname=libsidplay
pkgver=1.36.59
pkgrel=8
pkgdesc="A library for playing SID music files."
arch=('i386' 'amd64')
url="http://critical.ch/distfiles/"
license=('GPL')
source=(http://critical.ch/distfiles/${pkgname}-${pkgver}.tgz libsidplay-1.36.59-gcc43.patch)
md5sums=('37c51ba4bd57164b1b0bb7b43b9adece' 'c24d7bca2639f4fee03c40c7dcaadfee')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/libsidplay-1.36.59-gcc43.patch

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	  -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
	  ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}
