# $Id: PKGBUILD 189772 2013-07-07 19:08:06Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=dnsmasq
pkgver=2.75
pkgrel=1
pkgdesc="Lightweight, easy to configure DNS forwarder and DHCP server"
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
arch=('i386' 'amd64')
license=('GPL')
depends=('libc' 'libdbus' 'gmp' 'nettle')
install=$pkgname.install
backup=('etc/dnsmasq.conf')
source=("http://www.thekelleys.org.uk/$pkgname/$pkgname-$pkgver.tar.xz"
        'dnsmasq.in')
md5sums=('887236f1ddde6eb57cdb9d01916c9f72'
         '65695342cc4c55c0e74d54d538e208cc')

build() {
  cd "$pkgname-$pkgver"

  CFLAGS+=" -Wall -Wno-unused-value"

  make \
    CFLAGS="$CPPFLAGS $CFLAGS" \
    LDFLAGS="$LDFLAGS" \
    COPTS="-DHAVE_DNSSEC -DHAVE_DBUS"

}

package() {
  cd "$pkgname-$pkgver"

  make BINDIR=/usr/sbin PREFIX=/usr DESTDIR="$pkgdir" install

  install -dm755 "${pkgdir}/etc/"
  install -dm755 "${pkgdir}/etc/dbus-1/system.d"
  install -dm755 "${pkgdir}/etc/rc.d"
  install -m644 "dbus/dnsmasq.conf" "$pkgdir"/etc/dbus-1/system.d/dnsmasq.conf
  install -m644 "dnsmasq.conf.example" "$pkgdir"/etc/dnsmasq.conf
  install -m755 "$srcdir/dnsmasq.in" "$pkgdir"/etc/rc.d/dnsmasq
}

# vim: ts=2 sw=2 et ft=sh
