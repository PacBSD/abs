# Port: devel/subversion

pkgname=subversion
pkgver=1.9.4
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System"
arch=('i386' 'amd64')
license=('APACHE')
depends=('sqlite' 'serf' 'db')
makedepends=('python2' 'perl' 'java-runtime' 'java-environment'
             'libgnome-keyring' 'ruby' 'autoconf')
optdepends=('libgnome-keyring: for GNOME Keyring for auth credentials'
            'bash-completion: for svn bash completion'
            'python2: for some hook scripts'
            'java-environment: for Java support'
            'ruby: for some hook scripts')
backup=('etc/conf.d/svnserve')
url="http://subversion.apache.org/"
provides=('svn')
options=('!makeflags' 'libtoolfix' '!emptydirs')
source=(http://www.apache.org/dist/subversion/${pkgname}-${pkgver}.tar.bz2{,.asc}
        svn
        svnserve.initd2
        svnserve.confd
	subversion.rpath.fix.patch
	svnserve.tmpfiles
)
md5sums=('29121a038f87641055a8183f49e9739f'
         'SKIP'
         'a0db6dd43af33952739b6ec089852630'
         'ac39617ae727971088a85ff306b65878'
         '3b46c664a654279a52898fd8ecd68de3'
         '31c0f079a0b0815d6af5673ab7be96af'
         'bb2857eceafcfac35dde39dcffad2314')

# swig still not working...
prepare() {
   cd ${pkgname}-${pkgver}
   patch -Np0 -i ../subversion.rpath.fix.patch

   gsed -i 's|/usr/bin/env python|/usr/bin/env python2|' tools/hook-scripts/{,mailer/{,tests/}}*.py

   gsed -i -e 's/FLAGS="-Werror=unknown-warning-option \$/FLAGS="$/' configure
   gsed -i -e '/cd \$(SWIG_PL_DIR)\/native;/s/sed -i.*/bash -c "g&"/' Makefile.in
}

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   export PYTHON=/usr/bin/python2
   ./configure --prefix=/usr --with-apr=/usr --with-apr-util=/usr \
               --with-zlib=/usr --with-serf=/usr --with-apxs \
               --with-sqlite=/usr \
               --enable-javahl --with-gnome-keyring \
               --with-ruby-sitedir=/usr/lib/ruby/vendor_ruby \
               --with-apache-libexecdir=/usr/lib/httpd/modules \
               --disable-static --enable-bdb6 --with-apxs=no

   gmake LT_LDFLAGS="-L$Fdestdir/usr/lib"
   gmake swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python2.7/site-packages/svn javahl
     #swig_pydir_extra=/usr/lib/python2.7/site-packages/svn swig-py swig-pl javahl swig-rb
}

#check() {
#   cd "${srcdir}/${pkgname}-${pkgver}"
#   export LANG=C LC_ALL=C
#   make check check-swig-pl check-swig-py check-javahl CLEANUP=yes # check-swig-rb
#   make check check-swig-pl check-swig-py check-swig-rb CLEANUP=yes # check-javahl
#}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   export LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH}
   gmake DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
     swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python2.7/site-packages/svn \
     install install-javahl
     #install install-swig-py install-swig-pl install-javahl install-swig-rb

   install -dm755 "${pkgdir}"/usr/share/subversion
   # cp -a fails with acl issues on my system
   tar -C tools -cpf - hook-scripts | tar -C "${pkgdir}"/usr/share/subversion/ -xpvf -
   rm "${pkgdir}"/usr/share/subversion/hook-scripts/*.in

   install -dm755 "${pkgdir}"/usr/lib/tmpfiles.d
   install -m 644 "${srcdir}"/svnserve.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/svnserve.conf
  
    # ... common config
   install -dm755 "${pkgdir}"/etc/{conf.d,init.d}
   install -m644 "${srcdir}"/svnserve.confd "${pkgdir}"/etc/conf.d/svnserve
   install -m644 "${srcdir}"/svnserve.initd2 "${pkgdir}"/etc/init.d/svnserve
   install -dm755 "${pkgdir}"/usr/share/bash-completion/completions/
   install -m644 tools/client-side/bash_completion \
     "${pkgdir}"/usr/share/bash-completion/completions/subversion
   for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/usr/share/bash-completion/completions/${i}
   done
}
