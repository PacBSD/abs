# $Id: PKGBUILD 170882 2012-11-12 22:03:02Z heftig $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=gobject-introspection
pkgname=('gobject-introspection' 'gobject-introspection-runtime')
pkgver=1.44.0
pkgrel=2
url="https://live.gnome.org/GObjectIntrospection"
arch=('amd64' 'i686')
license=('LGPL' 'GPL')
makedepends=('cairo' 'python2' 'python2-mako')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('6f0c2c28aeaa37b5037acbf21558098c4f95029b666db755d3a12c2f1e1627ad')

prepare() {
  cd $pkgbase-$pkgver
  gsed -i '1s|#!/usr/bin/env python$|&2|' giscanner/*.py
}

build() {
  cd $pkgbase-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static --enable-doctool
  gmake
}

package_gobject-introspection-runtime() {
  pkgdesc="Introspection system for GObject-based libraries - runtime files"
  depends=('glib2')
  cd $pkgbase-$pkgver
  gmake DESTDIR="$pkgdir" install-libLTLIBRARIES install-typelibsDATA
}

package_gobject-introspection() {
  pkgdesc="Introspection system for GObject-based libraries"
  depends=("gobject-introspection-runtime=$pkgver" 'python2-mako' 'python2')
  options=('!emptydirs')

  cd $pkgbase-$pkgver
  gmake DESTDIR="$pkgdir" install
  gmake DESTDIR="$pkgdir" uninstall-libLTLIBRARIES uninstall-typelibsDATA
}

