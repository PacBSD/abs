# $Id: PKGBUILD 225572 2014-11-03 14:03:24Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>

pkgname=lv2
pkgver=1.12.0
pkgrel=1
pkgdesc="Successor to the LADSPA audio plug-in standard"
url="http://lv2plug.in/"
license=('LGPL' 'custom')
arch=('i386' 'amd64')
makedepends=('python2' 'libsndfile' 'gtk2')
optdepends=('libsndfile: Example sampler'
            'gtk2: Example sampler'
            'python2: Scripts')
provides=('lv2core')
conflicts=('lv2core')
replaces=('lv2core')
source=("http://lv2plug.in/spec/$pkgname-$pkgver.tar.bz2")
md5sums=('665580dc761de40b0d335001a204496a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
