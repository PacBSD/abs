# $Id: PKGBUILD 195475 2013-09-30 18:47:20Z bisson $
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: TuxSpirit<tuxspirit@archlinux.fr>  2007/11/17 21:22:36 UTC
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=p7zip
pkgver=9.38.1
pkgrel=2
pkgdesc='Command-line version of the 7zip compressed file archiver'
url='http://p7zip.sourceforge.net/'
license=('GPL' 'custom')
arch=('i386' 'amd64')
depends=('gcc-libs' 'bash')
makedepends=('yasm' 'nasm')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}_${pkgver}_src_all.tar.bz2"
        '7z')
sha1sums=('6b1eccf272d8b141a94758f80727ae633568ba69'
          '30a07dbc725c3c8607b2baee685961742ea1b0d2')
options=('!makeflags')

prepare() {
	cd "${srcdir}/${pkgname}_${pkgver}"
	rm GUI/kde4/p7zip_compress.desktop
	export CXXFLAGS="${CXXFLAGS} -fPIC"

	sed -e 's!g\+\+!clang\+\+!' \
		-e 's!gcc!clang!' \
		-e 's/-D_LARGEFILE_SOURCE//' \
		-e 's/-D_FILE_OFFSET_BITS=64//' \
		makefile.freebsd6 > makefile.machine

	sed -i ''  -e 's!-O!${CFLAGS}!' makefile.machine
}

build() {
	cd "${srcdir}/${pkgname}_${pkgver}"
	make all3 OPTFLAGS="${CXXFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}_${pkgver}"

	install -dm755 ${pkgdir}/usr/{bin,lib/p7zip}
	for BINARY in 7z 7za 7zr 7zCon.sfx; do
		install -m755 bin/${BINARY} ${pkgdir}/usr/lib/p7zip/
	done

	install -m644 bin/7z.so ${pkgdir}/usr/lib/p7zip/

	install -m755 ${srcdir}/7z ${pkgdir}/usr/bin/
	ln -sf 7z ${pkgdir}/usr/bin/7za
	ln -sf 7z ${pkgdir}/usr/bin/7zr

	chmod -R u+w "${pkgdir}/usr"

}
