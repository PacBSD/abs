# $Id: PKGBUILD 181867 2013-04-05 02:24:25Z eric $
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: XazZ <xazz.xazz [AT] googlemail.com>

pkgname=liblastfm
pkgver=1.0.9
pkgrel=1
pkgdesc="A Qt4 C++ library for the Last.fm webservices"
arch=('i386' 'amd64')
url='https://github.com/lastfm/liblastfm/'
license=('GPL3')
depends=('qt4' 'fftw' 'libsamplerate')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/lastfm/${pkgname}/tarball/${pkgver}")
sha1sums=('cd8c6f160529dbf9e0566973a8f37130c83a78c0')

build() {
  mkdir build
  cd build
  cmake ../lastfm-${pkgname}-* \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_TESTS=OFF
  make
}

package(){
  cd build
  make DESTDIR="${pkgdir}" install
}
