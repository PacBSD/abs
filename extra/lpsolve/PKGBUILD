# $Id: PKGBUILD 51162 2009-09-05 13:45:34Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=lpsolve
_origname=lp_solve
pkgver=5.5.2.0
_mainver=5.5
pkgrel=4
pkgdesc="a Mixed Integer Linear Programming (MILP) solver"
arch=('i386' 'amd64')
url="http://lpsolve.sourceforge.net/"
license=('GPL')
depends=('libc')
_port_patches=(patch-respect-TMPDIR patch-shared+commonlib.c)
source=(http://downloads.sourceforge.net/project/lpsolve/lpsolve/${pkgver}/lp_solve_${pkgver}_source.tar.gz
        cflags.patch
        "${_port_patches[@]}")
md5sums=('167c0fb4ab178e0b7ab50bf0a635a836'
         '7403f745d06619f59f52800b655d8751'
         '190a07a1d63c705ae066782b038ed321'
         '6b465d73486cc685c0ff9bf5f41397fc')

prepare() {
  cd "${_origname}_${_mainver}"
  patch -Np1 -i $srcdir/cflags.patch
  for i in "${_port_patches[@]}"; do
    patch -p0 -i "${srcdir}/$i"
  done
  find . -name ccc | xargs sed -i '' -e \
      's|-fpic|-fPIC|g ;
       s|-ldl||g'
}

build() {
  cd "${_origname}_${_mainver}"

  # taken from Fedora spec
  cd lpsolve55
  sh -x ccc
  rm bin/ux*/liblpsolve55.a
  cd ../lp_solve
  sh -x ccc
}

package() {
  cd "${srcdir}/${_origname}_${_mainver}"

  install -dm755 ${pkgdir}/usr/{bin,lib,include/lpsolve}
  install -m755 lp_solve/bin/ux*/lp_solve ${pkgdir}/usr/bin/
  install -m755 lpsolve55/bin/ux*/liblpsolve55.so ${pkgdir}/usr/lib/
  install -m644 lp*.h ${pkgdir}/usr/include/lpsolve/
}
