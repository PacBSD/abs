# Port: graphics/graphite2
pkgname=graphite
pkgver=1.3.8
pkgrel=1
arch=('i386' 'amd64')
url="http://projects.palaso.org/projects/graphitedev"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('LGPL' 'GPL' 'custom')
makedepends=('cmake' 'freetype2' 'python2') 
options=('!emptydirs')
source=(http://downloads.sourceforge.net/project/silgraphite/graphite2/graphite2-${pkgver}.tgz)
md5sums=('236d2cb1f831fe33083112afbb555f0e')

prepare() {
  cd "${srcdir}"

  gsed -i "s:\/usr\/bin\/python:\/usr\/bin\/python2:" graphite2-${pkgver}/tests/{corrupt.py,defuzz,fnttxtrender,fuzzbidi,fuzztest,hbspeeds,jsoncmp}

  # fix install path - .cmake files contain architecture dependend content - patch taken from FC/Slackware
  pushd graphite2-${pkgver}
  sed -i '' -e 's,/pkgconfig,data/pkgconfig,g' CMakeLists.txt
  popd
}

build() {
  cd "${srcdir}"
  mkdir build
  cd build

  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE:STRING=Release \
	-DGRAPHITE2_COMPARE_RENDERER=OFF \
	#-DCMAKE_VERBOSE_MAKEFILE:BOOL=ON # remove when docs install target will be fixed

  # fix unwanted -O3 cflag (taken form Debian)
  find . -type f ! -name "rules" ! -name "changelog" -exec sed -i '' -e 's/\-O3//g' {} \;

  make

}

check() {
  cd "${srcdir}"/build
  sed -i '' -e "s:python:python2:g" tests/CTestTestfile.cmake
  ctest || true
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="$pkgdir/" install

  # licenses
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/graphite2-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}
