# $Id: PKGBUILD 137917 2011-09-12 12:45:56Z pierre $
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgbase=('cdrdao')
pkgname=('cdrdao' 'gcdmaster')
pkgver=1.2.3
pkgrel=9
arch=('i386' 'amd64')
license=('GPL')
url="http://cdrdao.sourceforge.net/"
makedepends=('gcc-libs' 'lame' 'libmad' 'libvorbis' 'libao' 'libgnomeuimm')
port_patches=(
	patch-dao-cdrdao.man
	patch-dao-main.cc
	patch-scsilib-RULES-i386-freebsd-cc.rul
	patch-trackdb-FormatMp3.cc
	patch-trackdb-FormatOgg.cc
	patch-xdao-SoundIF-ao.cc
	patch-fix-strdup-declaration
)
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'cdrdao-1.2.3-autoconf-update.patch'
        'cdrdao-1.2.3-k3b.patch'
        'cdrdao-1.2.3-stat.patch'
        ${port_patches[@]})
md5sums=('8d15ba6280bb7ba2f4d6be31d28b3c0c'
         '8e53dfc174f7c0882194caa05e68b85e'
         '696f6ca01e1eeb9b6a5be88e535d9398'
         '0fce05542ebad283f36fa1c4d62992a0'
         'd1e6eab2e0420b41e9155d7c1d70aefb'
         'cdaeb8aa41ea4aca559269721dd61e23'
         '706272ae4ff95ae5ac67645ee6eb1323'
         '957632308c8dbcac09766873ea8466da'
         '024c52bcc320febb2997c2c492a8c99c'
         '700ac8e6383541faef913e381d6823c1'
         '23d7ad1c5c87a4717f936172a21643d6')

prepare() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	for i in ${port_patches[@]}; do
		msg "Applyign patch ${i}"
		patch -p0 -i "${srcdir}/${i}"
	done
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	patch -Np1 -i "${srcdir}/cdrdao-1.2.3-autoconf-update.patch"
	patch -Np1 -i "${srcdir}/cdrdao-1.2.3-k3b.patch"
	patch -Np1 -i "${srcdir}/cdrdao-1.2.3-stat.patch"

	./configure --prefix=/usr \
		--mandir=/usr/share/man \
		--sysconfdir=/etc \
		--with-xdao --with-lame \
		--with-ogg-support --with-mp3-support \
		LIBS="-lintl"
	gmake
}

package_cdrdao() {
	pkgdesc='Records audio/data CD-Rs in disk-at-once (DAO) mode'
	depends=('gcc-libs' 'lame' 'libmad' 'libvorbis' 'libao')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	for dir in trackdb utils paranoia dao
	do
	     gmake -C ${dir} DESTDIR="${pkgdir}" install
	done
}

package_gcdmaster() {
	pkgdesc='GUI frontend for creating audio CDs using cdrdao'
	depends=('libgnomeuimm' "cdrdao=${pkgver}")
	install=gcdmaster.install

	cd "${srcdir}/${pkgbase}-${pkgver}"
	gmake -C xdao GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

	mv "${pkgdir}/etc/gconf" "${pkgdir}/usr/share/"
	rmdir "${pkgdir}/etc"
}
