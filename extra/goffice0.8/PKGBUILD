# $Id: PKGBUILD 173347 2012-12-19 14:13:59Z jgc $
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=goffice0.8
pkgver=0.8.17
pkgrel=6
pkgdesc="A library of document-centric objects and utilities built on top of GLib and Gtk+"
arch=('i386' 'amd64')
url="http://www.gnome.org"
license=('GPL')
options=('!libtool')
depends=('gtk2' 'gconf' 'libgsf')
conflicts=('goffice<0.10')
makedepends=('pkgconf' 'intltool' 'gtk-doc')
source=(http://ftp.gnome.org/pub/gnome/sources/goffice/${pkgver%.*}/goffice-${pkgver}.tar.xz
        use-apiver-for-dirs.patch)
sha256sums=('165070beb67b84580afe80a8a100b674a81d553ab791acd72ac0c655f4fadb15'
            '4507a49f6ccb13e55c0b3cc6831b7e0dcec7badb58a3661190e843f8607fa270')

build() {
  cd "${srcdir}/goffice-${pkgver}"
  patch -Np0 -i "${srcdir}/use-apiver-for-dirs.patch"
  gsed -i -e 's/glib\/gregex.h/glib.h/' configure.in
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  gmake
}

package() {
  cd "${srcdir}/goffice-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
