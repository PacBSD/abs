# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andre Klitzing <aklitzing () online () de>

pkgname=libytnef
pkgver=1.5
pkgrel=7
pkgdesc="Yerase's TNEF Stream Reader library (decode winmail.dat)"
url="http://freshmeat.net/projects/ytnef/"
license=('GPL')
arch=('i386' 'amd64')
options=('!libtool')
depends=('libc')
source=("http://downloads.sourceforge.net/ytnef/${pkgname}-${pkgver}.tar.gz"
	'patch-ytnef.c')
md5sums=('6c44b955f33cf714c75a7bbe895cc352'
         '9dac42834894602e28300c1b1126640a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p0 -i ${srcdir}/patch-ytnef.c

  find . -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
