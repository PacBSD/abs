pkgname=imlib2
pkgver=1.4.7
pkgrel=4
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('amd64')
license=('BSD')
patchdir=("$srcdir/$pkgname-$pkgver/")
depends=('libtiff' 'giflib' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg-turbo')
options=('libtoolfix')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2")
source=(http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2)
sha1sums=('0ed225afbbee8e990c655cf13ed802bda8a5cd8a')

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -i '' -e 's/@my_libs@//' imlib2-config.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ $CARCH = "i686" ] && EXTRAOPTS="--enable-mmx"
  [ $CARCH = "amd64" ] && EXTRAOPTS="--enable-amd64"

  # Configure and Build
  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib $EXTRAOPTS \
              --with-tiff=/usr \
              --with-jpeg=/usr \
              --with-id3=/usr \
              --enable-visibility-hiding \
              ac_cv_lib_dl_dlopen=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Install License
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
