# $Id: PKGBUILD 146689 2012-01-16 11:36:24Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sdl_net
pkgver=1.2.8
pkgrel=3
pkgdesc="A small sample cross-platform networking library"
arch=('i386' 'amd64')
license=('custom')
url="http://www.libsdl.org/projects/SDL_net/"
depends=('sdl')
options=('!libtool')
source=(http://www.libsdl.org/projects/SDL_net/release/SDL_net-$pkgver.tar.gz
	patch-SDLnetsys.h)
md5sums=('20e64e61d65662db66c379034f11f718'
         'f53d717311d36e1822acf49c394db1b2')

build() {
  cd "$srcdir/SDL_net-$pkgver"
  patch -p0 -i "${srcdir}"/patch-SDLnetsys.h

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/SDL_net-$pkgver"
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
