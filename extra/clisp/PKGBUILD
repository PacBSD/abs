# $Id: PKGBUILD 154935 2012-03-31 09:53:13Z ronald $
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=clisp
pkgver=2.49
pkgrel=9
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=('i386' 'amd64')
license=('GPL')
url="http://clisp.cons.org/"
depends=('libsigsegv')
provides=('common-lisp')
makedepends=('ffcall')
options=('!makeflags' '!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/clisp/clisp-${pkgver}.tar.bz2"
	patch-configure
	patch-modules__clx__mit-clx__Makefile
	patch-modules__fastcgi__fastcgi_wrappers.c
	patch-modules__postgresql__config.h.in
	patch-src__makemake.in)
md5sums=('1962b99d5e530390ec3829236d168649'
         'd0be4f50fa4e2be37107a81166e2444e'
         '015d4300bc194d518e2e54d4af889dc5'
         '61081dfe0483bdf5a6599e373dcd6c55'
         'd4322357848c9c97f40841f959edc37e'
         'fdfbefe565643b0861eb23a934887224')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  for i in patch-configure \
	patch-modules__clx__mit-clx__Makefile \
	patch-modules__fastcgi__fastcgi_wrappers.c \
	patch-modules__postgresql__config.h.in \
	patch-src__makemake.in; do
  patch -p0 -i ${srcdir}/$i
  done

  ./configure --prefix=/usr --with-ffcall src --disable-mmap --without-readline
  cd src
  ./makemake --prefix=/usr --with-ffcall --with-dynamic-ffi --disable-mmap --without-readline > Makefile
  make
  gsed -i 's,http://www.lisp.org/HyperSpec/,http://www.lispworks.com/reference/HyperSpec/,g' config.lisp
  gmake
}

check() {
  cd $srcdir/${pkgname}-${pkgver}/src
  gmake check
}

package() {
  cd $srcdir/${pkgname}-${pkgver}/src
  gmake DESTDIR=$pkgdir install
}
