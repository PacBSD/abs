# Port: security/cryptopp

pkgname=crypto++
pkgver=5.6.2
_srcver=562
pkgrel=1
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('i386' 'amd64')
url="http://www.cryptopp.com/"
license=('custom')
depends=('gcc-libs')
makedepends=('unzip')
port_patches=(patch-GNUmakefile
		patch-misc.h
		patch-nbtheory.cpp
		patch-wake.cpp)
source=("http://www.cryptopp.com/cryptopp${_srcver}.zip"
        'libcrypto++.pc'
	${port_patches[@]})
md5sums=('7ed022585698df48e65ce9218f6c6a67'
         '46de3847c59ad16456fa863eb33e73b2'
         'ba01a2bde5b58f5aad780b4aea52adf6'
         '078c3c4268bc597ac0514156b75b1251'
         'f6386f853308075ea4cf98291d467693'
         '761f0357eef316fc15c4ea7b27735c5d')

prepare() {
  cd ${srcdir}

  for p in ${port_patches[@]}; do
	# Stupid build system tries to build patch-* as it's put into srcdir, remove them
	rm -f ${srcdir}/${p}
	patch -p0 -i ../${p}
  done
}

build() {
  cd ${srcdir}

  LDFLAGS+=" -lpthread"
  CXXFLAGS="${CXXFLAGS} -fPIC"
  gmake -f GNUmakefile  
  gmake libcryptopp.so
}

package() {
  cd ${srcdir}

  install -d ${pkgdir}/usr/{lib/pkgconfig,include/cryptopp}
  install -m644 *.h ${pkgdir}/usr/include/cryptopp/
  install -m644 libcryptopp.a "${pkgdir}/usr/lib/libcryptopp.a"
  install -m644 libcryptopp.so "${pkgdir}/usr/lib/libcryptopp.so"
  install -m644 ${srcdir}/libcrypto++.pc "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  install -d -m755             "${pkgdir}/usr/share/licenses/${pkgname}"
  install    -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
