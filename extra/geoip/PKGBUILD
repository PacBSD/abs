# $Id: PKGBUILD 183169 2013-04-18 10:54:20Z juergen $
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Manolis Tzanidakis <manolis@archlinux.org>

pkgname=geoip
pkgver=1.6.2
pkgrel=2
pkgdesc="Non-DNS IP-to-country resolver C library & utils"
arch=('i386' 'amd64')
url="http://www.maxmind.com/app/c"
license=('GPL')
depends=('geoip-database') # zlib in world
backup=('etc/geoip/GeoIP.conf')
options=('!libtool' '!emptydirs')
source=(https://github.com/maxmind/${pkgname}-api-c/archive/v${pkgver}.tar.gz)
sha256sums=('72562fc10792eea6cfa662d93ef40c6541b73619df8057a6e411144e671a9174')

prepare() {
  cd geoip-api-c-$pkgver
  autoreconf -vi
}

build() {
  cd geoip-api-c-$pkgver

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc/geoip
  make
}

check() {
  cd geoip-api-c-$pkgver
  ln -sf /usr/share/GeoIP data
  make check
}

package() {
  cd geoip-api-c-$pkgver
  make DESTDIR="$pkgdir" install
}

