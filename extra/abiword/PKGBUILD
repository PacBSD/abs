# $Id: PKGBUILD 184359 2013-05-06 19:36:30Z foutrelis $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=abiword
pkgname=('abiword' 'abiword-plugins')
pkgver=3.0.1
pkgrel=5
arch=('i386' 'amd64')
license=('GPL')
url="http://www.abisource.com"
makedepends=('pkg-config' 'asio' 'boost' 'fribidi' 'goffice' 'libwmf' 'wv' 'link-grammar'
             'aiksaurus' 'libxslt' 'enchant' 'libots' 'libwpg' 'librsvg' 'loudmouth' 'libsoup' 'psiconv')
options=('!makeflags')
source=(http://abisource.com/downloads/$pkgname/$pkgver/source/$pkgname-$pkgver.tar.gz
        abiword-3.0.0-link-grammar-5.patch
        abiword-3.0.0-link-grammar-5-second.patch
        link-grammar-panic.patch
        aiksaurus-plugin.m4
        gnutls-3.4.0.patch)
sha256sums=('e094f6fbf0afc5c5538b4894888e7c346f8ee8f49c9d24821dd696d0734865c6'
            '6d88800f1e16becd69ed93d5f070793a56b9f162f7d886c71756643bcaed7ef5'
            'd2c80da81a339634a7a4ee4cef12f7ee968f2c2a8c2f75533b6713b71f0d61fd'
            '94d1e638f7b85123dc2282d2e59b982bde6ad0dbbe1b6b54351e610c5a0ea578'
            '5f80a2f94f9929cdba9809c5e1a87cd5d537a2518bb879bfb9eab51a71c8dac1'
            'b393e26c19f92901f64d2bae54c86708ea7dd0b647572088a8ac0cd2eec89100')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Fix build with recent versions of link-grammar
  msg "link-grammar patching"
  patch -Np1 -i ../abiword-3.0.0-link-grammar-5.patch
  patch -Np1 -i ../abiword-3.0.0-link-grammar-5-second.patch
  patch -Np0 -i ../link-grammar-panic.patch

  msg "applying gnutls patch"
  # Fix build with new gnutls 3.4.x
  patch -Np0 -i ../gnutls-3.4.0.patch
  
  # Install missing m4 file
  install -m644 ../aiksaurus-plugin.m4 plugins/aiksaurus/plugin.m4

  # Generate m4 file for configure
  find plugins -name plugin.m4 | xargs cat > plugin-configure.m4

  libtoolize --force
  autoreconf -fi

  sed -i '' -e 's|glib/.*\.h>|glib.h>|g' \
		goffice-bits/goffice/app/goffice-app.h \
		src/af/util/xp/ut_go_file.h

  sed -i '' -e 's|" == "|" = "|g' configure

}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-clipart \
              --enable-templates \
              --disable-collab-backend-sugar \
              --enable-plugins --without-gnomevfs \
              --with-gio \
              --with-goffice \
              --disable-static \
              --with-psiconv-config=/nothere \
              --disable-collab-backend-service \
              --disable-collab-backend-tcp \
              WPG_CFLAGS="`pkg-config --cflags libwpg-0.3`"

  gmake
}

package_abiword() {
  pkgdesc="A fully-featured word processor"
  depends=('fribidi' 'wv' 'goffice' 'librsvg' 'enchant' 'desktop-file-utils')
  install=$pkgname.install
  optdepends=('abiword-plugins')
  conflicts=("abiword-plugins<${pkgver}-${pkgrel}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -i plugins/Makefile \
      -e 's/ collab / /' \
      -e 's/ wpg / /' \
      -e 's/ wmf / /' \
      -e 's/ grammar / /' \
      -e 's/ aiksaurus / /' \
      -e 's/ latex / /' \
      -e 's/ ots / /' \
      -e 's/ wordperfect / /'
  gmake DESTDIR="${pkgdir}" install

  # split out plugins, there's no Makefile target for a clean way
  mkdir ${srcdir}/_pluginsdir
  mv "$pkgdir"/usr/lib/abiword-${pkgver%.*}/plugins ${srcdir}/_pluginsdir
}

package_abiword-plugins() {
  pkgdesc="Additional plugins for Abiword"
  depends=("abiword=${pkgver}-${pkgrel}" 'loudmouth' 'libwpg' 'libwmf' 'link-grammar' 
           'aiksaurus' 'libxslt' 'libsoup' 'libots' 'libgsf' 'gnu-libiconv')

  install -dm755 ${pkgdir}/usr/lib/abiword-${pkgver%.*}
  mv ${srcdir}/_pluginsdir/plugins ${pkgdir}/usr/lib/abiword-${pkgver%.*}/
}
