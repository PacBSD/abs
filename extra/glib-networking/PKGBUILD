# $Id: PKGBUILD 185433 2013-05-14 09:44:30Z heftig $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=glib-networking
pkgver=2.44.0
pkgrel=2
pkgdesc="Network-related giomodules for glib"
arch=(i686 amd64)
url="http://www.gtk.org/"
license=(GPL2)
depends=(glib2 libproxy gnutls 'ca_root_nss' 'p11-kit' 'gsettings-desktop-schemas')
makedepends=(intltool)
options=('libtoolfix')
install=glib-networking.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
md5sums=('6989b20cf3b26dd5ae272e04a9acb0b3')

prepare() {
  cd $pkgname-$pkgver

  sed -i '' -e' s|-Wmissing-include-dirs||g' configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/libexec/$pkgname --disable-static \
    --disable-installed-tests \
    --with-ca-certificates=/usr/share/certs/ca-root-nss.crt
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
