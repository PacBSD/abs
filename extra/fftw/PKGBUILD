# $Id: PKGBUILD 176986 2013-02-03 11:04:50Z allan $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fftw
pkgver=3.3.4
pkgrel=5
pkgdesc="A library for computing the discrete Fourier transform (DFT)"
arch=('i386' 'amd64')
license=('GPL2')
url="http://www.fftw.org/"
depends=('libc' 'bash')
makedepends=('gcc-fortran')
options=('!libtool')
source=("http://www.fftw.org/${pkgname}-${pkgver}.tar.gz")
install=fftw.install
sha1sums=('fd508bac8ac13b3a46152c54b7ac885b69734262')

# notes:
# http://www.fftw.org/fftw2_doc/fftw_6.html#SEC69
# http://www.fftw.org/faq/section2.html#singleprec
# http://www.fftw.org/fftw3_doc/Precision.html#Precision


build() {
  cd ${srcdir}
  
  cp -a ${pkgname}-${pkgver} ${pkgname}-${pkgver}-double
  cp -a ${pkgname}-${pkgver} ${pkgname}-${pkgver}-long-double
  mv ${pkgname}-${pkgver} ${pkgname}-${pkgver}-single
  

  # use upstream default CFLAGS while keeping our -march/-mtune
  CFLAGS="-O2 -fomit-frame-pointer -fstrict-aliasing -ffast-math"

  case "$CARCH" in
    i386)
      CONFIGURE="./configure F77=gfortran --prefix=/usr \
                 --enable-shared --enable-threads --enable-i386-hacks"
      ;;
    amd64)
      CONFIGURE="./configure F77=gfortran --prefix=/usr \
                 --enable-shared --enable-threads"
      ;;
  esac

  # build double precision
  cd ${srcdir}/${pkgname}-${pkgver}-double
  $CONFIGURE --enable-sse2
  make

  # build & install long double precission
  cd ${srcdir}/${pkgname}-${pkgver}-long-double
  $CONFIGURE --enable-long-double
  make

  # build & install single precision
  cd ${srcdir}/${pkgname}-${pkgver}-single
  $CONFIGURE --enable-float
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}-double
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/${pkgname}-${pkgver}-long-double
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/${pkgname}-${pkgver}-single
  make DESTDIR=${pkgdir} install  
}
