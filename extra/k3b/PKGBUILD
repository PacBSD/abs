# $Id: PKGBUILD 190026 2013-07-13 03:43:42Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=k3b
pkgver=2.0.2
pkgrel=22
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=('i386' 'amd64')
url="http://sourceforge.net/projects/k3b/"
license=('GPL')
depends=('kdebase-runtime' 'libkcddb' 'libsamplerate' 'libmad' 'qt4'
         'ffmpeg' 'taglib' 'libmpcdec' 'libdvdread' 'cdrkit' 'libxft')
makedepends=('cmake' 'automoc4' 'docbook-xml')
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager: for vcd burning support'
            'transcode: for advanced mpeg conversion support'
            'emovix: for bootable multimedia cd/dvd support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for cd ripping support')
options=()
install=k3b.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	k3b-2.0.2-libavformat54.patch)
md5sums=('c86113af31a2032e57fd2f302b5f637a'
         '6c478468bec1aba1242421ffa69f46ce')

prepare(){ 
	cd ${pkgname}-${pkgver}
	patch -p1 -i "${srcdir}/k3b-2.0.2-libavformat54.patch"
}

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DK3B_ENABLE_HAL_SUPPORT=OFF
  gmake
}

package() {
  cd build
  gmake DESTDIR="${pkgdir}" install
}
