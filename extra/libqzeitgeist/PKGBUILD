# $Id: PKGBUILD 178968 2013-03-01 10:21:57Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libqzeitgeist
pkgver=0.8.0
pkgrel=11
pkgdesc="A Qt interface to the Zeitgeist event tracking system"
url='https://projects.kde.org/projects/kdesupport/libqzeitgeist'
arch=('i386' 'amd64')
license=('GPL')
depends=('qt4')
makedepends=('cmake' 'automoc4' 'python2' 'zeitgeist')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'set-declarative-installation-dir.patch')
md5sums=('97bdea6a1865db7d5f29c93e3a492f24'
         '712013e582acb73296e41ad842d0da01')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/set-declarative-installation-dir.patch
  cd ../

  # Fix python2 path
  gsed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    ${pkgname}-${pkgver}/scripts/onto2cpp.py

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
    -DDECLARATIVE_IMPORT_PREFIX=/usr/lib/qt4/imports/
 gmake
}

package() {
  cd build
  gmake DESTDIR="${pkgdir}" install
}
