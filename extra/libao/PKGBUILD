# $Id: PKGBUILD 175921 2013-01-25 01:07:36Z bisson $
# Contributor: dorphell <dorphell@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=libao
pkgver=1.2.0
pkgrel=3
pkgdesc='Cross-platform audio output library and plugins'
url='http://xiph.org/ao/'
arch=('i386' 'amd64')
license=('GPL')
depends=('alsa-lib')
makedepends=('libpulse')
optdepends=('libpulse: for pulseaudio plugin')
backup=('etc/libao.conf')
source=("http://downloads.xiph.org/releases/ao/${pkgname}-${pkgver}.tar.gz"
        'libao.conf')
sha1sums=('6b1d2c6a2e388e3bb6ebea158d51afef18aacc56'
          'e94ebf7dde7f5fd1503c3cdcbb4e5cc79865f7b8')
conflicts=('libao-pulse')
provides=("libao-pulse=${pkgver}-${pkgrel}")
replaces=('libao-pulse')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--enable-alsa-mmap \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -dm755 -o root -g wheel "${pkgdir}"/etc
	install -m644 -o root -g wheel "${srcdir}"/libao.conf "${pkgdir}"/etc/libao.conf
}
