pkgname=webcamd
pkgver=3.13.0.6
pkgrel=2
pkgdesc="A port of Linux USB webcam and DVB drivers into userspace"
arch=('i386' 'amd64')
url="http://www.selasky.org"
license=('bsdl')
depends=('libc' 'v4l-compat' 'cuse4bsd-kmod' 'dbus')
makedepends=('freebsd-source')
source=(http://www.selasky.org/hans_petter/distfiles/${pkgname}-${pkgver}.tar.bz2
	webcamd.in
	webcamd.conf.in)
sha256sums=('bebcfce207df913469434b51e521b21559a76afafa69b583a0c8c04fe9ec340e'
            '82e281c7115ae3426d7bd494566c93b9edd5d9be8914d0177b590470aa0810c3'
            '3151795d3b3df6df848ec24f410b7e6751df982e0302fc5009544acdbc5159bc')
install=${pkgname}.install

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	unset LDFLAGS

	make -C tools/linux_make

	make PREFIX=/usr PTHREAD_LIBS="-pthread" HAVE_HAL=YES \
		configure -C ${srcdir}/${pkgname}-${pkgver}

	cd patches; /bin/sh ./do_patch.sh

	cd ../ && make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -dm755 ${pkgdir}/usr/sbin
	install -dm755 ${pkgdir}/{etc/rc.d,etc/devd}
	make -j1 "DESTDIR=${pkgdir}" PREFIX=/usr KMODDIR=/usr/modules \
		MANDIR=/usr/share/man/man install

	install -m755 ${srcdir}/webcamd.in ${pkgdir}/etc/rc.d/webcamd
	install -m 755 ${srcdir}/webcamd.conf.in ${pkgdir}/etc/devd/webcamd.conf
}
