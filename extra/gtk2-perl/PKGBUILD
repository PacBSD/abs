# $Id: PKGBUILD 183485 2013-04-21 22:21:05Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=gtk2-perl
pkgver=1.247
pkgrel=6
pkgdesc="Perl bindings for GTK+ 2.x"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends' 'nettle')
depends=('gtk2' 'pango-perl')
checkdepends=('ttf-dejavu' 'xorg-server-xvfb')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Gtk2-${pkgver}.tar.gz)
md5sums=('b65b0293cce3ac3786c37a7fa508e506')

build() {
  cd "${srcdir}/Gtk2-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  # Disable failing test. Doesn't work for several versions, might be related to Xvfb too
  sed -e 's/gnome-foot.png/gnome-foot2.png/' -i t/GdkPixbufLoader.t

  gmake -j1
}

check() {
  cd "${srcdir}/Gtk2-${pkgver}"
  LD_PRELOAD="" Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
  LD_PRELOAD="" DISPLAY=:99 gmake test
  kill $!
}

package() {
  cd "${srcdir}/Gtk2-${pkgver}"
  gmake install DESTDIR="${pkgdir}"
}
