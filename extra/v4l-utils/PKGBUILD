# $Id: PKGBUILD 187074 2013-06-03 11:16:59Z allan $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=v4l-utils
pkgver=1.6.3
pkgrel=5
pkgdesc="Userspace tools and conversion library for Video 4 Linux"
arch=('i386' 'amd64')
url="http://freshmeat.net/projects/libv4l"
license=('LGPL')
provides=("libv4l=$pkgver")
replaces=('libv4l')
conflicts=('libv4l')
backup=(etc/rc_maps.cfg)
depends=('libc'  'libjpeg-turbo' 'argp' 'gcc-libs')
makedepends=('v4l-compat')
source=(http://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.bz2
        http://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.bz2.asc)
sha256sums=('164abf5c1befcd27e8e6ef824a82d4015bdfb5d99ae82daa00e77d895ff9864c'
            'SKIP')

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}/"


  sed -i '' -e 's|$$libdir/pkgconfig|${PREFIX}/libdata/pkgconfig|g' \
    configure

  export  LDFLAGS="-L/usr/lib -largp"

  find . -type f \( -iname "*.h" ! -iname "input.h" ! -iname "videodev2.h" \) | \
    xargs -n 10 sed -i '' \
      -e 's|linux/types.h|sys/types.h|g; s|__user||g' \
      -e 's|__u8|uint8_t|g; s|__s8|int8_t|g' \
      -e 's|__u16|uint16_t|g; s|__s16|int16_t|g' \
      -e 's|__u32|uint32_t|g; s|__s32|int32_t|g' \
      -e 's|__u64|uint64_t|g; s|__s64|int64_t|g'


}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-qv4l2 \
    --without-libudev

  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  MAKEFLAGS="-j1" gmake install DESTDIR="${pkgdir}/"

  #remove udev rules
  rm -r ${pkgdir}/lib
}

