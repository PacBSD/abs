# port: devel/argp-standalone

pkgname=argp
pkgver=1.3
pkgrel=5
pkgdesc="Standalone version of arguments parsing functions from GLIBC"
arch=('i386' 'amd64')
url="http://www.lysator.liu.se/~nisse/misc/"
license=('LGPL')
patchdir=("${pkgname}-standalone-${pkgver}")
source=(http://www.lysator.liu.se/~nisse/misc/${pkgname}-standalone-${pkgver}.tar.gz
	ltmain.sh)
md5sums=('720704bac078d067111b32444e24ba69'
         '5d6f3068219765b2367a429f7068b283')

prepare(){
  cd "${srcdir}/${pkgname}-standalone-${pkgver}"

  cp ${srcdir}/ltmain.sh "${srcdir}/${pkgname}-standalone-${pkgver}/"

  aclocal
  autoconf 
  automake -c -a 
  libtoolize --automake
}

build() {
  cd "${srcdir}/${pkgname}-standalone-${pkgver}"
  export CFLAGS="${CFLAGS} -std=gnu89"
  ./configure
  gmake
}

package() {
 cd "${srcdir}/${pkgname}-standalone-${pkgver}"

 install -dm755 "${pkgdir}"/usr/{lib,include}
 install -m644 argp.h "${pkgdir}"/usr/include/
 install -m755 .libs/libargp.la "${pkgdir}"/usr/lib/
 install -m755 .libs/libargp.a "${pkgdir}"/usr/lib/
 install -m755 .libs/libargp.so.0 "${pkgdir}"/usr/lib/
 cd "${pkgdir}"/usr/lib/
 ln -sh libargp.so.0 libargp.so
}
