#$Id: PKGBUILD 197972 2013-10-30 12:00:43Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgbase=gconfmm
pkgname=('gconfmm' 'gconfmm-docs')
pkgver=2.28.3
pkgrel=3
arch=('i386' 'amd64')
url="http://gtkmm.sourceforge.net/"
license=('LGPL')
makedepends=('pkg-config' 'gtkmm' 'glibmm-docs' 'gconf' 'glibmm')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.xz)
sha256sums=('d7bd2d29c1a87b85329547fb29a0eca52d944e60699982152775002e24c09228')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var MAKE=gmake
  gmake
}

package_gconfmm() {
  pkgdesc="C++ bindings for GConf"
  depends=('gconf>' 'glibmm')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  gmake DESTDIR="${pkgdir}" install
}

package_gconfmm-docs() {
  pkgdesc="Developer documentation for gconfmm"

  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  gmake DESTDIR="${pkgdir}" install
}
