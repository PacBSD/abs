# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libxml2
pkgver=2.9.2
pkgrel=11
pkgdesc="XML parsing library, version 2"
arch=('i386' 'amd64')
license=('custom')
depends=('pkgconf')
url="http://www.xmlsoft.org/"
source=(ftp://ftp.xmlsoft.org/${pkgname}/${pkgname}-${pkgver}.tar.gz
        http://www.w3.org/XML/Test/xmlts20080205.tar.gz)
md5sums=('9e6a9aca9d155737868b3dc5fd82f788'
         'b255be9a1c7f7021e52448e4ec8d7a0d')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for d in . doc doc/devhelp doc/examples
  do
    sed -i '' -e '/^install-data-am:/ s|install-data-local||' \
      ${d}/Makefile.in
  done

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure  --prefix=/usr \
                --with-html-dir=/usr/share/doc \
		--with-iconv=/usr \
		--with-html-subdir=${pkgname} \
		--with-lzma=/usr \
		--without-python \
		--with-threads

  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644  COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
