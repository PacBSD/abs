# $Id: PKGBUILD 155385 2012-04-03 01:23:12Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=intltool
pkgver=0.50.2
pkgrel=3
pkgdesc="The internationalization tool collection"
arch=('any')
url="https://edge.launchpad.net/intltool"
license=('GPL')
depends=('perl-xml-parser')
source=(http://edge.launchpad.net/intltool/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz{,.asc})
sha1sums=('7fddbd8e1bf94adbf1bc947cbf3b8ddc2453f8ad'
          '85c71e9651df41fc330c5f171fdfe1bf11e44f65')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  install    -m644 doc/I18N-HOWTO "${pkgdir}/usr/share/doc/${pkgname}/I18N-HOWTO"
}
