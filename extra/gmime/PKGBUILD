# $Id: PKGBUILD 197999 2013-10-30 12:10:54Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime
pkgver=2.6.20
pkgrel=2
pkgdesc="Core mime parsing library"
arch=('i386' 'amd64')
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2' 'gpgme' 'zlib')
makedepends=('gtk-sharp-2')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('e0a170fb264c2ae4cecd852f4e7aaddb8d58e8f3f0b569ce2d2a4704f55bdf65')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="$srcdir/weird"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-smime \
      --disable-static
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
