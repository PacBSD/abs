# $Id$
# Maintainer: Eric B'elanger <eric@archlinux.org>

pkgname=xchat
pkgver=2.8.8
pkgrel=18
pkgdesc="A GTK+ based IRC client"
arch=('i386' 'amd64')
url="http://www.xchat.org/"
license=('GPL')
depends=('gtk2' 'dbus-glib' 'libnotify' 'hicolor-icon-theme')
makedepends=('tcl' 'perl' 'python2' 'librsvg')
optdepends=('enchant: for spell checking support' 'tcl: for tcl plugin'
            'python2: for python plugin')
install=xchat.install
source=(http://www.xchat.org/files/source/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        http://xchat.org/files/icons/xchat-svg.tar.bz2
        xchat-2.8.8-libnotify07.patch
        xchat-2.8.8-link-against-libnotify.patch
        xchat-2.8.8-glib-2.31.patch
	patch-configure
	patch-socks
	patch-src_common_xchat.h
	patch-version-script)
sha1sums=('e12305da42d1aacc26c2ca25e239f393d4dd3532'
          '0d366346cc11e0efb57fc2648fe423c94a3469bd'
          'a053fba4e1911d1ee6a8248fe19e344797920fe3'
          '70c3cc29fc55ff35f701ef8ac23078b6e3761ce1'
          '7f794e680593bf8e0928a6bcb95c8281522c0655'
          '2b5adcb6f68ff8868f9355169cb4845f0f1990c4'
          '91aa345073a6ebcb93d0224665651ca5138cffd3'
          '0a209f2b95e43edfb2fe3a4c80543415e8ca51fb'
          '574d0e2f12dd6558f7ff55d893947272e78ce46f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's/GDK_HAND1/GDK_HAND2/' src/fe-gtk/xtext.c
#  patch -p1 -i "${srcdir}/xchat-2.8.8-libnotify07.patch"
#  patch -p1 -i "${srcdir}/xchat-2.8.8-link-against-libnotify.patch"
  patch -p1 -i "${srcdir}/xchat-2.8.8-glib-2.31.patch"
  patch -p0 -i "${srcdir}/patch-configure"
  patch -p0 -i "${srcdir}/patch-socks"
  patch -p0 -i "${srcdir}/patch-src_common_xchat.h"
  patch -p0 -i "${srcdir}/patch-version-script"

  LIBS+="-lgmodule-2.0" ./configure --prefix=/usr --sysconfdir=/etc --datadir=/usr/share \
    --enable-openssl --enable-dbus --disable-textfe --enable-ipv6 --enable-spell=static
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  for i in 24 32 48 64 128 256 ; do
    install -d "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"
    rsvg-convert -w $i -h $i -o "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/xchat.png" ../xchat-cloned.svg
  done
  install -dm755 "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
  install -m644 ../xchat-cloned.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
  install -dm755 "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
  install -m644 ../xchat-used.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

  #These files are owned / installed by gettext - Removing to prevent conflicts.
  rm -f "${pkgdir}"/usr/lib/charset.alias
  rm -f "${pkgdir}"/usr/share/locale/locale.alias 
}
