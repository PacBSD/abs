# $Id: PKGBUILD 191829 2013-07-31 09:01:10Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell
pkgver=3.12.2
pkgrel=3
pkgdesc="The next generation GNOME Shell"
arch=('i386' 'amd64')
url="http://live.gnome.org/GnomeShell"
license=(GPL2)
depends=(evolution-data-server gcr gjs gnome-menus
         gnome-session gnome-settings-daemon gnome-themes-standard gsettings-desktop-schemas
         libcanberra-pulse libcroco libsecret mutter caribou
         telepathy-logger telepathy-mission-control unzip libgdm)
makedepends=(intltool gtk-doc gnome-control-center)
optdepends=('gnome-control-center: System settings')
options=('libtoolfix' '!emptydirs')
install=gnome-shell.install
groups=(gnome)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz)
sha256sums=('3ae230e8cb7a31e7b782c16ca178af5957858810788e26a6d630b69b3f85ce71')

prepare() {
  cd $pkgname-$pkgver

  sed -i '' -e 's|\-DG_DISABLE_DEPRECATED||g' \
		src/Makefile.in \
		browser-plugin/Makefile.in 

  sed -i '' -e 's|libnm-glib libnm-util ||g' \
		configure
}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-shell \
      --localstatedir=/var --disable-static \
      --enable-compile-warnings=no \
      --with-ca-certificates=/usr/share/certs/ca-root-nss.crt

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}
