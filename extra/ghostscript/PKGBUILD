pkgname=ghostscript
pkgver=9.06
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
arch=('i386' 'amd64')
license=('AGPL' 'custom')
depends=('libxt' 'libcups' 'fontconfig' 'jasper' 'libpng>=1.5.7' 'libjpeg' 'libtiff>=4.0.0' 'lcms2' 'dbus' 'libpaper' 'libiconv')
url="http://www.ghostscript.com/"
source=("http://downloads.sourceforge.net/project/ghostscript/GPL%20Ghostscript/${pkgver}/ghostscript-${pkgver}.tar.bz2")
# http://downloads.ghostscript.com/public/SHA1SUMS
sha1sums=('4c1c2b4cddd16d86b21f36ad4fc15f6100162238')


prepare() {
  cd ghostscript-${pkgver}

  # force it to use system-libs
  # rm -rf jpeg libpng zlib jasper expat tiff lcms freetype 
  rm -rf jpeg libpng zlib jasper expat tiff lcms lcms2 freetype cups/libs # jbig2dec is in community

  autoreconf

}

build() {
  cd ghostscript-${pkgver}

  ./configure --prefix=/usr \
    --enable-dynamic \
    --with-ijs \
    --with-jbig2dec \
    --without-omni \
    --with-x \
    --with-drivers=ALL \
    --with-fontpath=/usr/share/fonts/Type1:/usr/share/fonts \
    --enable-fontconfig \
    --enable-freetype \
    --enable-openjpeg \
    --without-luratech \
    --with-system-libtiff \
    --with-libpaper \
    --disable-compile-inits \ #--help # needed for linking with system-zlib L

  export CFLAGS_STANDARD="${CFLAGS}"
  export SOC_LOADER="dxmainc.c"
  LDFLAGS+=" -lpthread"
  gmake

  cd ijs
  gsed -i "s:AM_PROG_CC_STDC:AC_PROG_CC:g" configure.ac
  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --disable-static
  gmake
}

package() {
  cd ${srcdir}/ghostscript-${pkgver}
  
  gmake DESTDIR=${pkgdir} \
  cups_serverroot=${pkgdir}/etc/cups \
  cups_serverbin=${pkgdir}/usr/lib/cups install install-so

  # install missing doc files # http://bugs.archlinux.org/task/18023
  install -m 644 "${srcdir}"/ghostscript-${pkgver}/doc/{Ps2ps2.htm,gs-vms.hlp,gsdoc.el,pscet_status.txt} "${pkgdir}"/usr/share/ghostscript/$pkgver/doc/
  
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove unwanted localized man-pages
  rm -rf "$pkgdir"/usr/share/man/[^man1]*

  # install IJS
  cd ijs
  gmake DESTDIR="${pkgdir}" install

  # remove filters that are now maintained in cups-filters as upstream home
  rm -rf "$pkgdir"/usr/lib/cups/filter/{gstopxl,gstoraster}

}
