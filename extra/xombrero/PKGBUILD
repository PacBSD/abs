# Maintainer: Kyle Keen <keenerd@gmail.com>

# aur-breaking split pkgbuild stuff commented out
#pkgbase=xombrero
pkgname=xombrero
pkgver=1.6.3
pkgrel=3
pkgdesc="A minimalists web browser, vi-like while maintaining traditional web browser behavior."
arch=('i386' 'amd64')
url="https://opensource.conformal.com/wiki/xombrero"
_watch="https://opensource.conformal.com/snapshots/xombrero/"
license=('custom:ISC')
depends=('webkitgtk3' 'libsoup' 'pango' 'gtk3' 'glib2' 'gnutls' 'gdk-pixbuf2' 
         'libgcrypt' 'freetype2' 'fontconfig' 'cairo' 'atk')
makedepends=('webkitgtk3' 'desktop-file-utils')
optdepends=('mpv: playflash.sh')
install=$pkgname.install
replaces=('xxxterm')
conflicts=('xxxterm')
options=('!emptydirs' '!strip')
source=(http://opensource.conformal.com/snapshots/$pkgname/$pkgname-$pkgver.tgz \
        LICENSE
	xombrero.in)
md5sums=('11248a0f40542265022e8dfe692644da'
         'f3eeb6e8b70a3dcccb8ee57daf584c9e'
         '06d73adea595138bc10e30395b043531')

prepare() {
   cd "$srcdir/$pkgbase-$pkgver"

   unset LDFLAGS
   sed -i '' -e 's|/usr/local/bin/mplayer|/usr/bin/mpv|g' playflash.sh
}

build() {
    cd "$srcdir/$pkgbase-$pkgver"

    # no spam
    gsed -i 's|https://www.cyphertite.com|https://google.com|g' xombrero.{c,conf,h}

    # relocations
    gsed -i 's|/etc/ssl/cert.pem|/usr/share/certs/ca-root-nss.crt|g' xombrero.conf

    find . -name Makefile | xargs sed -i '' -e 's|${PREFIX}/man|${PREFIX}/share/man|g' \
	-e 's|$(PREFIX)/man|$(PREFIX)/share/man|g'

    gsed -i 's|/usr/local/share/|/usr/share/|g' {unix.c,xombrero.conf,xombrero.h}
    cd ..

    mkdir -p build-gtk3
    cp -R "$srcdir/$pkgname-$pkgver" build-gtk3

    cd "$srcdir/build-gtk3/$pkgname-$pkgver/"
    make -j1 PREFIX="/usr" -C freebsd GTK_VERSION=gtk3
}

package() {
    unset LDFLAGS

    cd "$srcdir/build-gtk3/$pkgbase-$pkgver"

    mkdir -p "$pkgdir/usr/share/"{xombrero,applications}  # buggy makefile
    make PREFIX="/usr" DESTDIR="$pkgdir" install -C freebsd GTK_VERSION=gtk3

    install -dm755 "$pkgdir/etc/skel"
    install -dm755 "$pkgdir/usr/share/applications"
    install -dm755 "$pkgdir/usr/bin"
    install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
    install -dm755 "$pkgdir/usr/share/examples"
    install -m755 playflash.sh "$pkgdir/usr/share/examples/"
    install -m755 xombrero.conf "$pkgdir/usr/share/examples/"

    mv "$pkgdir/usr/bin/xombrero" "$pkgdir/usr/bin/xombrero-bin"

    install -m755 "${srcdir}/xombrero.in" "$pkgdir/usr/bin/xombrero"
    chmod +x "$pkgdir/usr/bin/xombrero"

    install -m644 xombrero.conf     "$pkgdir/etc/skel/.xombrero.conf"
    install -m644 xombrero.desktop  "$pkgdir/usr/share/applications/xombrero.desktop"
    install -m755 config-checker.pl "$pkgdir/usr/bin/config-checker.pl"
    install -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
