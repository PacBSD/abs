# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=aalib
pkgver=1.4rc5
pkgrel=13
pkgdesc="A portable ASCII art graphic library"
arch=('i386' 'amd64')
url="http://aa-project.sourceforge.net/aalib/"
license=('LGPL')
depends=('libx11')
makedepends=('libxt')
options=('!libtool')
install=aalib.install
source=(http://downloads.sourceforge.net/sourceforge/aa-project/${pkgname}-${pkgver}.tar.gz
    patch-ag patch-ah patch-ai)
sha1sums=('a23269e950a249d2ef93625837cace45ddbce03b'
          '6f21abf268b0ba9fdb46a05470d6eb1f6fc4855f'
          '076f179c4891f14e18d286df1a961060afe0c0f4'
          '24fc751d78e0166605e82123ceb7dc7cf59ba28d')

build() {
  cd "${srcdir}/${pkgname}-1.4.0"
  for i in patch-{ag,ah,ai}; do
    patch -p0 -i "${srcdir}/$i"
  done

  # this was aclocal-fixes.patch - we and ports use this sed now...
  sed -i '' -e 's|AC_DEFUN(AM_PATH_AALIB|AC_DEFUN([AM_PATH_AALIB]|g' aalib.m4

  sed -i '' \
      -e 's|\${CONFIG_SHELL-/bin/sh} \$ac_aux_dir|$ac_aux_dir|' \
      -e 's|MAKEINFO=makeinfo|MAKEINFO="makeinfo --no-split"|g' \
      configure

  find . \( -name '*.[ch]' \! -name acconfig.h \) | xargs sed -i '' -e 's|<malloc.h>|<stdlib.h>|g'

  rm -f config.cache
  export CFLAGS="${CFLAGS} -fPIC"

  unset LDFLAGS
  export LDFLAGS="${LDFLAGS//--as-needed/}"
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
              --with-x \
              --without-gpm
  make
}

package() {
  cd "${srcdir}/${pkgname}-1.4.0"
  make DESTDIR="${pkgdir}" install
}
