# $Id: PKGBUILD 173197 2012-12-12 17:29:21Z heftig $

pkgname=htop
pkgver=1.0.3
pkgrel=2
pkgdesc="Interactive process viewer"
arch=('i686' 'amd64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses' 'libexecinfo')
makedepends=('python2')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
options=('!emptydirs' 'libtoolfix')
install=htop
source=("http://hisham.hm/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('e768b9b55c033d9c1dffda72db3a6ac7')

build() {
  cd "$pkgname-$pkgver"

  gsed -i'' 's|ncursesw/curses.h|curses.h|' RichString.[ch]
  gsed -i'' 's|ncursesw/curses.h|curses.h|' configure
  gsed -i'' 's|python|python2|' scripts/MakeHeader.py

  ./configure --prefix=/usr --with-proc=/compat/linux/proc --enable-unicode

  make LDFLAGS="$LDFLAGS -lexecinfo -lkvm"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
