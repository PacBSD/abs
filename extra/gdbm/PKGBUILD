# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=gdbm
pkgver=1.10
pkgrel=4
pkgdesc="GNU database library"
url="http://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL')
arch=('i686' 'x86_64')
source=(ftp://ftp.gnu.org/gnu/gdbm/${pkgname}-${pkgver}.tar.gz
        gdbm-1.10-zeroheaders.patch)
options=('!makeflags')
install=gdbm.install
md5sums=('88770493c2559dc80b561293e39d3570'
         'ac255b10452005237836cd2d3a470733')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Prevent gdbm from storing uninitialized memory content
  # to database files. This patch improves security, as the
  # uninitialized memory might contain sensitive informations
  # from other applications.
  # https://bugzilla.redhat.com/show_bug.cgi?id=4457
  # http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=208927
  patch -Np1 -i ../gdbm-1.10-zeroheaders.patch

  sed -i '' -e 's|LIBS = @LIBS@ -lc|LIBS = @LIBS@|' \
            -e 's|BINOWN = bin|BINOWN = root|' \
            -e 's|BINGRP = bin|BINGRP = wheel|' \
            Makefile.in

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --enable-libgdbm-compat

  make prefix=/usr
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}"/usr \
       manprefix="${pkgdir}"/usr/share/man \
       man3dir="${pkgdir}"/usr/share/man/man3 \
       infodir="${pkgdir}"/usr/share/info \
       install
  
  # create symlinks for compatibility
  install -dm755 "${pkgdir}"/usr/include/gdbm

  mv ${pkgdir}/usr/include/gdbm.h "${pkgdir}"/usr/include/gdbm/gdbm.h
  mv ${pkgdir}/usr/include/ndbm.h "${pkgdir}"/usr/include/gdbm/ndbm.h
  sed -i '' -e 's|<gdbm.h>|<gdbm/gdbm.h>|g' "${pkgdir}"/usr/include/gdbm/{g,n}dbm.h
  ln -sf ../dbm.h  "${pkgdir}"/usr/include/gdbm/dbm.h
}
