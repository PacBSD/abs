# $Id: PKGBUILD 178949 2013-03-01 10:19:23Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=automoc4
pkgver=0.9.88
pkgrel=15
pkgdesc="Automatic moc for Qt4"
arch=('i386' 'amd64')
url='http://www.kde.org/'
license=('custom')
depends=('qt4')
makedepends=('pkgconf' 'cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'license.txt')
md5sums=('91bf517cb940109180ecd07bc90c69ec'
         '9014edbb7e2f02fe90b2db8707487bce')

build() {
	mkdir build
	cd build
	LDFLAGS="${LDFLAGS/--as-needed/--no-as-needed}"
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
    cd build
	make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 "${srcdir}/license.txt" \
      "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
