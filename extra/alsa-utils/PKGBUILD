# $Id: PKGBUILD 191710 2013-07-29 10:16:55Z tpowa $
# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=alsa-utils
pkgver=1.0.29
pkgrel=4
pkgdesc="An alternative implementation of Linux sound support"
arch=('amd64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.24" 'pciutils' 'psmisc' 'libsamplerate' 'ncurses')
license=('GPL')
makedepends=('xmlto' 'docbook-xsl' 'getopt')
source=(ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2)
md5sums=('6b289bf874c4c9a63f4b3973093dd404')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  sed -i ''  -e '/LIBS/s/-ldl//g; /LIBRT/s/-lrt//g;' configure

}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Use ncurses in usr/local
  CPPFLAGS+=" -I/usr/local/include -I${startdir}/files"
  export LDFLAGS="${LDFLAGS} -pthread"
  ./configure --prefix=/usr --disable-alsaconf --sbindir=/usr/bin \
    --with-udev-rules-dir=/usr/lib/udev/rules.d --disable-nls

  gmake
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}
