# $Id: PKGBUILD 47483 2011-05-23 15:54:16Z tdziedzic $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=perl-crypt-openssl-bignum
pkgver=0.06
pkgrel=3
pkgdesc="OpenSSL's multiprecision integer arithmetic "
arch=('i386' 'amd64')
url="http://search.cpan.org/dist/Crypt-OpenSSL-Bignum"
depends=('perl')
license=('GPL')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/K/KM/KMX/Crypt-OpenSSL-Bignum-$pkgver.tar.gz")
md5sums=(5673ee17919231c6394382e48f80c582)
sha512sums=(98e18ce2d8db09b2a552de641e403944e9e0f413b835dcf560fb8b552bd442a2753aea2ed7cb1b62cb261f1b6debfe56db82b15bd872bc45e12ff1aed92fa478)
_ddir="Crypt-OpenSSL-Bignum-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;

}
