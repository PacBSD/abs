# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Port: x11/xterm

pkgname=xterm
pkgver=320
pkgrel=3
pkgdesc="X Terminal Emulator"
arch=('i386' 'amd64')
url="http://invisible-island.net/xterm/"
license=('custom')
depends=('libxft' 'libxaw' 'ncurses' 'xorg-luit' 'xbitmaps' 'pcre')
source=(ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz
        LICENSE)
md5sums=('0d7f0e6390d132ae59876b3870e5783d'
         '10ecc3f8ee91e3189863a172f68282d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --libdir=/etc \
      --mandir=/usr/share/man \
      --enable-256-color \
      --enable-wide-chars \
      --enable-luit \
      --with-pcre \
      --with-app-defaults=/usr/share/X11/app-defaults/ \
      --with-x \
      --disable-full-tgetent \
      --disable-imake \
      --enable-ansi-color \
      --enable-88-color \
      --enable-broken-osc \
      --enable-broken-st \
      --enable-load-vt-fonts \
      --enable-i18n \
      --enable-doublechars \
      --enable-warnings \
      --enable-tcap-query \
      --enable-logging \
      --enable-dabbrev \
      --enable-freetype \
      --enable-mini-luit \
      --enable-narrowproto \
      --enable-exec-xterm \
      --with-tty-group=tty
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  chmod 0755 "${pkgdir}/usr/bin/xterm"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m755 -d ${pkgdir}/usr/share/applications
  install -m644 ${srcdir}/${pkgname}-${pkgver}/{xterm,uxterm}.desktop ${pkgdir}/usr/share/applications/
}
