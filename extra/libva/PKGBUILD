# Maintainer : Anthony Donnelly <Amzo@archbsd.net>

pkgname=libva
pkgver=1.6.0
pkgrel=1
pkgdesc="Video Acceleration (VA) API for Linux"
arch=('i686' 'amd64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libgl' 'libdrm' 'libxfixes')
makedepends=('mesa' 'v4l-compat')
options=('libtoolfix')
#optdepends=('libva-vdpau-driver: vdpau back-end for nvidia'
#            'libva-intel-driver: back-end for intel cards')
source=(http://cgit.freedesktop.org/${pkgname}/snapshot/$pkgname-${pkgver}.tar.bz2)
sha1sums=('b0c665337f290242cc5a4e7b72a611e15c649205')

build() {
  cd "$pkgname-$pkgver"

  sed -i '' -e 's/-ldl//' va/Makefile.am

  mkdir -p m4
  autoreconf -v --install

  CPPFLAGS="$CPPFLAGS -isystem/usr/include" \
  LDFLAGS="$LDFLAGS -L/usr/lib" \
  CFLAGS="$CFLAGS -DNDEBUG -DHAVE_LINUX_INTEGER_TYPES" \
  ./configure --prefix=/usr --program-prefix=va

  sed -i ''  's/-lpthread/-pthread/' \
		test/putsurface/Makefile
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
