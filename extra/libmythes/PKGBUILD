# $Id: PKGBUILD 162736 2012-06-29 10:55:24Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libmythes
pkgver=1.2.4
pkgrel=2
epoch=1
pkgdesc="a simple thesaurus"
arch=('i386' 'amd64')
url="http://hunspell.sourceforge.net/ "
license=('custom')
depends=('libc' 'perl' 'gcc-libs')
makedepends=('hunspell')
options=('!libtool')
provides=('mythes')
source=(http://downloads.sourceforge.net/hunspell/${pkgname/lib/}-${pkgver}.tar.gz)
md5sums=('a8c2c5b8f09e7ede322d5c602ff6a4b6')

build() {
  cd ${srcdir}/${pkgname/lib/}-$pkgver
  ./configure --prefix=/usr --disable-static
  gmake
  # run the example program:
  ./example th_en_US_new.idx th_en_US_new.dat checkme.lst
  # run the example program with stemming and morphological generation:
  # e.g. to check mouse, mice, rodents, eats, eaten, ate, eating etc. words
  ./example morph.idx morph.dat morph.lst morph.aff morph.dic
}

package() {
  cd ${srcdir}/${pkgname/lib/}-$pkgver
  make DESTDIR=$pkgdir install
  # license
  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -m644 ${srcdir}/${pkgname/lib/}-$pkgver/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
