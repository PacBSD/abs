# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgbase=poppler
pkgname=('poppler' 'poppler-glib' ) #'poppler-qt4'
pkgver=0.24.0
pkgrel=2
arch=(i686 x86_64)
license=('GPL')
makedepends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg' 'gtk2' 'qt4' 'pkgconf' 'lcms' 'gobject-introspection' 'icu')
options=('!emptydirs')
url="http://poppler.freedesktop.org/"
_testtag=0d2bfd4af4c76a3bac27ccaff793d9129df7b57a
source=(http://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.xz
        http://cgit.freedesktop.org/poppler/test/snapshot/test-${_testtag}.tar.bz2)
md5sums=('891000481f639e996d000d767d953a7f'
         '9dc64c254a31e570507bdd4ad4ba629a')
prepare() {
  ln -sf test-${_testtag} test
  cd ${pkgbase}-${pkgver}

  sed -i '' -e '/AC_PATH_XTRA/d' configure.ac
  sed -i '' -e "s:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:" configure.ac

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	  -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
	  ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-cairo-output \
      --enable-xpdf-headers \
      --enable-libjpeg --enable-zlib \
      --disable-poppler-qt4 \
      --enable-poppler-glib \
      --with-html-dir=/usr/share/doc/poppler \
      --disable-gtk-test

  # for some reason sys/cdefs.h doesn't define it in this poppler version...
  echo '#define __LONG_LONG_SUPPORTED' >> config.h
  gmake
}

check() {
  cd "${srcdir}"
  ln -sf test-${_testtag} test
  cd ${pkgbase}-${pkgver}
  LANG=en_US.UTF8 gmake check ||true # one test failure.
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'cairo' 'fontconfig' 'openjpeg' 'lcms' 'poppler-data')
  conflicts=("poppler-qt3<${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i '' \
      -e 's/^glib_subdir =.*/glib_subdir =/' \
      -e 's/^qt4_subdir =.*/qt4_subdir =/' Makefile
  gmake DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib/pkgconfig/poppler-{glib,qt4}.pc

}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib2>=2.34.3')

  cd "${srcdir}/${pkgbase}-${pkgver}/poppler"
  gmake DESTDIR="${pkgdir}" install-libLTLIBRARIES
  cd "${srcdir}/${pkgbase}-${pkgver}/glib"
  gmake DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 ../poppler-glib.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
  rm -f "${pkgdir}/usr/bin/poppler-glib-demo"
}

#package_poppler-qt4() {
#  pkgdesc="Poppler Qt4 bindings"
#  provides=("poppler-qt=${pkgver}")
#  replaces=('poppler-qt')
#  conflicts=('poppler-qt')
#  depends=("poppler=${pkgver}" 'qt4')

#  cd "${srcdir}/${pkgbase}-${pkgver}/poppler"
#  gmake DESTDIR="${pkgdir}" install-libLTLIBRARIES
#  cd "${srcdir}/${pkgbase}-${pkgver}/qt4"
#  gmake DESTDIR="${pkgdir}" install
#  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
#  install -m644 ../poppler-qt4.pc "${pkgdir}/usr/lib/pkgconfig/"
#  rm -f "${pkgdir}"/usr/lib/libpoppler.*
#}
