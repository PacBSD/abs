pkgname=gnome-doc-utils
pkgver=0.20.10
pkgrel=11
pkgdesc="Documentation utilities for Gnome"
arch=('any')
license=('GPL' 'LGPL')
depends=('libxslt' 'python2' 'docbook-xml' 'rarian' 'python2-libxml2')
makedepends=('intltool' 'gawk')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('cb0639ffa9550b6ddf3b62f3b1add92fb92ab4690d351f2353cffe668be8c4a6')

build() {
  cd "$pkgname-$pkgver"

  unset CONFIG_SITE

  find "$srcdir/$pkgname-$pkgver" -name "configure" -type f | xargs sed -i '' -e \
                                's|-lpthread|-pthread|g' \
				-e 's|DATADIRNAME=lib|DATADIRNAME=share|g'  \
				-e 's|{libdir}/locale|{prefix}/share/locale|g'

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc --mandir=/usr/share/man \
      --localstatedir=/var --disable-scrollkeeper
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
  sed -i '' -e "s|\/bin\/bash|\/usr\/bin\/bash|g" ${pkgdir}/usr/bin/gnome-doc-tool
}
