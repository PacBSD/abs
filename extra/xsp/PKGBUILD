# $Id: PKGBUILD 188154 2013-06-10 16:36:39Z daniel $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=xsp
pkgver=3.0.11
pkgrel=1
pkgdesc="A simple webserver based on mono - provides ASP.NET support"
arch=('i386' 'amd64')
license=('custom')
url="http://www.go-mono.com"
depends=('mono' 'sqlite')
options=(!makeflags)
install=${pkgname}.install
source=(https://github.com/mono/xsp/archive/${pkgver}.tar.gz
        ${pkgname}.webapp)
md5sums=('6a694d5e87ebe57e532ec80a16b6966d'
         'c917c07f68b945691506c29750db482f')

build() {
 # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR=${srcdir}/src/weird
  mkdir -p "${MONO_SHARED_DIR}"
 # import pathes
 # build
  cd ${srcdir}/${pkgname}-${pkgver}
  #sed -i 's|/bin/sh|\0\nexport MONO_PATH=${MONO_PATH}:/opt/mono/lib/mono/1.0|' scripts/script.in
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  gmake
 # tweak the xsp shellscript to grab system dll's
  for script in scripts/*2; do
    gsed -i 's|/usr/lib/mono/1.0|/usr/lib/mono/2.0|' $script
  done
   #destdir related bugfixes
  gsed -i 's|mkdir \$(datadir)|mkdir $(DESTDIR)$(datadir)|' test/2.0/treeview/Makefile
  gsed -i 's|gif \$(datadir)|gif $(DESTDIR)$(datadir)|' test/2.0/treeview/Makefile
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir}/ install
 # move test files from share to arch' default html home
  mkdir -p ${pkgdir}/srv/http/html
  mv ${pkgdir}/usr/lib/xsp/test ${pkgdir}/srv/http/html/xsp
  rm -rf ${pkgdir}/usr/share

 # install a xsp configuration home
  install -dm755 ${pkgdir}/etc/${pkgname}
  install -m644 ${srcdir}/${pkgname}.webapp \
    ${pkgdir}/etc/${pkgname}/${pkgname}.webapp
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
