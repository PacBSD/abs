# $Id$
# Maintainer: tobias <tobias@justdreams.de>
# Modified by: eliott <eliott@cactuswax.net>
# Original by Benjamin Andresen, klapmuetz [at] gmail [dot] com

pkgname=fcgi
pkgver=2.4.0
pkgrel=18
pkgdesc="FASTCgi(fcgi) islanguage independent, high performant extension to CGI"
arch=('i386' 'amd64')
license=('custom')
options=('!libtool' '!makeflags')
url="http://www.fastcgi.com"
source=("http://www.fastcgi.com/dist/${pkgname}-${pkgver}.tar.gz"
        'gcc44-fix-include.patch'
        'configure-der.patch')
md5sums=('d15060a813b91383a9f3c66faf84867e'
         'a8028462163755f3ce457a5c641f237b'
         'f5bccddcba0303c8632d90860e2c0fdf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/gcc44-fix-include.patch"
  patch -p1 -i "${srcdir}/configure-der.patch"
  libtoolize --force
  aclocal
  autoconf
  automake --foreign
  ./configure --prefix=/usr/local/
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/local/share/licenses/${pkgname}/"
  install -m644 LICENSE.TERMS "${pkgdir}/usr/local/share/licenses/${pkgname}/LICENSE"
}

