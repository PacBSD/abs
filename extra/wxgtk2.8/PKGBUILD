# $Id: PKGBUILD 171304 2012-11-17 03:08:29Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk2.8
pkgver=2.8.12.1
pkgrel=7
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'amd64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'libgl' 'libxxf86vm' 'libsm' 'sdl' 'gnu-libiconv')
makedepends=('gstreamer0.10-base-plugins' 'gconf' 'mesa' 'glu' 'libxt')
patchdir=(wxPython-src-${pkgver})
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2 wxGTK-collision.patch)
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5'
          '575fb4688051c246777988332f55703df403d6b9')

prepare() {
  cd wx*-${pkgver}
  patch -p1 -i ../wxGTK-collision.patch
}

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"

  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx  --disable-optimize --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-sdl --disable-precomp-headers --with-libiconv-prefix=/usr/local
  gmake
  gmake -C locale allmo
  cd contrib/src
  gmake
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}"
#  cd "${srcdir}/wxGTK-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  cd contrib/src
  gmake DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
