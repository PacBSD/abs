# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgname=libgksu
pkgver=2.0.12
pkgrel=8
pkgdesc="gksu authorization library"
arch=('i386' 'amd64')
url="http://www.nongnu.org/gksu/index.html"
license=(GPL)
depends=('gconf' 'gtk2' 'libgnome-keyring' 'libgtop' 'startup-notification')
makedepends=('intltool' 'gtk-doc')
options=( '!emptydirs')
install=libgksu.install
source=(http://people.debian.org/~kov/gksu/${pkgname}-${pkgver}.tar.gz
        libgksu-2.0.0-fbsd.patch
        libgksu-2.0.12-automake-1.11.2.patch
        libgksu-2.0.12-fix-make-3.82.patch
	libgksu-2.0.12-notests.patch
        libgksu-2.0.12-revert-forkpty.patch
        libgksu-2.0.7-libs.patch
        libgksu-2.0.7-polinguas.patch
        patch-libgksu.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #patch -p0 -i "${srcdir}/patch-libgksu.patch"
  patch -Np1 -i "${srcdir}/libgksu-2.0.0-fbsd.patch"
  patch -Np1 -i "${srcdir}/libgksu-2.0.7-libs.patch"
  patch -Np1 -i "${srcdir}/libgksu-2.0.7-polinguas.patch"
  patch -Np1 -i "${srcdir}/libgksu-2.0.12-revert-forkpty.patch"
  patch -Np0 -i "${srcdir}/libgksu-2.0.12-fix-make-3.82.patch"
  patch -Np1 -i "${srcdir}/libgksu-2.0.12-notests.patch"
  patch -Np1 -i "${srcdir}/libgksu-2.0.12-automake-1.11.2.patch"
  patch -p0 -i "${srcdir}/patch-libgksu.patch"

  touch NEWS README

  intltoolize --force --copy --automake
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --disable-schemas-install \
      LIBS="-lintl"
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  rm -f ${pkgdir}/usr/lib/*.a
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgksu ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

md5sums=('c7154c8806f791c10e7626ff123049d3'
         '063a2b45d8e7cbba898d1db413242da0'
         'c798a97865801654ac3889ac4cf0a9a0'
         'afeaf5caab03d793258d62d60a9bf0f0'
         'af5728c350f99f597c77029cdf34bd3a'
         'aebbe57e5286c654e27cf714cf3b704a'
         '58d3a4a9d2ac741951720043ea3f7b5f'
         '0b5c3d5d9b32cb3e65d9f0bfbcb11a76'
         '6b12afe414caea69d64bcf2477ae03be')
