# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=qt-assistant-compat
pkgver=4.6.3
pkgrel=4
pkgdesc="compat version of Qt Assistant"
url="http://qt-project.org/"
arch=('i386' 'amd64')
license=('GPL3' 'LGPL')
depends=('qt4')
source=('ftp://ftp.qt.nokia.com/qt/source/qt-assistant-qassistantclient-library-compat-src-4.6.3.tar.gz'
        'debian_patches_01_build_system.diff')
md5sums=('a20148e0488d5c12ab35ccc107dcc64d'
         '1b55290dc9fab0c035797e9301d2fa61')

# separate package function does not really work here due to broken build files
build() {
	cd ${srcdir}/qt-assistant-qassistantclient-library-compat-version-${pkgver}

	patch -Np1 -i ${srcdir}/debian_patches_01_build_system.diff

	cd lib; if [ -e Makefile ]; then gmake distclean; fi
	rm -f translations/assistant_adp_*.qm

	qmake-qt4 CONFIG+=create_prl
	gmake
	cd  ../translations; lrelease-qt4 assistant_adp_*.ts
	cd ..
	qmake-qt4 CONFIG+=create_prl
	gmake
}

package() {
	cd ${srcdir}/qt-assistant-qassistantclient-library-compat-version-${pkgver}

	gmake install INSTALL_ROOT=${pkgdir}
	cd lib
	gmake install INSTALL_ROOT=${pkgdir}
	cd ..

	# Fix wrong path in prl file
	sed -i '' -e '/^QMAKE_PRL_BUILD_DIR/d' \
		${pkgdir}/usr/lib/libQtAssistantClient.prl

	# Install translations
	install -d ${pkgdir}/usr/share/qt4/translations/
	install -p -m0644 translations/assistant_adp_*.qm \
		${pkgdir}/usr/share/qt4/translations/

	# Install prf file
	install -dm755 ${pkgdir}/usr/share/qt4/mkspecs/features/
	install -p -m0644 features/assistant.prf \
		${pkgdir}/usr/share/qt4/mkspecs/features/assistant.prf
}
