# $Id: PKGBUILD 143014 2011-11-20 23:59:13Z bisson $

# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=gts
pkgver=0.7.6
pkgrel=6
pkgdesc='Provides useful functions to deal with 3D surfaces meshed with interconnected triangles'
url='http://gts.sourceforge.net/'
license=('LGPL')
arch=('i386' 'amd64')
depends=('glib2')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
	patch-ltmain.sh
	patch-src-rounding.h)
sha1sums=('000720bebecf0b153eb28260bd30fbd979dcc040'
          '69591867759af393c915ca394c402c92c7a260b2'
          '68c8889a519742b7e0636f7ef2ef554ee6cfbc4b')
options=('!libtool')

build() {
	cd "${pkgname}-${pkgver}"
        patch -p0 -i ${srcdir}/patch-ltmain.sh
        patch -p0 -i ${srcdir}/patch-src-rounding.h
	./configure --prefix=/usr --disable-static
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
