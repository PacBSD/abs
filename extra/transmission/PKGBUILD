# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>

pkgbase=transmission
pkgname=('transmission-cli' 'transmission-gtk')
pkgver=2.77
pkgrel=7
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
makedepends=('gtk3' 'intltool' 'curl' 'libevent')
source=(http://mirrors.m0k.org/transmission/files/$pkgbase-$pkgver.tar.xz)
md5sums=('60c0e77a1852656215f7102a437b2a3d')

build() {
  cd $pkgbase-$pkgver

  ./configure --enable-cli \
	      --disable-daemon \
	      --disable-mac \
	      --enable-inotify \
	      --prefix=/usr
  gmake

}
package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
  depends=('curl' 'libevent')
  #backup=('etc/conf.d/transmissiond')

  cd $pkgbase-$pkgver

  for dir in daemon cli web utils
  do
      gmake -C "$dir" DESTDIR="$pkgdir" install
  done

  install -dm755 "$pkgdir/usr/share/licenses/transmission-cli/"
  install -m644 COPYING "$pkgdir/usr/share/licenses/transmission-cli/"
}

package_transmission-gtk() {
  pkgdesc="Fast, easy, and free BitTorrent client (GTK+ GUI)"
  depends=('curl' 'libevent' 'gtk3' 'desktop-file-utils' 'hicolor-icon-theme')
  optdepends=('notification-daemon: Desktop notification support'
  	      'transmission-cli: daemon and web support')

  cd $pkgbase-$pkgver

  gmake -C gtk DESTDIR="$pkgdir" install
  gmake -C po DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/transmission-gtk/"
  install -m644 COPYING "$pkgdir/usr/share/licenses/transmission-gtk/"
}

