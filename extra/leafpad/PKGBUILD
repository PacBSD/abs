# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=leafpad
pkgver=0.8.18.1
pkgrel=6
pkgdesc="A notepad clone for GTK+ 2.0"
arch=('i386' 'amd64')
url="http://tarot.freeshell.org/leafpad/"
license=('GPL')
depends=('gtk2' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('gio-fam')
install=leafpad.install
groups=('lxde')
source=(http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('254a72fc67505e3aa52884c729cd7b97')

build() {
  export CPPFLAGS="${CPPFLAGS} -pthread"

  cd "${srcdir}/${pkgname}-${pkgver}"
  chmod a+x install-sh
  ./configure --prefix=/usr --enable-chooser
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  echo 'StartupNotify=true' >> "${pkgdir}/usr/share/applications/leafpad.desktop"
}
