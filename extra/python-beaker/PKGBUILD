# $Id: PKGBUILD 173260 2012-12-16 23:56:23Z eric $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Massimiliano Torromeo < massimiliano DOT torromeo AT gmail DOT com >

pkgname=('python-beaker' 'python2-beaker')
pkgver=1.6.4
pkgrel=1
arch=('any')
license=('custom')
pkgdesc="Caching and sessions WSGI middleware for use with web applications and stand-alone Python scripts and applications"
url="http://beaker.groovie.org/"
makedepends=('python-distribute' 'python2-distribute' 'pacman<4.1.0')
source=("http://cheeseshop.python.org/packages/source/B/Beaker/Beaker-${pkgver}.tar.gz")
md5sums=('c2e102870ed4c53104dec48ceadf8e9d')

build() {
	cd "$srcdir"
    cp -r Beaker-${pkgver} python2-Beaker-${pkgver}

    cd "${srcdir}/Beaker-${pkgver}"
    gsed -i "s#/usr/bin/python#/usr/bin/python3#" beaker/crypto/pbkdf2.py
    python3 setup.py build

    cd "${srcdir}/python2-Beaker-${pkgver}"
    gsed -i "s#/usr/bin/python#/usr/bin/python2#" beaker/crypto/pbkdf2.py
    python2 setup.py build
}

package_python-beaker() {
    depends=('python')

    cd "${srcdir}/Beaker-${pkgver}"
    python3 setup.py install --root="${pkgdir}" --optimize=1
    install -dm755 "${pkgdir}/usr/share/licenses/python-beaker"
    install -m644 LICENSE "${pkgdir}/usr/share/licenses/python-beaker/LICENSE"
}

package_python2-beaker() {
    depends=('python2')

    cd "${srcdir}/python2-Beaker-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1
    install -dm755 "${pkgdir}/usr/share/licenses/python2-beaker"
    install -m644 LICENSE "${pkgdir}/usr/share/licenses/python2-beaker/LICENSE"
}
