# $Id: PKGBUILD 198174 2013-10-30 13:15:24Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=libgnomeuimm
pkgver=2.28.0
pkgrel=14
pkgdesc="A C++ wrapper for libgnomeui"
arch=('i386' 'amd64')
url="http://gtkmm.sourceforge.net/"
license=('GPL')
depends=('libgnomeui>=2.24.4' 'libgnomecanvasmm>=2.26.0' 'libgnomemm>=2.30.0' 'libglademm>=2.6.7' 'gconfmm>=2.28.2' 'gnome-vfsmm>=2.26.0')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2
	patch-fix-main-declaration)
sha256sums=('6cb46494913f1e5e34b94a0f5b9ff8ef238bb71e3b08d0ef0ab7f4a7c88211d3'
            'fd7aaa69613e176f68d260c73c292569486279671976a0afb42fd4dfd3716664')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p0 -i "${srcdir}/patch-fix-main-declaration"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CXXFLAGS+=" -std=c++11"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static LIBS="-lintl"
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
