# $Id: PKGBUILD 189651 2013-07-04 03:57:29Z eric $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libpwquality
pkgver=1.2.4
pkgrel=1
pkgdesc="Library for password quality checking and generating random passwords"
arch=('i386' 'amd64')
url="https://fedorahosted.org/libpwquality/"
license=('GPL')
options=('!libtool')
depends=('cracklib')
optdepends=('python2: Python bindings')
makedepends=('python2')
source=(https://fedorahosted.org/releases/l/i/libpwquality/$pkgname-$pkgver.tar.bz2)
md5sums=('5c8b1d984a9c184fc62a4d07bb36b922')

build() {
    cd "$pkgname-$pkgver"
    sed -i -e 's/python setup.py/python2 setup.py/' python/Makefile.in

    PYTHON=python2 ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static --disable-pam \
        --with-python-rev=2.7 LIBS="-lintl"
    gmake
}

package() {
    cd "$pkgname-$pkgver"
    gmake DESTDIR="$pkgdir" install
}
