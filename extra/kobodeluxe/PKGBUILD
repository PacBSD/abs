# $Id: PKGBUILD 192417 2013-08-11 02:14:23Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kobodeluxe
pkgver=0.5.1
pkgrel=5
pkgdesc="An enhanced version of Akira Higuchi's game XKobo, an addictive space shoot'em up"
arch=('i386' 'amd64')
url="http://www.olofson.net/kobodl/"
license=('GPL' 'LGPL')
depends=('sdl_image' 'hicolor-icon-theme' 'gtk-update-icon-cache')
install=kobodeluxe.install
source=(http://www.olofson.net/kobodl/download/KoboDeluxe-${pkgver}.tar.gz)
sha1sums=('12abe182ccde52050cd23077092d0424d25c3e75')

prepare() {
  cd KoboDeluxe-${pkgver}
  tar -xf icons.tar.gz
  sed -i ''  -e 's|pipe2|kobopipe2|g' enemies.h \
		enemy.cpp
}

build() {
  cd KoboDeluxe-${pkgver}
  ./configure --prefix=/usr --localstatedir=/var --sharedstatedir=/var/games
  gmake
}

package() {
  cd KoboDeluxe-${pkgver}
  gmake DESTDIR="${pkgdir}" install
 
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 icons/KDE/kobo-deluxe.desktop "${pkgdir}/usr/share/applications/kobo-deluxe.desktop"
  for i in 16 22 32 48 64 128 ; do
    install -dm755 "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/#"
    install -m644 icons/KDE/icons/${i}x${i}/kobodl.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/kobodl.png"
  done

  chown root:games "${pkgdir}/usr/bin/kobodl"
  chmod 2755 "${pkgdir}/usr/bin/kobodl"
  chown root:games "${pkgdir}/var/games"
  chmod 775 "${pkgdir}/var/games"
  chown root:games "${pkgdir}/var/games/kobo-deluxe"
  chmod 775 "${pkgdir}/var/games/kobo-deluxe"
  chown root:games "${pkgdir}/var/games/kobo-deluxe/scores"
  chmod 775 "${pkgdir}/var/games/kobo-deluxe/scores"
}
