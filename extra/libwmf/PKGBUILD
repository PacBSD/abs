# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libwmf
pkgver=0.2.8.4
pkgrel=24
pkgdesc="A library for reading vector images in Microsoft's native Windows Metafile Format (WMF)"
arch=('amd64')
url="http://wvware.sourceforge.net/libwmf.html"
license=('LGPL')
depends=('libpng' 'libx11' 'libjpeg' 'gsfonts')
makedepends=('gtk2' 'libxt')
optdepends=('gdk-pixbuf2: for pixbuf loader')
options=('!libtool' '!docs' '!emptydirs')
install=libwmf.install
source=(http://downloads.sourceforge.net/sourceforge/wvware/${pkgname}-${pkgver}.tar.gz
        libwmf-0.2.8.4-libpng-1.5.patch libwmf-0.2.8.4-useafterfree.patch
        patch-cve-2006-3376 patch-gd_png.c patch-ltmain.sh patch-src__font.c)

sha1sums=('822ab3bd0f5e8f39ad732f2774a8e9f18fc91e89'
          '42aa4c2a82e4e14044c875a7f439baea732a355a'
          'ea6d28880840e86c96f9079bfd591da54dcffa5c'
          '386f78a9366afaaf462a0f7eb7c94f08e1854c04'
          'dd2d2da75dfb19c3f2abbf067fe4cccb0da9a9fc'
          '8a24d289081686afab4f6a2ed39113f897389752'
          '827d6817c7d0f8356b9306ff614c52f99f369217')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/libwmf-0.2.8.4-libpng-1.5.patch"
  patch -p1 -i "${srcdir}/libwmf-0.2.8.4-useafterfree.patch"
  for i in patch-cve-2006-3376 patch-gd_png.c patch-ltmain.sh patch-src__font.c
  do
    patch -p0 -i "${srcdir}/$i"
  done

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	  -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
	  ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

  ./configure --prefix=/usr \
              --with-gsfontdir=/usr/share/fonts/Type1 \
              --with-fontdir=/usr/share/fonts/Type1 \
              --with-png=/usr \
              --with-jpeg=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  #Remove fonts, these are in gsfonts
  rm -rf "${pkgdir}/usr/share/fonts"
  #Remove static GTK loader, can't use it anyways
  rm -f "${pkgdir}"/usr/lib/gtk-2.0/*/loaders/io-wmf.a
}
