# $Id: PKGBUILD 184160 2013-05-03 01:23:26Z eric $
# Maintainer:
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>

pkgbase=python-cssselect
pkgname=('python-cssselect' 'python2-cssselect')
pkgver=0.9.1
pkgrel=4
license=('BSD')
arch=('amd64')
url="http://pypi.python.org/pypi/cssselect"
makedepends=('python2' 'python')
source=("http://pypi.python.org/packages/source/c/cssselect/cssselect-${pkgver}.tar.gz")
md5sums=('c74f45966277dc7a0f768b9b0f3522ac')

prepare() {
  cp -r cssselect-${pkgver} python2-cssselect-${pkgver}
}

package_python-cssselect() {
  pkgdesc="A Python3 library that parses CSS3 Selectors and translates them to XPath 1.0"
  depends=('python')

  cd cssselect-${pkgver}
  python setup.py install --root="${pkgdir}/" --optimize=1

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-cssselect() {
  pkgdesc="A Python2 library that parses CSS3 Selectors and translates them to XPath 1.0"
  depends=('python2')

  cd python2-cssselect-${pkgver}
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # Fix python2 shebang
  sed -i'' -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/lib/python2.7/site-packages/cssselect/tests.py
}
