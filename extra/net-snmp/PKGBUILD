# $Id: PKGBUILD 186390 2013-05-26 09:24:49Z bluewind $
# Maintainer:
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=net-snmp
pkgver=5.7.2
pkgrel=5
pkgdesc="A suite of applications used to implement SNMP v1, SNMP v2c and SNMP v3 using both IPv4 and IPv6"
arch=('i686' 'amd64')
url="http://www.net-snmp.org/"
license=('BSD')
depends=('pciutils')
makedepends=('python2-distribute')
optdepends=('perl-term-readkey: for snmpcheck application'
            'perl-tk: for snmpcheck and tkmib applications'
            'python2: for the python modules')
options=('!libtool' '!emptydirs' '!makeflags')
port_patches=(
	patch-agent__auto_nlist.c
	patch-agent__mibgroup__agent__extend.c
	patch-agent__mibgroup__host__data_access__swrun_kinfo.c
	patch-include__net-snmp__net-snmp-config.h.in
	patch-snmpusm.c
	patch-tcpTable.c
	patch-transform_oids.h
)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        libnl32.patch snmpd.conf snmpd.in snmpd.init-d
	snmptrapd.conf snmptrapd.in snmptrapd.init-d
	${port_patches[@]})
sha1sums=('c493027907f32400648244d81117a126aecd27ee'
          'SKIP'
          '74a9848b95f63378eb1753fc309d2b74de5afb0f'
          '51ebdca93c68ca04b7f402fad4f504422ec2c6cf'
          '00ce6983d60a96c369d4a965be149be68ae06ba2'
          '658c671b0c6ed135d2ec6aa8b6221861db1a5a73'
          '0d296caa01c789455c3cd4c67cd81aca5547681d'
          '29e1dae125561e33dbd3d8b87f62e47c2577e3a7'
          'b59e952065202afefae40def8fe50ae5f32662f3'
          '218c944a7653b935af449c48244aa91522051bff'
          '52feff56e5c52bdf07a632258dbe96e43ed68da2'
          'a6237b5f83e955471eecd3a8a6b4c558de89d162'
          '5050d03d8e5499781c6320cce61c37a64c0a55a6'
          'b8afe9eed239beaf6c2a33817aef1f35f2c60e52'
          '6d328e88239520aa2c0e4b61677fe7ea57571285'
          '1fd95d84654b0cbba6822322f5281407b42f7e5a')
prepare() {
  cd ${pkgname}-${pkgver}
  
    for _patch in ${port_patches[@]}; do
    msg "Patching port patch $_patch"
    patch -p0 -i "${srcdir}/$_patch"
  done

  mkdir "${srcdir}/bin"
  ln -s /usr/bin/gmake "${srcdir}/bin/make"
  export PATH="${srcdir}/bin:$PATH"
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHONPROG=/usr/bin/python2 ./configure --prefix=/usr \
    --sysconfdir=/etc --sbindir=/usr/bin \
    --mandir=/usr/share/man \
    --enable-ucd-snmp-compatibility \
    --enable-ipv6 \
    --with-python-modules \
    --with-default-snmp-version="3" \
    --with-sys-contact="root@localhost" \
    --with-sys-location="Unknown" \
    --with-logfile="/var/log/snmpd.log" \
    --with-mib-modules="host disman/event-mib smux mibII/mta_sendmail mibII/tcpTable ucd-snmp/diskio" \
    --with-persistent-directory="/var/net-snmp" \
    --disable-static --with-ldflags="-lm -lkvm -ldevstat"
  make NETSNMP_DONT_CHECK_VERSION=1
}

package() {
  cd ${pkgname}-${pkgver}
  gsed -i -e "s:install --basedir=\$\$dir:install --basedir=\$\$dir --root=${pkgdir}:" Makefile
  make -j1 DESTDIR="${pkgdir}" INSTALL_PREFIX="${pkgdir}" INSTALLDIRS=vendor install
  rm -f "${pkgdir}"/usr/share/snmp/mibs/BRIDGE-MIB.txt
  
  install -dm755 "${pkgdir}"/etc/{rc.d,init.d,conf.d}
  
  for file in snmpd.in snmptrapd.in; do
    install -m755 "${srcdir}"/$file "${pkgdir}"/etc/rc.d/${file%%.*}
  done
  
  for conf in snmpd.conf snmptrapd.conf; do
    install -m755 "${srcdir}"/$conf "${pkgdir}"/etc/conf.d/$conf
  done

  for file in snmpd.init-d snmptrapd.init-d; do
    install -m755 "${srcdir}"/$file "${pkgdir}"/etc/init.d/${file%%.*}
  done

  chmod +x "${pkgdir}"/etc/{rc.d,init.d}/{snmpd,snmptrapd}

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
