# $Id: PKGBUILD 137758 2011-09-10 15:29:12Z pierre $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gmetadom
pkgver=0.2.6
pkgrel=7
pkgdesc="C++ bindings for GDome2 DOM implementation"
arch=('i386' 'amd64')
url="http://gmetadom.sourceforge.net/"
license=('LGPL')
depends=('gdome2' 'gnu-libiconv')
makedepends=('pkg-config' 'ocaml')
options=( '!makeflags')
port_patches=(patch-GdomeSmartDOMGdomeString.cc
	patch-src-gdome_cpp_smart-test-main.cc)
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gmetadom-0.2.6-gcc43.patch'
	${port_patches[@]})
md5sums=('7bfb5d9c6f20ffa5e790b63d1f8cb481'
         'ea0271007f8650733029327823353d0d'
         'b08e2d436da6c9e650332806591c8545'
         'c02a214f374e9ec60ec9e88243fd1135')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  for i in ${port_patches[@]}; do
	msg "Applying patch $i"
	patch -p0 -i ${srcdir}/$i
  done

	sed -i''  -e 's/-lstdc++//g' \
		src/gdome_cpp_smart/Makefile.in \
		src/gdome_cpp_smart/test/Makefile.in

}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/gmetadom-0.2.6-gcc43.patch
  export LDFLAGS=${LDFLAGS/,--as-needed}
  export LDFLAGS="${LDFLAGS} -L/usr/local/lib -liconv"
  ./configure --prefix=/usr 
  gmake
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir}/ install
}
