# $Id: PKGBUILD 186388 2013-05-26 09:24:47Z bluewind $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libproxy
# fails to build...
#pkgver=0.4.11
#pkgrel=2
pkgver=0.4.6
pkgrel=15
pkgdesc="A library that provides automatic proxy configuration management"
arch=(i686 amd64)
license=('LGPL')
depends=('gcc-libs')
optdepends=('kdelibs: KDE configuration module'
            'perl: Perl bindings'
            'python2: Python bindings')
makedepends=('cmake' 'automoc4' 'python2' 'perl')
url="http://libproxy.googlecode.com"
source=(http://libproxy.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('199c6b120baf1f7258a55f38d5ec74f5')

prepare() {
	[[ ! -d build ]] && mkdir build
	[[ ! -d python-path ]] && mkdir python-path
	[[ ! -L python-path/python ]] && ln -s /usr/bin/python2 python-path/python
}

build() {
  cd "${srcdir}"

  export PATH="${srcdir}/python-path:${PATH}"

  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/libexec/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=yes \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_WEBKIT=OFF \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}"
  gmake
}

package() {
  cd "${srcdir}/build"
  gmake DESTDIR="${pkgdir}" install
}
