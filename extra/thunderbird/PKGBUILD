# $Id: PKGBUILD 239426 2015-05-15 23:59:15Z foutrelis $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Anders Bostrom <anders.bostrom@home.se>

pkgname=thunderbird
pkgver=38.3.0
pkgrel=2
pkgdesc="Standalone Mail/News reader"
arch=('i386' 'amd64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/thunderbird/"
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libvpx' 'libxt' 'mime-types' 'mozilla-common' 'nss' 'sqlite' 
'startup-notification' 'libv4l' 'v4l-compat')
makedepends=('unzip' 'zip' 'python2' 'yasm' 'mesa' 'libpulse' 'autoconf2.13')
optdepends=('libcanberra: for sound support')
install=thunderbird.install
patchdir=('comm-esr31')
source=(https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2
mozconfig
thunderbird.desktop
thunderbird-install-dir.patch
vendor.js)
options=(!emptydirs)
sha256sums=('9f3cb19a5ceabc5e8fa31f782b6d92720a77ef2c2d6c7b63b3d792cde491c961'
            '7021a0907ebaffec7cad2928a773d8a0763a9790b9b97b58f72fad52e981296b'
            '3fba13d88aeb003ab0811ef739463858172ce0662a1c7d62835df3d83ddbb8fb'
            '9049ab3f9600a1592a54c41a166c76e046c393e1cfe7c4e769155b7317d197ee'
            'e4ea8e6788163d9f8db8f1f40023db3ea0a1358f9a4510169f2d4c4fe6a887ed')
validpgpkeys=('2B90598A745E992F315E22C58AB132963A06537A')

prepare() {
  cd comm-esr31
  patch -Np1 -i ../thunderbird-install-dir.patch

  # Fix build with libvpx 1.4.0 (patch from Fedora)
  patch -Np1 -i ../thunderbird-31.6.0-libvpx-modern.patch

  cp "$srcdir/mozconfig" .mozconfig

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  gsed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' mozilla/configure
}

build() {
  cd comm-esr31

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/thunderbird"
  export PYTHON="/usr/bin/python2"

  autoconf-2.13
  cd mozilla && autoconf-2.13
  cd js/src && autoconf-2.13

  cd ../../../
  gmake -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd comm-esr31
  gmake -j1 -f client.mk DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/lib/thunderbird/defaults/preferences"
  install -m644 "$srcdir"/vendor.js "$pkgdir/usr/lib/thunderbird/defaults/preferences/vendor.js"

  
  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -dm755 "$pkgdir/usr/share/icons/hicolor/$i/apps"
      install -m644 other-licenses/branding/thunderbird/mailicon${i/x*/}.png \
          "$pkgdir/usr/share/icons/hicolor/$i/apps/thunderbird.png"
  done

  install -dm755 "$pkgdir/usr/share/applications"
  install -m644 "$srcdir/thunderbird.desktop" \
      "$pkgdir/usr/share/applications/thunderbird.desktop"

  rm -rf "$pkgdir"/usr/lib/thunderbird/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir/usr/lib/thunderbird/dictionaries"
  ln -sf /usr/share/hyphen "$pkgdir/usr/lib/thunderbird/hyphenation"

}
