# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=libogg
pkgver=1.3.2
pkgrel=3
pkgdesc="Ogg bitstream and framing library"
arch=('i386' 'amd64')
url="http://www.xiph.org/ogg/"
options=('libtoolfix')
license=('BSD')
source=(http://downloads.xiph.org/releases/ogg/$pkgname-$pkgver.tar.xz
	config_types.h)
sha1sums=('5e525ec6a4135066932935c01d2c309ea5009f8d'
          '3f7e0d0d0ed983e633d698c83308b910f6611a63')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  gmake
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  gmake DESTDIR="$pkgdir" install

# install BSD license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  if [[ $CARCH == "x86_64" ]]; then
    mv "$pkgdir"/usr/include/ogg/config_types{,-64}.h
    install -m 644 "$srcdir/config_types.h" \
      "${pkgdir}/usr/include/ogg/config_types.h"
  fi


}
