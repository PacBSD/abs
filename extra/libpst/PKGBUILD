# $Id: PKGBUILD 183115 2013-04-17 16:05:23Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libpst
pkgname=('libpst' 'libpst-docs')
pkgver=0.6.63
pkgrel=5
arch=('i386' 'amd64')
url="http://www.five-ten-sg.com/libpst/"
license=('GPL')
makedepends=('python2' 'boost' 'libgsf')
source=(http://www.five-ten-sg.com/libpst/packages/${pkgbase}-${pkgver}.tar.gz)
sha1sums=('3625c94bc82555d490805d4bbac07d9160195537')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  find -X . -type f | xargs sed -i '' \
		-e 's/malloc.h/stdlib.h/g'

}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
      --disable-static --enable-libpst-shared --disable-dii PYTHON_VERSION=2
  gmake
}

package_libpst() {
  pkgdesc="Outlook .pst file converter"
  depends=( 'libgsf')
  optdepends=('boost-libs: for libpst python interface')
  options=('!libtool' '!docs')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}

package_libpst-docs() {
  pkgdesc="Documentation for Outlook .pst file converter"
  options=('!libtool' 'docs')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install-htmlDATA
  cd "${srcdir}/${pkgbase}-${pkgver}/html"
  gmake DESTDIR="${pkgdir}" install-htmldevelDATA
}
