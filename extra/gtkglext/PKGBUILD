# $Id: PKGBUILD 169936 2012-10-30 22:45:47Z heftig $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gtkglext
pkgver=1.2.0
pkgrel=5
pkgdesc="opengl extensions for gtk2"
arch=('i386' 'amd64')
url="http://gtkglext.sourceforge.net/"
license=('LGPL')
depends=('gtk2' 'glu' 'libxmu' 'pangox-compat')
makedepends=('mesa')
options=()
source=("http://downloads.sourceforge.net/sourceforge/gtkglext/${pkgname}-${pkgver}.tar.bz2"
	'gtk2.20.patch')
md5sums=('ed7ba24ce06a8630c07f2d0ee5f04ab4'
         'e5a87ec3f2d0e616c6f32f90c3f7237f')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/gtk2.20.patch"
  gsed '/AC_PATH_XTRA/d' -i configure.in

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	  -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
	  ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | \
      xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --disable-static
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
