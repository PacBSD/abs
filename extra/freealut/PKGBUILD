# $Id: PKGBUILD 197959 2013-10-30 11:55:45Z allan $ 
# Maintainer:
# Contributor: Arkamririon <Arkamririon@gmx.net>

pkgname=freealut
pkgver=1.1.0
pkgrel=7
pkgdesc="OpenAL Utility Toolkit (ALUT)"
arch=('i386' 'amd64')
url="http://www.openal.org"
license=("LGPL")
depends=('openal')
port_patches=(
	patch-src_alutInit.c
	patch-src_alutInternal.h
	patch-src_alutLoader.c
	patch-src_alutUtil.c
)
source=(http://pkgs.fedoraproject.org/repo/pkgs/freealut/freealut-1.1.0.tar.gz/e089b28a0267faabdb6c079ee173664a/freealut-1.1.0.tar.gz
	${port_patches[@]})
md5sums=('e089b28a0267faabdb6c079ee173664a'
         '2dc6bf0d67eded9bb87270a46a913c04'
         '83bd95fb4c28b289c5abbdb2ba4fb539'
         '107d22b928f5ee0da872639ad9c8a563'
         '0add46f425f9a927217ff76d20001ce5')

prepare() {
	cd $srcdir/$pkgname-$pkgver

	for i in ${port_patches[@]}; do
		msg "Applying patch ${i}"
		patch -p0 -i "${srcdir}/${i}"
	done

	sed -i '' -e 's/@CFLAGS@/& -lopenal -pthread/g' \
		examples/Makefile.in \
		test_suite/Makefile.in

	sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	  -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
	  ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
	find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr  --disable-static
  gmake
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install
}
