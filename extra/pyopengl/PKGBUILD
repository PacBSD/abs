# $Id$
# Maintainer:
# Contributor: Josh Taylor <joshtaylor.mail@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=pyopengl
pkgname=('python2-opengl' 'python-opengl')
pkgver=3.1.0
pkgrel=5
pkgdesc="The cross platform Python binding to OpenGL and related APIs"
url="http://pyopengl.sourceforge.net/"
license=('BSD')
arch=('amd64')
makedepends=('freeglut' 'python2-setuptools' 'python-setuptools')
source=("http://pypi.python.org/packages/source/P/PyOpenGL/PyOpenGL-${pkgver}.tar.gz"
        'LICENSE')
md5sums=('0de021941018d46d91e5a8c11c071693'
         '0b53c508a63e5dbaf44cdfb0fa103b3a')

prepare() {
    cp -r PyOpenGL-${pkgver} python-PyOpenGL-${pkgver}
}

package_python2-opengl() {
    depends=('python2' 'freeglut')
	conflicts=('python-pyopengl' 'python-opengl<=3.0.1-4')
	replaces=('python-pyopengl' 'python-opengl<=3.0.1-4')

    cd PyOpenGL-${pkgver}
	python2 setup.py install --root="${pkgdir}" --optimize=1
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -m644 "${srcdir}/LICENSE" \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Fix the shebang
    sed -i'' -e 's|#!/usr/bin/env python|#!/usr/bin/python2|' \
      "${pkgdir}"/usr/lib/python2.7/site-packages/OpenGL/Tk/__init__.py
    sed -i'' -e 's|#! /usr/bin/env python|#!/usr/bin/python2|' \
      "${pkgdir}"/usr/lib/python2.7/site-packages/OpenGL/arrays/{_,}buffers.py
}

package_python-opengl() {
    depends=('python' 'freeglut')
 
    cd python-PyOpenGL-${pkgver}
 	python setup.py install --root="${pkgdir}" --optimize=1
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -m644 "${srcdir}/LICENSE" \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
