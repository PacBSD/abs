# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Adam Walters <abwalters@abwalters.us>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=libatomic_ops
pkgver=7.4.2
_tag=$pkgname-${pkgver//./_}

pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('i386' 'amd64')
url="https://github.com/ivmai/libatomic_ops"
depends=('libc')
license=('GPL2' 'MIT')
source=(https://github.com/ivmai/libatomic_ops/archive/$_tag.zip)
md5sums=('60384ffb3fc87d8b7f7e2abf24e1c3f0')

build() {
  cd $pkgname-$_tag
  ./autogen.sh
  ./configure --prefix=/usr --disable-static --enable-shared

  make
}

package() {
  cd $pkgname-$_tag
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname"
  install -m644 doc/LICENSING.txt "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
