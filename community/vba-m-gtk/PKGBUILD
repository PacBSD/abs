# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Barthalion <aur at bpiotrowski dot pl>

pkgname=vba-m-gtk
_pkgname="visualboyadvance-m"
pkgver=1.8.0.1001
pkgrel=1
pkgdesc="A VBA fork which improves the capatibility and features of the emulator"
arch=('i386' 'amd64')
url="http://vba-m.com"
license=('GPL2')
depends=('desktop-file-utils' 'gtkglextmm' 'hicolor-icon-theme' 'sdl' 'sfml')
makedepends=('cmake' 'mesa')
[[ $CARCH == i386 ]] && makedepends+=('nasm')
conflicts=('vba-m-wx')
install="${pkgname%-*}.install"
port_patches=(patch-CMakeLists.txt
	patch-fex-fex-Data_Reader.cpp
	patch-src-Util.cpp
	patch-src-common-Patch.cpp
	patch-src-gtk-filters.cpp
	patch-src-gtk-filters.h
	patch-src-gtk-gvbam.desktop
	patch-src_expr.cpp)
source=("http://update.cooltrainer.org/emulators/${_pkgname}/${_pkgname}-${pkgver%.*}r${pkgver##*.}.tar.bz2"
	${port_patches[@]})
sha256sums=('8668c8b556826681c8e848d999ff3ba1383daf78e3410a20bcfc0e8f2eeed4c2'
            'c94337143f436f2b4c820d1935308e622df91b6df4791d9e9f243c75a305303a'
            'b910efb63548225ee37f00d017363bebf0d4d3b8d1236e27e7b880b8a85bddb9'
            '9f73c03b6de702c6238f2881433e4bd1f737fec60b290885dd7ee51fff31139a'
            '5c07a8258ccf6b353b2e35393f7e53029a93430ecf2e1ac69b9221d1a603d90a'
            'c74d82d24949169e9890e551d440b614f404de3b86dbb2413f840f54b200c9fd'
            '6d22ef366debabdf17343b0ce872db3ea5c5bc7c670764944683926cdfc73cb0'
            'bc997274c6151a6ce52faf468e6f26f5e7fd80b3b540d13f295ce81344acdc72'
            '3d29a45c064ed974a974ed4b2d76b1eba4efdf1758f9e154b2c87707b3121c82')

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver%.*}r${pkgver##*.}
  for p in ${port_patches[@]}; do
	patch -p0 -i ${srcdir}/${p}
  done

  sed -i '' -e 's/<malloc.h>/<stdlib.h>/g' src/gba/GBALink.cpp
}

build() {
  cd ${srcdir}/${_pkgname}-${pkgver%.*}r${pkgver##*.}
  if [[ $CARCH == i386 ]]; then
    cmake -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_CXX_FLAGS='-I/usr/include/sfml-1.6' -DENABLE_ASM_{CORE,SCALERS}='ON'
  else
    cmake -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_CXX_FLAGS='-I/usr/include/sfml-1.6'
  fi
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver%.*}r${pkgver##*.}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
