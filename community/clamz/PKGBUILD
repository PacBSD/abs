pkgname=clamz
pkgver=0.5
pkgrel=6
pkgdesc="Command-line program to download MP3 files from Amazon.com's music store."
arch=('i386' 'amd64')
url='http://code.google.com/p/clamz/'
license=('GPL3')
depends=('libgcrypt' 'curl' 'expat' 'shared-mime-info' 'desktop-file-utils')
install='clamz.install'
source=("http://clamz.googlecode.com/files/clamz-${pkgver}.tar.gz")
md5sums=('84543c9346b285bdc218879e241941a8')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Fix issue with update-mime-database
	gsed -i 's|$(UPDATE_DESKTOP_DATABASE)\ $(DESTDIR)$(applications_dir)||' Makefile.in
	gsed -i 's|$(UPDATE_MIME_DATABASE)\ $(DESTDIR)$(mime_dir)||' Makefile.in

	./configure --prefix=/usr/
	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake DESTDIR="${pkgdir}" install
}
