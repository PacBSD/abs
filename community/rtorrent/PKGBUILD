pkgname=rtorrent
pkgver=0.9.4
pkgrel=2
pkgdesc='Ncurses BitTorrent client based on libTorrent'
url='http://libtorrent.rakshasa.no/'
license=('GPL')
arch=('amd64')
depends=('libtorrent=0.13.6' 'curl' 'xmlrpc-c' 'libsigc++' 'ncurses')
source=("http://distcache.FreeBSD.org/ports-distfiles/${pkgname}-${pkgver}.tar.gz")
md5sums=('fd9490a2ac67d0fa2a567c6267845876')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# turn into c++11
	find . -name '*.h' -o -name '*.cc' | xargs gsed -i \
		-e 's/tr1::/std::/g' \
		-e 's/std::std::/std::/g' \
		-e '/include/s,tr1/,,' \
		-e '/namespace tr1/d'
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -std=c++11"
	./configure \
		--prefix=/usr \
		--disable-debug \
		--with-xmlrpc-c \

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
	install -m644 doc/rtorrent.rc "${pkgdir}"/usr/share/doc/rtorrent/rtorrent.rc
}
