# $Id: PKGBUILD 121051 2014-10-20 10:30:34Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=lash
pkgver=0.6.0~rc2
pkgrel=2
pkgdesc="A session management system for JACK and ALSA"
arch=('i386' 'amd64')
url="http://lash.nongnu.org/"
license=('GPL')
depends=('dbus' 'gtk2' 'jack' 'libxml2' 'python2' 'e2fsprogs-libuuid')
options=('libtoolfix')
source=("http://download.savannah.gnu.org/releases/lash/${pkgname}-${pkgver}.tar.bz2"
	"makefile.patch")
md5sums=('af1dc4f4ceb284b1b0845de4f4c2fe47'
         '24ceb7e3d008c25e1490102983165612')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < "${srcdir}/makefile.patch"
  # Python2 fixes
  export PYTHON="python2"

  sed -i '' -e \
	'/if test/s|==|=|' configure
  sed -i '' -e \
	'/texi2html/s|--number||' docs/Makefile.in

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr LIBS="-luuid" \
	--disable-alsa-midi
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake -j1 DESTDIR=${pkgdir} install
}
