# Maintainer: Claudiu Traistaru <claudiu2004[at]gmail[dot]com>
pkgname=('virtualbox-ose')
pkgver=4.3.30
pkgrel=1
url="http://www.virtualbox.org"
arch=('amd64')
license=('GPL' 'custom')
depends=('freebsd-lib32' 'libxslt' 'python2' 'libidl2' 'libpng' 'libxcursor' 'libxinerama' 'libxrandr' 
         'libxml2' 'libxmu' 'glu' 'pulseaudio' 'gnutar' 'qt4' 'libvpx' 'cdrkit')
makedepends=('kBuild' 'sdl' 'freebsd-source' 'yasm' 'gcc')
options=('iconv')
install='virtualbox-ose.install'
provides=('virtualbox')
patchdir=("VirtualBox-${pkgver}")
source=("http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}.tar.bz2"
	vboxheadless.in
	vboxwatchdog.in
	vboxwebsrv.in
)
md5sums=('cc053340f88922a11ad9d4fab56557bd'
         'd7f73a917e28e4fb27133e68968fa467'
         '1046015c4f0fa519f409a1d940a127d8'
         '9f074c4cf6fcac8e4a950928b27a838e')

build() {
  cd "${srcdir}/VirtualBox-${pkgver}"

	sed -i '' -e 's| -finline-limit=8000||' \
		-e 's| -mpreferred-stack-boundary=2||' Config.kmk
	sed -i '' -e 's| -fpermissive||' Config.kmk src/VBox/Main/webservice/Makefile.kmk
	sed -i '' -e 's|iconv||' Config.kmk src/VBox/Runtime/Makefile.kmk
	echo 'VBOX_ICONV_DEFS = LIBICONV_PLUG' >> LocalConfig.kmk

	echo 'VBOX_GCC_Wno-unused-parameter = -Wno-unused-parameter' >> LocalConfig.kmk
	echo 'TOOL_VBoxGccFreeBSD_LD = ${CXX}' >> LocalConfig.kmk
	sed -e 's|GXX3|VBoxGccFreeBSD|g' /usr/share/kBuild/tools/GXX3.kmk > tools/kBuildTools/VBoxGccFreeBSD.kmk
	echo 'VBOX_WITH_INSTALLER = 1' >> LocalConfig.kmk
	echo 'VBOX_WITH_VBOXDRV =' >> LocalConfig.kmk
	echo 'VBOX_WITH_VIDEOHWACCEL =' >> LocalConfig.kmk
	echo 'VBOX_WITH_NETFLT =' >> LocalConfig.kmk
	echo 'VBOX_WITH_NETADP =' >> LocalConfig.kmk
	echo 'VBOX_WITH_TESTCASES=' >> LocalConfig.kmk
	echo 'SDK_VBOX_LIBPNG_LIBS = png' >> LocalConfig.kmk
	echo 'VBOX_WITH_ADDITIONS=' >> LocalConfig.kmk
	echo 'VBOX_WITH_X11_ADDITIONS=' >> LocalConfig.kmk
	echo 'VBOX_WITH_ADDITION_DRIVERS =' >> LocalConfig.kmk
	echo 'VBOX_WITH_TESTSUITE =' >> LocalConfig.kmk
	echo 'VBOX_WITH_ORIGIN :=' >> LocalConfig.kmk
	echo 'VBOX_PATH_APP_PRIVATE_ARCH := /usr/lib/virtualbox' >> LocalConfig.kmk
	echo 'VBOX_PATH_SHARED_LIBS := $(VBOX_PATH_APP_PRIVATE_ARCH)' >> LocalConfig.kmk
	echo 'VBOX_WITH_RUNPATH := $(VBOX_PATH_APP_PRIVATE_ARCH)' >> LocalConfig.kmk
	echo 'VBOX_PATH_APP_PRIVATE := /usr/share/virtualbox' >> LocalConfig.kmk
	echo 'VBOX_PATH_APP_DOCS := /usr/share/virtualbox' >> LocalConfig.kmk
	echo 'VBOX_PATH_PACKAGE_DOCS := $(VBOX_PATH_APP_DOCS)' >> LocalConfig.kmk
	echo 'VBOX_WITH_REGISTRATION_REQUEST =' >> LocalConfig.kmk
	echo 'VBOX_WITH_UPDATE_REQUEST =' >> LocalConfig.kmk
	echo 'VBOX_WITH_VNC :=' >> LocalConfig.kmk
	echo 'VBOX_BLD_PYTHON = python2' >> LocalConfig.kmk
	echo 'PATH_SDK_QT4_LIB=/usr/lib'>> LocalConfig.kmk

	sed -i'' -e 's|$KBUILDDIR_BIN/kmk_sed|/usr/bin/kmk_sed|g' configure

	./configure --disable-docs \
		--enable-webservice \
		--passive-mesa \
		--disable-java \
		--with-gcc="${CC}" \
		--with-g++="${CXX}"


	source ./env.sh
	/usr/bin/kmk
}

package_virtualbox-ose() {
	pkgdesc="VirtualBox is a x86 and AMD64/Intel64 virtualization product"
	depends=('curl'
           'libpng'
           'libvpx'
           'libxcursor'
           'libxinerama'
           'libxml2'
           'libxmu'
           'sdl'
           'shared-mime-info'
           'virtualbox-ose-kmod'
           )
	optdepends=('qt4: VirtualBox GUI support')
	backup=('etc/vbox/vbox.cfg')
	source "VirtualBox-$pkgver/env.sh"

	cd "VirtualBox-$pkgver/out/freebsd.amd64/release/bin"
	install -dm755 "$pkgdir"/usr/{bin,lib/virtualbox/components,lib/virtualbox/ExtensionPacks,share/virtualbox/nls,share/virtualbox/rdesktop-vrdp-keymaps,/lib/virtualbox}

	# for i in VBoxHeadless VBoxManage VBoxSDL VirtualBox vboxwebsrv VBoxBalloonCtrl; do
	[ -f VBox.sh ] && install -m755 VBox.sh "$pkgdir/usr/bin/VBox"
	for i in VBoxHeadless VBoxManage VBoxSDL VirtualBox VBoxBalloonCtrl; do
		ln -sf "/usr/lib/virtualbox/$i" "$pkgdir/usr/bin/$i"
		chmod +x "$pkgdir/usr/bin/$i"
	done


	## setuid root binaries
	install -m4755 VBoxSDL VirtualBox VBoxHeadless VBoxNetDHCP VBoxNetAdpCtl VBoxNetNAT "$pkgdir/usr/lib/virtualbox"
	## other binaries
	install -m755 VBoxManage VBoxSVC VBoxExtPackHelperApp VBoxXPCOMIPCD VBoxTestOGL VBoxBalloonCtrl "$pkgdir/usr/lib/virtualbox"
	[ -f VBoxTunctl ] && install -m755 VBoxTunctl "$pkgdir/usr/bin"
	[ -f rdesktop-vrdp ] && install -m755 rdesktop-vrdp "$pkgdir/usr/bin"

	#components
	install -dm755 "$pkgdir/usr/lib/virtualbox/components"
	install -m755 components/* "$pkgdir/usr/lib/virtualbox/components"
	install -dm755 "$pkgdir/usr/lib/virtualbox/ExtensionPacks"

	#lib
	install -dm755 "$pkgdir/usr/lib/virtualbox"
	install -m755 *.so "$pkgdir/usr/lib/virtualbox"
	install -m644 *.gc *.r0  VBoxEFI*.fd "$pkgdir/usr/lib/virtualbox"

	install -m755 VBoxManage VBoxSVC VBoxExtPackHelperApp VBoxXPCOMIPCD VBoxBalloonCtrl "$pkgdir/usr/lib/virtualbox"

	#language
	install -dm755 "$pkgdir/usr/share/virtualbox"
	install -dm755 "$pkgdir/usr/share/virtualbox/nls"
	install -m755 nls/*.qm "$pkgdir/usr/share/virtualbox/nls"

	#desktop
	install -dm755 "$pkgdir/usr/share/applications"
	install -m644 "${srcdir}/VirtualBox-${pkgver}/src/VBox/Installer/freebsd/virtualbox.desktop" "$pkgdir/usr/share/applications/virtualbox.desktop"
	install -dm755 "$pkgdir/usr/share/mime/packages"
	install -m644 "${srcdir}/VirtualBox-${pkgver}/src/VBox/Installer/common/virtualbox.xml" "$pkgdir/usr/share/mime/packages/virtualbox.xml"

	#install configuration
	mkdir -p "$pkgdir/etc/vbox"
	echo 'INSTALL_DIR=/usr/lib/virtualbox' > "$pkgdir/etc/vbox/vbox.cfg"

	#licence
	install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
	install -m644 "$srcdir/VirtualBox-$pkgver/COPYING" \
	"$pkgdir/usr/share/licenses/$pkgname/LICENSE"


	install -dm755 "$pkgdir/usr/bin"
	install -dm755 "${pkgdir}/etc/rc.d"
	install -m755 "${srcdir}/vboxheadless.in" "${pkgdir}/etc/rc.d/vboxheadless"
	install -m755 "${srcdir}/vboxwatchdog.in" "${pkgdir}/etc/rc.d/vboxwatchdog"
	install -m755 "${srcdir}/vboxwebsrv.in" "${pkgdir}/etc/rc.d/vboxwebsrv"
}


# vim:set ts=2 sw=2 et:
