# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Kristoffer Fossg√•rd <kfs1@online.no>

pkgname=terminus-font
pkgver=4.39
pkgrel=1
pkgdesc="A superb, monospace bitmap font (for X11 and console)"
arch=('any')
url="http://sourceforge.net/projects/terminus-font/"
license=('GPL2' 'custom:OFL')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-util' 'fontconfig')
makedepends=('xorg-bdftopcf')
replaces=('xserver-terminus-fonts')
install=terminus-font.install
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('3108383986b365a4b9cc08c31a4195196f6de974d2735d77f6c6efa9feb1f5bf')

build() { 
  cd "${srcdir}/${pkgname}-${pkgver}"

  sh ./configure --prefix=/usr \
              --x11dir=/usr/share/fonts/terminus-font \
              --psfdir=/removed
  gmake
}

package() {
  gmake -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/removed"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 "${srcdir}/${pkgname}-${pkgver}/OFL.TXT" \
                 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
