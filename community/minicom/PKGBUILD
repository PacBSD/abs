#
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Denis Tikhomirov <dvtikhomirov@gmail.com>

pkgname=minicom
pkgver=2.7
pkgrel=2
pkgdesc="A serial communication program"
arch=('i386'  'amd64')
url="http://alioth.debian.org/projects/minicom/"
license=('GPL')
depends=('bash')
optdepends=('lrzsz: for xmodem, ymodem and zmodem file transfer protocols')
backup=('etc/minirc.dfl')
source=("https://alioth.debian.org/frs/download.php/file/3977/${pkgname}-${pkgver}.tar.gz")
md5sums=('7044ca3e291268c33294f171d426dc2d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-dfl-baud=57600 \
              --enable-lock-dir=/var/spool/lock \
              --enable-dfl-port=/dev/cuau0
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake DESTDIR="${pkgdir}/" install
  install -dm755 ${pkgdir}/etc
  install -m644 doc/minirc.dfl ${pkgdir}/etc/minirc.dfl
}
