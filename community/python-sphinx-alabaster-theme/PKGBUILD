# $Id: PKGBUILD 136599 2015-07-09 17:11:51Z arojas $
# Maintainer:
# Contributor: xantares

pkgbase=python-sphinx-alabaster-theme
pkgname=(python-sphinx-alabaster-theme python2-sphinx-alabaster-theme)
pkgver=0.7.6
pkgrel=3
arch=('i386' 'amd64')
pkgdesc="Modified Kr Sphinx doc theme"
url='https://github.com/bitprophet/alabaster'
license=(BSD)
makedepends=(python-setuptools python2-setuptools)
source=("https://github.com/bitprophet/alabaster/archive/${pkgver}.tar.gz")
md5sums=('940d47184c4d67d9e1943ba2e5b15a65')

build() {
  cp -r ${srcdir}/alabaster-${pkgver} ${srcdir}/alabaster-${pkgver}-py2
  
  cd "$srcdir"/alabaster-${pkgver}
  python setup.py build

  cd "$srcdir"/alabaster-${pkgver}-py2
  python2 setup.py build
}

package_python-sphinx-alabaster-theme() {
  cd "$srcdir"/alabaster-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sphinx-alabaster-theme() {
  cd "$srcdir"/alabaster-${pkgver}-py2 
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

