# Maintainer: Claudiu Traistaru <claudiu2004[at]gmail[dot]com>
pkgname=('virtualbox-ose-kmod')

pkgver=4.3.30
pkgrel=1
url="http://www.virtualbox.org"
arch=('i386' 'amd64')
license=('GPL' 'custom')
depends=('freebsd-kernel>=1:10.1')
makedepends=('kBuild' 'yasm' 'freebsd-source>=1:10.1')
patchdir=("VirtualBox-${pkgver}")
source=("http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}.tar.bz2"
	vboxnet.in)
md5sums=('cc053340f88922a11ad9d4fab56557bd'
         '3cd3061a42ebcf112fd3621a9c34aabb')

build() {
	unset LDFLAGS
	cd "${srcdir}/VirtualBox-${pkgver}"

	echo 'VBOX_WITH_VBOXDRV = 1' >> LocalConfig.kmk
	echo 'VBOX_WITH_NETFLT = 1' >> LocalConfig.kmk
	echo 'VBOX_WITH_NETADP = 1' >> LocalConfig.kmk
	echo 'VBOX_WITH_TESTCASES=' >> LocalConfig.kmk
	echo 'VBOX_WITH_ADDITIONS=' >> LocalConfig.kmk

	./configure  --nofatal --disable-pulse \
		--disable-xpcom --disable-sdl-ttf --disable-pulse \
		--disable-alsa --disable-dbus --disable-python \
		--build-headless --with-gcc="${CC}" --with-g++="${CXX}"

  source ./env.sh

  #ko build
  cd "${srcdir}/VirtualBox-$pkgver/src/VBox/HostDrivers"

  /usr/bin/kmk HostDrivers-scripts vboxdrv-mod VBoxNetFlt-src VBoxNetAdp-src

  cd "${srcdir}/VirtualBox-$pkgver/out/freebsd.amd64/release/bin/src" 
  make 

}

package_virtualbox-ose-kmod() {
	#ko files
	install -dm755 "$pkgdir/usr/modules"
  
	pushd "${srcdir}/VirtualBox-$pkgver/out/freebsd.amd64/release/bin/src" 
	for i in vboxnetadp.ko vboxnetflt.ko vboxdrv.ko; do
		install -m755 $i "$pkgdir/usr/modules"
	done
	popd

	install -dm755 "${pkgdir}/etc/rc.d"
	install -m755 "${srcdir}/vboxnet.in" "${pkgdir}/etc/rc.d/vboxnet" 
}

# vim:set ts=2 sw=2 et:
