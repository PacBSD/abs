# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=0.10.28
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=( 'libexecinfo')
makedepends=('python2' 'gcc' 'gsed')
checkdepends=('curl')
options=('!emptydirs')
source=(http://nodejs.org/dist/v$pkgver/node-v$pkgver.tar.gz)
sha256sums=('abddc6441e0f208f6ed8a045e0293f713ea7f6dfb2d6a9a2024bf8b1b4617710')

prepare() {
  cd node-v$pkgver

  msg 'Fixing for python2 name'
  find . -type f -exec gsed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec gsed 's_python _python2 _' -i {} \;
}

build() {
  cd node-v$pkgver

  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --without-npm

  gmake
}

check() {
  cd node-v$pkgver
  gmake test || true
}

package() {
  cd node-v$pkgver

  gmake DESTDIR="$pkgdir" install

  # install docs as per user request
  install -d "$pkgdir"/usr/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "$pkgdir"/usr/share/doc/nodejs

  install -dm755 "$pkgdir"/usr/share/licenses/nodejs/
  install -m644 LICENSE \
   "$pkgdir"/usr/share/licenses/nodejs/LICENSE

  mv "${pkgdir}/usr/man" "${pkgdir}/usr/share/man"
}

# vim:set ts=2 sw=2 et:
