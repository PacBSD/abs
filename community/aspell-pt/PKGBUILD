# $Id: PKGBUILD 188442 2013-06-14 04:11:14Z eric $
# Maintainer:

pkgname=aspell-pt
_pkgverPT=20130331
_pkgverBR=20090702
pkgver=$_pkgverPT
pkgrel=3
pkgdesc="Portuguese and Brazilian Portuguese dictionary for aspell"
arch=('i686' 'amd64')
url="http://aspell.net/"
license=('GPL2')
depends=('aspell')
conflicts=('aspell-pt_br')
replaces=('aspell-pt_br')
source=(ftp://ftp.gnu.org/gnu/aspell/dict/pt_BR/aspell6-pt_BR-$_pkgverBR-0.tar.bz2
        http://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-$_pkgverPT.tar.bz2
        http://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-preao-$_pkgverPT.tar.bz2)
sha1sums=('add1db9a6a908dccaad13a7fd85c3b202299ff26'
          '694b35b1a6fcb90ff808b730a4513ff283e1ceee'
          'fa164ac44adc4544c7fdbb3dc5c146c28f30525a')

build() {
  cd aspell6-pt_BR-$_pkgverBR-0
  ./configure
  gmake

  cd ../aspell6-pt_PT-$_pkgverPT-0
  ./configure
  gmake

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  ./configure
  gmake
}

package() {
  cd aspell6-pt_BR-$_pkgverBR-0
  gmake DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-$_pkgverPT-0
  gmake DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  gmake DESTDIR="$pkgdir" install

  echo "add pt_BR.multi" > "$pkgdir"/usr/lib/aspell-0.60/brasileiro.alias
}
