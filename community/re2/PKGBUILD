# $Id: PKGBUILD 113335 2014-06-18 14:57:49Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>

pkgname=re2
pkgver=20140304
pkgrel=2
pkgdesc='C++ library for fast, safe, thread-friendly alternative to backtracking regular expression engines like those used in PCRE, Perl, and Python'
url='http://code.google.com/p/re2/'
depends=(gcc-libs)
arch=('i386' 'amd64')
license=(BSD)
source=(https://re2.googlecode.com/files/re2-$pkgver.tgz)
sha1sums=('f30dda8e530921b623c32aa58a5dabbe9157f6ca')

build() {
  cd re2
  LDFLAGS+=" -pthread"
  gmake prefix=/usr

  sed -i '' -e 's|tr1/||; s|tr1::||' util/util.h
}

check() {
  cd re2
  LDFLAGS+=" -pthread"
  gmake prefix=/usr test
}

package() {
  cd re2
  gmake prefix=/usr DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
