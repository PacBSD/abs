# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jelle van der Waa <jelle vdwaa nl>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Michael Düll <mail@akurei.me>
# Contributor: Wolfgang Bumiller <archbsd@bumiller.com>

# I would just like to take a minute here and state that synergy is
# some of the worst packaged software ever. They BUNDLE a fucking
# zip for cryptopp and do not provide an option to use a system-
# installed version of that library. They change around paths every
# update and just generally don't seem to care much.

pkgname=synergy
pkgver=1.5.1
_pkgver=2398
pkgrel=2
pkgdesc="Share a single mouse and keyboard between multiple computers"
url="http://synergy-foss.org"
arch=('i386' 'amd64')
depends=('gcc-libs' 'libxtst' 'libxinerama' 'crypto++' 'libxkbcommon-x11')
makedepends=('libxt' 'cmake' 'qt5-base' 'unzip')
optdepends=('qt5-base: gui support')
license=('GPL2')
source=("http://synergy-project.org/files/packages/synergy-${pkgver}-r${_pkgver}-Source.tar.gz"
        "unfuck-cryptopp-thanks-gentoo.patch")
sha1sums=('b7bf0384d914772da9f8c49453a47cf3405612fa'
          '8e321e664ae4b7a763175524dd938a88d85c7909')
patchdir="${pkgname}-${pkgver}-Source"

# bundled gmock uses tr1... silly people...
#options=('!clang')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"

  # Unfuck the bundled cryptopp stuff. Thanks a lot, Gentoo!
  # You and Fedora are our only friends in this crazy world.
  patch -Np1 < "${srcdir}/unfuck-cryptopp-thanks-gentoo.patch"

  # Types, seriously, come on
  sed -i '' -e 's/debugArgPos != CString::npos/debugArgPos != static_cast<UInt32>(CString::npos)/' \
    src/lib/synergy/DaemonApp.cpp

  # use their tr1/tuple as we don't have std::tr1 with libc++
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_FLAGS="${CXXFLAGS} -pthread -DGTEST_USE_OWN_TR1_TUPLE=1" .
  make -j1

  cd src/gui
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source/bin"

  # install binary
  install -dm755 "$pkgdir/usr/bin"
  install -m755 synergy "$pkgdir/usr/bin/synergy"
  install -m755 synergyc "$pkgdir/usr/bin/synergyc"
  install -m755 synergys "$pkgdir/usr/bin/synergys"

  # install config
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/$pkgname-$pkgver-Source/doc/${pkgname}.conf.example" "${pkgdir}/etc/${pkgname}.conf.example" 
  install -m644 "$srcdir/$pkgname-$pkgver-Source/doc/${pkgname}.conf.example-advanced" "${pkgdir}/etc/${pkgname}.conf.example-advanced"
  install -m644 "$srcdir/$pkgname-$pkgver-Source/doc/${pkgname}.conf.example-basic" "${pkgdir}/etc/${pkgname}.conf.example-basic" 

  # install manfiles
  install -dm755 "$pkgdir/usr/share/man/man1"
  install -m644 "$srcdir/$pkgname-$pkgver-Source/doc/${pkgname}c.man" "${pkgdir}/usr/share/man/man1/${pkgname}c.1"
  install -m644 "$srcdir/$pkgname-$pkgver-Source/doc/${pkgname}s.man" "${pkgdir}/usr/share/man/man1/${pkgname}s.1"

  # install desktop/icon stuff
  cd ../res
  install -dm755 "$pkgdir/usr/share/icons"
  install -m644 "synergy.ico" "$pkgdir/usr/share/icons/synergy.ico"
  install -dm755 "$pkgdir/usr/share/applications"
  install -m644 "synergy.desktop" "$pkgdir/usr/share/applications/synergy.desktop"
}
