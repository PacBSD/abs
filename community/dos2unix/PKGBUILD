# $Id: PKGBUILD 83060 2013-01-27 09:35:12Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: Renato Garcia <fgar.renatoATgmailDOTcom>
# Contributor: Gerson E. Ruotolo <gersonruotolo@globo.com>

pkgname=dos2unix
pkgver=6.0.3
pkgrel=1
pkgdesc='Text file format converter'
arch=('i686' 'x86_64')
url='http://waterlan.home.xs4all.nl/dos2unix.html'
license=('BSD')
depends=('libc')
makedepends=('perl')
conflicts=('hd2u')
source=("http://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f5268c5b28a1983537210c72b3fd43f4aad17a8dacb6727b615b56dca2ef9a04')

build() {
  cd $srcdir/$pkgname-$pkgver
  gmake
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
