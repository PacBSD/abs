# $Id: PKGBUILD 109121 2014-04-10 14:16:23Z fyan $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=('python-itsdangerous' 'python2-itsdangerous')
pkgver=0.24
pkgrel=3
pkgdesc='Various helpers to pass trusted data to untrusted environments'
arch=('amd64')
url="http://pypi.python.org/pypi/itsdangerous"
license=('BSD')
makedepends=('python' 'python2')
source=("https://github.com/mitsuhiko/itsdangerous/archive/${pkgver}.tar.gz")
sha256sums=('b035b2da1d493b00a5ee7a5b750f96929de87b610643939fbe382902774626fc')

prepare() {
  cp -r "itsdangerous-$pkgver" "python-itsdangerous-$pkgver"
  cp -r "itsdangerous-$pkgver" "python2-itsdangerous-$pkgver"
}

package_python-itsdangerous() {
  cd "$pkgname-$pkgver"
  depends+=('python')

  python setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-itsdangerous() {
  cd "$pkgname-$pkgver"
  depends+=('python2')

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
