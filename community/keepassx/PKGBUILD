# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Greg Bouzakis <gregbouzakis@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Philipp Giebel <arch@ambience-design.net>

pkgname=keepassx
pkgver=0.4.3
pkgrel=5
pkgdesc='Password manager'
arch=('i386' 'amd64')
url='http://www.keepassx.org'
license=('GPL2')
depends=('libxtst' 'qt4' 'shared-mime-info')
makedepends=('optipng')
install=keepassx.install
port_patches=(
	patch-src__lib__AutoTypeX11.cpp
	patch-src__lib__random.cpp
	patch-src_lib_random.cpp
)
source=(http://downloads.sourceforge.net/keepassx/$pkgname-$pkgver.tar.gz
	${port_patches[@]})
md5sums=('1df67bb22b2e08df49f09e61d156f508'
         'dfd12d0b5d4232e1f911c687e7bbe437'
         '9b782af4eef5930bf807b76d887aa7cd'
         'fdfddbcf6d6e7d849c8a6f6b2a7b65e6')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  for i in ${port_patches[@]}; do
	patch -p0 -i ${srcdir}/${i}
  done
}

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i''  -e '1i\
  #include <unistd.h>' src/lib/random.cpp
  qmake-qt4
  gmake
  find ./ -name '*.png' -exec optipng -quiet -force -fix {} \;
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
}
