# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgbase=luafilesystem
pkgname=(lua-filesystem lua51-filesystem lua52-filesystem)
pkgver=1.6.3
_tag=v_${pkgver//./_}
pkgrel=1
pkgdesc='File System Library for the Lua Programming Language'
arch=('i386' 'amd64')
url='http://www.keplerproject.org/luafilesystem/'
license=('MIT')
makedepends=(lua lua51 lua52 gcc)
#source=(http://github.com/downloads/keplerproject/$pkgbase/$pkgbase-$pkgver.tar.gz
source=(luafilesystem-$_tag.tar.gz::https://github.com/keplerproject/luafilesystem/archive/$_tag.tar.gz
        LICENSE)
md5sums=('d0552c7e5a082f5bb2865af63fb9dc95'
         'beb2b58c4b5c2782304a5b0267a5651b')

build() {
  msg2 "Building $pkgbase with Lua 5.1"
  cp -a $pkgbase-$_tag $pkgbase-$_tag-51
  cd $pkgbase-$_tag-51
  gmake LUA_INC=/usr/include/lua5.1

  msg2 "Building $pkgbase with Lua 5.2"
  cd "$srcdir"
  cp -a $pkgbase-$_tag $pkgbase-$_tag-52
  cd $pkgbase-$_tag-52
  gmake LUA_INC=/usr/include/lua5.2

  msg2 "Building $pkgbase with Lua 5.3"
  cd "$srcdir"/$pkgbase-$_tag
  gmake
}

package_lua-filesystem() {
  depends=('lua')

  cd $pkgbase-$_tag
  gmake PREFIX="$pkgdir"/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.3' install
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_lua51-filesystem() {
  depends=('lua51')
  conflicts=('luafilesystem')
  replaces=('luafilesystem')

  cd $pkgbase-$_tag-51
  gmake PREFIX="$pkgdir"/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.1' install
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_lua52-filesystem() {
  depends=('lua52')

  cd $pkgbase-$_tag-52
  gmake PREFIX="$pkgdir"/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.2' install
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

