# $Id: PKGBUILD 193811 2013-09-02 08:57:55Z bpiotrowski $
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ffmpeg-compat
pkgver=0.10.16
pkgrel=2
epoch=1
pkgdesc='Complete and free Internet live audio and video broadcasting solution for Linux/Unix'
arch=('i686' 'amd64')
url='http://ffmpeg.org/'
license=('GPL')
install=${pkgname}.install
depends=(
      'alsa-lib' 'gsm' 'lame' 'libass'
      'libmodplug' 'libpulse' 'libtheora' 'libva' 'libvorbis' 'libvpx'
      'opencore-amr' 'openjpeg' 'rtmpdump' 'schroedinger' 'sdl' 'speex'
      'x264' 'xvidcore' 'zlib'
      )
makedepends=('libvdpau' 'yasm' 'gcc')
source=(http://ffmpeg.org/releases/ffmpeg-$pkgver.tar.bz2)
sha256sums=('e77823cbd58dfdb61f88059476070bc432d80e3821c14abcf804ef709d2f3fd1')

build() {
  cd ffmpeg-$pkgver

  export CC=gcc CPP=cpp CXX=g++

  CFLAGS+=" -I/usr/include/openjpeg-1.5" \
  ./configure \
    --prefix=/usr \
    --incdir=/usr/include/ffmpeg-compat \
    --libdir=/usr/lib/ffmpeg-compat \
    --shlibdir=/usr/lib/ffmpeg-compat \
    --disable-debug \
    --disable-static \
    --enable-gpl \
    --enable-libass \
    --enable-libfreetype \
    --enable-libgsm \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libopenjpeg \
    --enable-libpulse \
    --enable-librtmp \
    --enable-libschroedinger \
    --enable-libspeex \
    --enable-libtheora \
    --disable-libv4l2 \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libxvid \
    --enable-postproc \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-vdpau \
    --enable-version3 \
    --enable-x11grab
 

  gmake
}

package() {
  cd ffmpeg-$pkgver
  gmake DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,share}

  install -dm755 "${pkgdir}/usr/lib/ldconfig"
  echo "/usr/lib/${pkgname}" >> "${pkgdir}/usr/lib/ldconfig/${pkgname}"
}

# vim:set ts=2 sw=2 et:
