pkgname=dwm
pkgver=6.0
pkgrel=4
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i386' 'amd64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft')
install=dwm.install
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
	config.h
	dwm.desktop
	patch-Makefile
	patch-config.mk)
md5sums=('8bb00d4142259beb11e13473b81c0857'
         'ea17455203b5e0da839ab08375d501c2'
         '939f403a71b6e85261d09fc3412269ee'
         '0461509d066ea62938dee64aa8703502'
         '68311ad9daf960370e677463bd214584')

build() {
  cd $srcdir/$pkgname-$pkgver
  cp $srcdir/config.h config.h
  patch -p0 -i ${srcdir}/patch-config.mk
  patch -p0 -i ${srcdir}/patch-Makefile

  sed -i '' -e 's|CPPFLAGS =|CPPFLAGS +=|g' config.mk
  sed -i '' -e 's|^CFLAGS = -g|#CFLAGS += -g|g' config.mk
  sed -i '' -e 's|^#CFLAGS = -std|CFLAGS += -std|g' config.mk
  sed -i '' -e 's|^LDFLAGS = -g|#LDFLAGS += -g|g' config.mk
  sed -i '' -e 's|^#LDFLAGS = -s|LDFLAGS += -s|g' config.mk

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -dm755 $pkgdir/usr/share/doc/$pkgname/
  install -dm755 $pkgdir/usr/share/xsessions/
  install -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop

}
