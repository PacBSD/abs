# $Id$
# Maintainer: Anthony donnelly <amzo@archbsd.net>

pkgname=warzone2100
pkgver=3.1.2
pkgrel=2
pkgdesc="3D realtime strategy game on a future Earth"
url="http://wz2100.net/"
arch=('i386' 'amd64')
license=('GPL')
depends=('sdl' 'glew' 'fribidi' 'openal' 'libvorbis' 'libtheora' 'physfs' 'ttf-dejavu' 'qt4')
makedepends=('gawk' 'flex' 'zip' 'unzip' 'asciidoc' 'mesa')
source=("http://downloads.sourceforge.net/project/warzone2100/releases/${pkgver}/${pkgname}-${pkgver}.tar.xz"
	buildfix.patch)
md5sums=('4e947125e9604821164f1ad9d1922447'
         '7503052fc52d1cea0bae275db33544e6')

options=(!strip)
export PATH=/usr/local/bin:$PATH

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ../buildfix.patch
  sed -i '' -e 's|-Werror||g; s|-R$$found_dir||g' configure
  sed -i '' -e 's|./build_tools/autorevision|& -f|g' src/Makefile.in

}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-distributor="Arch BSD" --with-backend=qt --enable-debug=no
  sed -i '' -e 's/\/bin\/bash/\/usr\/bin\/bash/g' build_tools/autorevision
  gmake
}

package() {
  cd ${pkgname}-${pkgver}

  gmake DESTDIR=${pkgdir} install
}

