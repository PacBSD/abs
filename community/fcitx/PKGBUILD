# $Id: PKGBUILD 94235 2013-07-17 12:26:33Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgbase=fcitx
pkgname=('fcitx' 'fcitx-gtk2' 'fcitx-gtk3' 'fcitx-qt4')
pkgver=4.2.8.4
pkgrel=1
groups=('fcitx-im')
arch=('i686' 'x86_64')
url="http://fcitx-im.org"
license=('GPL')
makedepends=('cmake' 'gtk2' 'gtk3' 'qt4' 'icu' 'mesa' 'gnu-libiconv'
             'iso-codes' 'gobject-introspection' 'libxkbfile' 'libexecinfo')
# ^ doxygen
source=(http://download.fcitx-im.org/$pkgbase/$pkgbase-${pkgver}_dict.tar.xz)
sha512sums=('39bffcd003aebb4e0714d245c32e839833b8507eaf819d5f6ec100ba2ed35b321f38e9a6052371abf0f5b73bb2828f10ae1bd555a41c30353beaf81bd121d03b')

build() {
  cd "$srcdir"
  mkdir -p build
  cd build

	# LIBICONV doesn't contain any data, workaround it for now
	# Most likely because the c library contains iconv_* stuff so doesn't need to add 
	# -liconv, so we'll add it for now to use gnu-iconv

  sed -i '' 's/${LIBICONV_LIBRARIES}/-liconv/g' ../${pkgbase}-${pkgver}/src/lib/fcitx-utils/CMakeLists.txt
  export LDFLAGS="${LDFLAGS} -L/usr/local/lib -liconv" 

  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXECDIR=/usr/lib \
    -DSYSCONFDIR=/etc \
    -DFORCE_OPENCC=ON \
    -DFORCE_PRESAGE=ON \
    -DFORCE_ENCHANT=ON \
    -DENABLE_TEST=ON \
    -DENABLE_GTK2_IM_MODULE=ON \
    -DENABLE_GTK3_IM_MODULE=ON \
    -DENABLE_QT_IM_MODULE=ON \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4
  gmake
}

check() {
  cd "${srcdir}/build"
  # the backtrace test failed, this is odd but not really important
  #gmake test
}

package_fcitx() {
  pkgdesc="Flexible Context-aware Input Tool with eXtension"
  depends=('pango' 'libxinerama' 'gtk-update-icon-cache' 'shared-mime-info' \
           'hicolor-icon-theme' 'desktop-file-utils' 'libxkbfile' \
           'libxfixes' 'dbus-core' 'icu' 'libexecinfo' 'gnu-libiconv')
  optdepends=('enchant: for word predication support'
              'opencc: optional engine to do chinese convert'
              'fcitx-anthy: Hiragana text to Kana Kanji mixed text Japanese input method'
              'fcitx-chewing: Intelligent Zhuyin input method library for traditional Chinese'
              'fcitx-cloudpinyin: Standalone module for fcitx that uses pinyin API on the internet to provide additional input candidates'
              'fcitx-configtool: GTK based config tool for Fcitx'
              'fcitx-fbterm: Fbterm support for Fcitx'
              'fcitx-googlepinyin: A fork from google pinyin on android for Fcitx'
              'fcitx-gtk2: GTK2 IM Module for fcitx'
              'fcitx-gtk3: GTK3 IM Module for fcitx'
              'fcitx-hangul: Hangul (Korean) support for fcitx'
              'fcitx-kkc: libkkc (Japanese Kana Kanji input method) engine for fcitx'
              'fcitx-libpinyin: Fcitx Wrapper for libpinyin, Library to deal with pinyin'
              'fcitx-m17n: Multilingual text processing library'
              'fcitx-mozc: Fcitx Module of A Japanese Input Method for Chromium OS, Windows, Mac and Linux (the Open Source Edition of Google Japanese Input)'
              'fcitx-qt4: QT4 IM Module for fcitx'
              'fcitx-qt5: QT5 IM Module for fcitx'
              'fcitx-rime: Rime input method engine (Traditional Chinese)'
              'fcitx-sayura: Sinhala Transe IME engine for fcitx'
              'fcitx-sunpinyin: Statistical Language Model based pinyin IME by Sun'
              'fcitx-table-extra: Extra tables for Fcitx, including Boshiamy, Zhengma, Cangjie3, Cangjie5'
              'fcitx-table-other: Provides some other tables for Fcitx, fork from ibus-table-others, scim-tables'
              'fcitx-ui-light: Light weight xlib and xft based ui for fcitx'
              'fcitx-unikey: Fcitx Wrapper for unikey'
              'kcm-fcitx: KDE Config Module for Fcitx'
              'gettext: for fcitx-po-parser')
  install=fcitx.install

  cd "${srcdir}/build"
  gmake DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}/usr/lib/"{gtk-2.0,gtk-3.0,qt4}
  rm -r "${pkgdir}/usr/lib/fcitx/qt"
  rm -r "${pkgdir}/usr/include/fcitx-qt"
  rm "${pkgdir}/usr/lib/"libfcitx-qt.so*
  rm "${pkgdir}/usr/lib/pkgconfig/fcitx-qt.pc"
}

package_fcitx-gtk2() {
  pkgdesc='GTK2 IM Module for fcitx'
  depends=('gtk2' "fcitx>=$pkgver")
  install=fcitx-gtk2.install

  cd "${srcdir}/build/src/frontend/gtk2"
  gmake DESTDIR="${pkgdir}" install
}

package_fcitx-gtk3() {
  pkgdesc='GTK3 IM Module for fcitx'
  depends=('gtk3' "fcitx>=$pkgver")
  install=fcitx-gtk3.install

  cd "${srcdir}/build/src/frontend/gtk3"
  gmake DESTDIR="${pkgdir}" install
}

package_fcitx-qt4() {
  pkgdesc='QT4 IM Module for fcitx'
  depends=('qt4' "fcitx>=$pkgver")
  replaces=('fcitx-qt')
  provides=('fcitx-qt')
  conflicts=('fcitx-qt')

  cd "${srcdir}/build/src/frontend/qt"
  gmake DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/tools/gui"
  gmake DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/src/lib/fcitx-qt"
  gmake DESTDIR="${pkgdir}" install
}
