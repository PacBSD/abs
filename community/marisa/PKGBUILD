# $Id: PKGBUILD 125479 2015-01-05 22:55:09Z anatolik $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgbase=marisa
pkgname=('marisa' 'perl-marisa' 'python2-marisa' 'ruby-marisa')
pkgver=0.2.4
pkgrel=2
arch=('i386' 'amd64')
url="https://code.google.com/p/marisa-trie/"
license=('BSD' 'LGPL')
makedepends=('python2' 'ruby' 'perl')
source=(https://marisa-trie.googlecode.com/files/$pkgbase-$pkgver.tar.gz)
sha1sums=('fb0ed7d993e84dff32ec456a79bd36a00022629d')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static \

  gsed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
  gsed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool
  gmake

  # Perl bindings
  cd "${srcdir}/${pkgbase}-${pkgver}/bindings/perl"
  perl Makefile.PL \
    INC="-I${srcdir}/${pkgbase}-${pkgver}/lib" \
    LIBS="-L${srcdir}/${pkgbase}-${pkgver}/lib/.libs"
  gmake

  # Python bindings
  cd "${srcdir}/${pkgbase}-${pkgver}/bindings/python"
  python2 setup.py build_ext \
    --include-dirs="${srcdir}/${pkgbase}-${pkgver}/lib" \
    --library-dirs="${srcdir}/${pkgbase}-${pkgver}/lib/.libs"
  python2 setup.py build

  # Ruby bindings
  cd "${srcdir}/${pkgbase}-${pkgver}/bindings/ruby"
  ruby extconf.rb \
    --with-opt-include="${srcdir}/${pkgbase}-${pkgver}/lib" \
    --with-opt-lib="${srcdir}/${pkgbase}-${pkgver}/lib/.libs" \
    --vendor
  gmake
}

package_marisa() {
  pkgdesc="Static and space-efficient trie data structure library"
  depends=('gcc-libs')

  cd "${pkgbase}-${pkgver}"
  gmake DESTDIR="$pkgdir" install

  install -d "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}"
  install -m 644 docs/* README "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}/"

  install -d "${pkgdir}/usr/share/licenses/$pkgname"
  install -m 644 AUTHORS COPYING "${pkgdir}/usr/share/licenses/$pkgname/"
}

package_perl-marisa() {
  pkgdesc="Perl language binding for marisa"
  depends=('perl' 'marisa')

  cd "${pkgbase}-${pkgver}/bindings/perl"
  gmake DESTDIR="$pkgdir" install

  cd ../..
  install -d "${pkgdir}/usr/share/licenses/$pkgname"
  install -m 644 AUTHORS COPYING "${pkgdir}/usr/share/licenses/$pkgname/"
}

package_python2-marisa() {
  pkgdesc="Python 2.x language binding for marisa"
  depends=('python2' 'marisa')

  cd "${pkgbase}-${pkgver}/bindings/python"
  python2 setup.py install -O1 --root="$pkgdir"

  cd ../..
  install -d "${pkgdir}/usr/share/licenses/$pkgname"
  install -m 644 AUTHORS COPYING "${pkgdir}/usr/share/licenses/$pkgname/"
}

package_ruby-marisa() {
  pkgdesc="Ruby language binding for marisa"
  depends=('ruby' 'marisa')

  cd "${pkgbase}-${pkgver}/bindings/ruby"
    _hdrdir=$(pkg-config --variable=rubyhdrdir ruby-2.2)
    _arch=$(pkg-config --variable=arch ruby-2.2)
  gmake DESTDIR="$pkgdir" install \
    hdrdir="$_hdrdir" \
    arch_hdrdir="${_hdrdir}/${_arch}" \
    rubyhdrdir="$_hdrdir"

  cd ../..
  install -d "${pkgdir}/usr/share/licenses/$pkgname"
  install -m 644 AUTHORS COPYING "${pkgdir}/usr/share/licenses/$pkgname/"
}
