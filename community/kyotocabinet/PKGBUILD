# $Id: PKGBUILD 99150 2013-10-25 05:03:47Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Alexander Duscheleit <jinks@archlinux.us>
# Contributor: Joaquim Pedro (osmano807) <osmano807@gmail.com>

pkgname=kyotocabinet
pkgver=1.2.76
pkgrel=2
pkgdesc="a modern implementation of DBM in C++"
arch=('i386' 'amd64')
url="http://fallabs.com/kyotocabinet"
license=('LGPL3')
depends=('zlib' 'gcc-libs')
port_patches=(
	patch-kccommon.h
)
source=("http://fallabs.com/${pkgname}/pkg/${pkgname}-${pkgver}.tar.gz"
	${port_patches[@]})
sha512sums=('278db7b327eb4c21bf0137d9aa14fb67d74d5ce7ed1cb29fc9120d157a60de165ec0cf842903eb7952e8f998045ae585b958977fa973ba0e0773381de71d9f6a'
            '3de0806a863ef6c8e0a32302ea0d6a14e809cbd0c1023f0108d99854624093b138ff32b9d3a4dde3bbfe02604a3c330e3010f1f822b56edeb2d7c9cdd9f3566f')

prepare() {
  cd $pkgname-$pkgver

  for i in ${port_patches[@]}; do
    patch -p0 -i "${srcdir}/${i}"
  done

  sed -i '' -e 's|-lstdc++ ||g' configure doc/spex.html example/Makefile lab/kcdict/Makefile
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --disable-opt CPPFLAGS="${CPPFLAGS} -fPIC"
  gmake
}

package() {
  cd $pkgname-$pkgver

  gmake install DESTDIR="$pkgdir/"
}

