# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=libvirt
pkgver=1.2.0
pkgrel=8
pkgdesc="API for controlling virtualization engines (openvz,kvm,qemu,virtualbox,xen,etc)"
arch=('amd64')
url="http://libvirt.org/"
license=('LGPL')
depends=('e2fsprogs' 'gnutls'  'libxml2'   'python2'
	 'avahi' 'polkit'  'libpciaccess'  'dbus-core' 'libxau' 'libxdmcp' 
	 'curl' 'libsasl' 'libgcrypt' 'libgpg-error' 'openssl' 'libxcb' 'gcc-libs'
	'libx11' 'libssh2' 'libgcrypt' 'libxml2' 'qemu' 'yajl')
makedepends=( 'dnsmasq' 'automake' 'autoconf' 'gm4' 'perl' 'pkg-config')
optdepends=('bridge-utils: for briged networking (default)'
	    'dnsmasq: for NAT/DHCP for guests'
	    'openbsd-netcat: for remote management over ssh'
	    'radvd'
	    'dmidecode'
	    'ebtables')
options=('emptydirs' )
backup=('etc/libvirt/libvirtd.conf'
	'etc/libvirt/qemu.conf'
	'etc/sasl2/libvirt.conf')
install="libvirt.install"
source=("http://libvirt.org/sources/$pkgname-$pkgver.tar.gz"
	libvirtd.in)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=`which python2`

  for file in $(find . -name '*.py' -print); do
    sed -i'' -e 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i'' -e 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
  export LDFLAGS=-lX11
  export RADVD=/usr/bin/radvd
 [ -f Makefile ] || SSH2_LIBS="-L/usr/lib -lssh2 -lssl" SH2_CFLAGS="-I/usr/include" \
		./configure --prefix=/usr --libexec=/usr/libexec/"$pkgname" \
		--sbindir=/usr/bin CFLAGS="${CFLAGS} -fPIC " \
		--without-polkit \
		--disable-werror \
		--with-pic \
		--with-qemu-user=nobody --with-qemu-group=nobody \
		--without-sasl \
		--without-avahi \
		--without-hal \
		--without-udev \
		--without-netcf \
		--without-network \
		--without-sysctl \
		--enable-nls  --with-libintl-prefix=/usr
  gmake

  sed -i'' -e 's|#group =.*|group="kvm"|' src/qemu/qemu.conf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install

  install -dm755 ${pkgdir}/etc/rc.d
  install -m755 ${srcdir}/libvirtd.in ${pkgdir}/etc/rc.d/libvirtd

  rm ${pkgdir}/usr/lib/charset.alias
}
md5sums=('f74f78059def4e68d69b975ad6e6c3e2'
         '27b0080fa472332fa2a7d00c512d146a')

