# $Id: PKGBUILD 118688 2014-09-09 00:34:44Z foutrelis $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.5.12
pkgrel=5
pkgdesc="Advanced shading language for production GI renderers"
arch=('i386' 'amd64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio>=1.4.13' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff' 'llvm')
makedepends=('boost' 'cmake')
patchdir=("OpenShadingLanguage-Release-$pkgver")
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
md5sums=('484752a0f97df55962c3a667075deddc')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver

  sed -i '' "s|#!/bin/bash|#!/usr/bin/bash|" src/liboslexec/serialize-bc.bash
}

build() {
  cd OpenShadingLanguage-Release-$pkgver

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVM_STATIC=0 \
    -DCMAKE_CXX_FLAGS="-Wno-error=array-bounds -std=c++11 -Wno-deprecated-register -Wno-c++11-narrowing"
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir"/usr/share/OSL/
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/LICENSE "$pkgdir"/usr/share/licenses/$pkgname # TODO: Tell upstream about this shit
  mv "$pkgdir"/usr/{CHANGES,README.md,INSTALL} "$pkgdir"/usr/share/OSL/
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/OSL/doc
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders
}

# vim:set ts=2 sw=2 et:
