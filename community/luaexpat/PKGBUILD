# $Id: PKGBUILD 81392 2012-12-19 13:28:13Z seblu $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lucas Hermann Negri <kkndrox@gmail.com>

pkgbase=luaexpat
pkgname=(lua-expat lua51-expat)
pkgver=1.3.0
pkgrel=2
pkgdesc='SAX XML parser based on the Expat library'
arch=('i386' 'amd64')
url='http://matthewwild.co.uk/projects/luaexpat/'
license=('MIT')
makedepends=('gcc' 'lua' 'lua51' 'expat')
source=("http://matthewwild.co.uk/projects/luaexpat/luaexpat-$pkgver.tar.gz"
	    'config.51'
	    'config.52'
	    'lua52.patch')
patchdir="$pkgbase-$pkgver/"
md5sums=('3c20b5795e7107f847f8da844fbfe2da'
         '64abbf5b602e0e31b68db2925a5e6b2f'
         '60ae0633ac6f829cc3359f3533252eb0'
         '21da93cbf0d3cef80d00e2380b7c2679')

build() {
  # 5.1
  msg2 "Building $pkgbase with lua 5.1"
  cp -r $pkgbase-$pkgver $pkgbase-$pkgver-51
  cd $pkgbase-$pkgver-51
  cp -L "$srcdir/config.51" config
  gmake LUA_V=5.1
  # 5.2
  msg2 "Building $pkgbase with lua 5.2"
  cd "$srcdir/$pkgbase-$pkgver"
  cp -L "$srcdir/config.52" config
  gmake LUA_V=5.2
}

package_lua-expat() {
  depends=('lua' 'expat')

  cd $pkgbase-$pkgver
  gmake LUA_V=5.2 DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua51-expat() {
  depends=('lua51' 'expat')
  replaces=('luaexpat')
  conflicts=('luaexpat')

  cd $pkgbase-$pkgver-51
  gmake LUA_V=5.1 DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

