pkgname=xmlrpc-c
pkgver=1.41.02
pkgrel=2
#epoch=1
pkgdesc='XML-RPC for C and C++'
arch=('amd64' 'i386')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2' 'gcc-libs' 'readline')
makedepends=('libtool' 'subversion' 'python')
options=('!makeflags'  '!emptydirs')
source=('svn://svn.code.sf.net/p/xmlrpc-c/code/advanced')
sha256sums=('SKIP')

pkgver() {
  python -c 'print("%01d.%02d.%02d" % tuple(map(int, (x.rsplit(" ",1)[1] for x in open("advanced/version.mk").read().split("\n")[-4:-1]))))'
}

build() {
  cd "$srcdir/advanced"

  [ "$CARCH" != "i386" ] && export CFLAGS="$CFLAGS -fPIC"
  grep -lR '\-lpthread' $srcdir/advanced | xargs sed -i '' -e 's|-lpthread|-pthread|g'
  sed -i '' -e '/#include.*features.h/ s/features.h/sys\/cdefs.h/' $srcdir/advanced/tools/xmlrpc_pstream/xmlrpc_pstream.cpp
  sed -i '' -e '/define _XOPEN_SOURCE 600/ s,^,//,' $srcdir/advanced/src/xmlrpc_server_abyss.c
  sed -i '' -e 's,sys/unistd.h,unistd.h,' $srcdir/advanced/test/cpp/server_abyss.cpp

  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-libxml2-backend \
    --disable-cgi-server \
    --disable-libwww-client \
    --disable-wininet-client \
    --enable-abyss \
    --enable-cplusplus
  if [ ! -d include/curl ]; then
    mkdir include/curl
  fi
  touch include/curl/types.h
  LDFLAGS="$LDFLAGS -lz -lm"
  gmake CFLAGS_PERSONAL="$CFLAGS"
  gmake CFLAGS_PERSONAL="$CFLAGS" -C tools
}

package() {
  cd "$srcdir/advanced"

  gmake DESTDIR="$pkgdir" install
  gmake DESTDIR="$pkgdir" -C tools install
  # Clean out build-vars
  sed -i '' -e 's/BLDDIR=.*/BLDDIR=/' "$pkgdir/usr/bin/xmlrpc-c-config"
  sed -i '' -e 's/ABS_SRCDIR=.*/ABS_SRCDIR=/' "$pkgdir/usr/bin/xmlrpc-c-config"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
