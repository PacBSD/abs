# $Id$
# Maintainer:  Anthony Donnelly <amzo@archbsd.net>

pkgname=mpv
pkgver=0.9.2
pkgrel=3
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('amd64')
license=('GPL')
url='http://mpv.io'
depends=(
         'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libquvi' 'libgl' 'enca'
         'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva'
         'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua' 'libdvdnav'
         'libguess' 'libv4l' 'portaudio'
        )
makedepends=('mesa' 'python-docutils' 'ladspa')
options=(!emptydirs)
source=(https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz)
md5sums=('ed1384e703f7032e531731842e4da4f7')

prepare() {
  cd $pkgname-$pkgver
  sed -i '' -e 's/\#include <alloca.h>//' audio/out/ao_alsa.c
  ./bootstrap.py
}


build() {
  cd $pkgname-$pkgver

  export CFLAGS="-I/usr/local/include"
  export LDFLAGS="-L/usr/local/lib -liconv"
  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-zsh-comp \
    --enable-libmpv-shared \
    --enable-cdda

  ./waf build
}

package() {
  cd $pkgname-$pkgver
  ./waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
