# $Id: PKGBUILD 111040 2014-05-12 09:22:30Z tredaelli $
# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=('wireshark-cli' 'wireshark-gtk')
pkgbase=wireshark
pkgver=1.12.6
pkgrel=1
arch=('i686' 'amd64')
license=('GPL2')
makedepends=('gtk3' 'bash' 'gnutls' 'portaudio' 'lua52' 'python'
             'desktop-file-utils' 'hicolor-icon-theme' 'libtool')
url='http://www.wireshark.org/'
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('22ac0cc872f12cef9bb2cacfe0720eed8533dc5cea102d21de511620606cb3b6')

build() {
  cd ${pkgbase}-${pkgver}

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-gtk3=yes \
      --with-pcap \
      --with-zlib \
      --with-lua \
      --with-portaudio \
      --with-ssl \
      --with-krb5
  gmake all
}

package_wireshark-cli() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - CLI version'
  depends=('libgcrypt' 'bash' 'gnutls' 'glib2' 'lua')
  install=wireshark.install
  conflicts=(wireshark)

  cd ${pkgbase}-${pkgver}

  gmake DESTDIR="${pkgdir}" install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "${pkgdir}/usr/bin/wireshark"

  # Headers
  install -dm755 "${pkgdir}"/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

  install -m644 color.h config.h register.h ws_symbol_export.h "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
  done
}

package_wireshark-gtk() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK frontend'
  depends=('gtk3' 'portaudio' 'wireshark-cli' 'desktop-file-utils' 'hicolor-icon-theme')
  install=wireshark-gtk.install
  replaces=(wireshark)
  conflicts=(wireshark)

  cd ${pkgbase}-${pkgver}
  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  for d in 16 32 48; do
    install -dm755  "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
 
    install -m644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -dm755  "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"

    install -m644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -dm755 "${pkgdir}/usr/share/applications/wireshark.desktop"
 
  install -m644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
