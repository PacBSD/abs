# $Id: PKGBUILD 64715 2012-02-18 02:34:57Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=openarena
pkgver=0.8.8
pkgrel=3
pkgdesc="A violent, sexy, multiplayer first person shooter based on the ioquake3 engine"
arch=('i386' 'amd64')
url="http://openarena.ws/"
license=('GPL')
depends=('sdl' 'libvorbis' 'curl' 'openarena-data' 'libgl' 'glu')
makedepends=('openal' 'mesa')
optdepends=('openal')
port_patches=(patch-Makefile
	patch-feature-DEFAULT_LIBDIR
        patch-README
        patch-code-client-snd_openal.c
        patch-code-qcommon-vm_x86_64.c
        patch-code-sys-sys_unix.c
)
source=("http://heanet.dl.sourceforge.net/project/oarena/src/${pkgname}-engine-source-${pkgver}.tar.bz2"
	"http://heanet.dl.sourceforge.net/project/oarena/${pkgname}-${pkgver}.zip"
        $pkgname.png
        $pkgname-server.png
        $pkgname.desktop
        $pkgname-server.desktop
	${port_patches[@]})
md5sums=('ca9b239b477ad678ebf781e14ce6ed7a'
         '9f353d96d7889c377349d692c3905e5b'
         'c69f824dc500d436fc631b78b6a62dc8'
         '17e2f4fa060ed97ff6fe1eaff647bcfb'
         '229c87648c10e48b523c25d24aba8372'
         '00649517a4dd08cd9c6e99d6703f01f1'
         '517e97f7b39ce1df5d6593837103a919'
         '41e02b20fe5d90001d0d528e22049e81'
         'bbe0343053aceeeb7e1a2112714d7686'
         '85782cf7fc55c20d388a90ac3ed0e14e'
         'cc13c5c136bd1d0c581c20cd04f8e743'
         '21b8c85817ee8ac8c63efe8a2c5c8a2b')
prepare(){
  cd $srcdir/$pkgname-engine-source-$pkgver
  for _patch in ${port_patches[@]}; do
    msg "Patching port patch $_patch"
    patch -p0 -i "${srcdir}/$_patch" || true
  done

}

build() {
    cd $srcdir/$pkgname-engine-source-$pkgver

    gsed -i 's/#define ALDRIVER_DEFAULT "libopenal.so.0"/#define ALDRIVER_DEFAULT "libopenal.so.1"/g' code/client/snd_openal.c

    gmake DEFAULT_BASEDIR="/usr/share/$pkgname" \
		DEFAULT_LIBDIR="/usr/lib" \
		HOMEPATH="/home" \
		VERSION="1.36" \
		ARCH="$CARCH" \
		USE_LOCAL_HEADERS=0

}

package() {
    cd $srcdir/$pkgname-engine-source-$pkgver

    install -d $pkgdir/usr/{bin,share/{$pkgname,applications,pixmaps}}

    gmake COPYDIR=$pkgdir/usr/share/$pkgname copyfiles

    [ "$CARCH" = "x86_64" ] && {
        mv -f $pkgdir/usr/share/$pkgname/$pkgname.$CARCH $pkgdir/usr/bin/$pkgname
        mv -f $pkgdir/usr/share/$pkgname/oa_ded.$CARCH $pkgdir/usr/bin/$pkgname-server
    } || {
        mv -f $pkgdir/usr/share/$pkgname/$pkgname.i386 $pkgdir/usr/bin/$pkgname
        mv -f $pkgdir/usr/share/$pkgname/oa_ded.i386 $pkgdir/usr/bin/$pkgname-server
    }

    find $pkgdir/usr/share -type f -exec chmod 644 {} +

    install -m 644 $srcdir/$pkgname{,-server}.png $pkgdir/usr/share/pixmaps/
    install -m 644 $srcdir/$pkgname{,-server}.desktop $pkgdir/usr/share/applications/
}

# vim: sw=2:ts=2 et:
