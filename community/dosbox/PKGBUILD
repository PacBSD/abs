# $Id: PKGBUILD 98144 2013-10-06 16:29:33Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=dosbox
pkgver=0.74
pkgrel=1
pkgdesc='Emulator with builtin DOS for running DOS Games'
arch=('i386' 'amd64')
url='http://dosbox.sourceforge.net/'
license=('GPL')
depends=('sdl_net' 'zlib' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs' 'glu')
makedepends=('mesa-libgl' 'gendesk')
makedepends=(             'gendesk')
_port_patches=(
        patch-include-dos_inc.h
        patch-include-setup.h
        patch-src-cpu_core_dynrec_risc_x64.h
        patch-src-fpu-fpu_instructions_x86.h
)
# arch's gcc46 patch is included in the patches above
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        dosbox.png
        "${_port_patches[@]}")
options=('!strip' 'debug')

prepare() {
  cd "$srcdir"

  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"

  cd "$pkgname-$pkgver"
  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  gsed -i 's/png_check_sig/png_sig_cmp/' configure 

  ./configure --prefix=/usr --sysconfdir=/etc/dosbox \
              --enable-core-inline \
              --enable-debug=heavy
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake DESTDIR="$pkgdir" install 

# install docs, make does not install them
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README "$pkgdir"/usr/share/doc/$pkgname/README
  install -m644 docs/README.video "$pkgdir"/usr/share/doc/$pkgname/README.video

  install -dm755 "$pkgdir/usr/share/pixmaps"
  install -m644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -dm755 "$pkgdir/usr/share/applications"
  install -m644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
sha256sums=('13f74916e2d4002bad1978e55727f302ff6df3d9be2f9b0e271501bd0a938e05'
            '491c42d16fc5ef7ee2eca1b736f7801249d4ca8c0b236a001aec0d3e24504f3b'
            '60efd056f36429cde94cd2e53751cbeb5a9d1d7737992aed124cdeeea6c32b4d'
            '58a811a6a7135a4748ea991cbc7a48013df6a6d9f5443b12940152170afe8de4'
            'c5dc63b4c2457d95d1055043751f32df4407f9d5de1466673fbef71bf62f5022'
            'a7d2cd5e1062c71878a2b566c0c9dccbea8fefa2df8bb6f40cdb75b7d50cfcec')
