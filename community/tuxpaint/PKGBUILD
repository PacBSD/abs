# $Id: PKGBUILD 91097 2013-05-19 11:35:37Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Sergio Jovani Guzman <moret.sjg@gmail.com>
# Contributor: royrocks <royrocks13@gmail.com>

pkgname=tuxpaint
pkgver=0.9.22
pkgrel=2
pkgdesc='Drawing program designed for young children'
arch=('amd64' 'i686')
url='http://www.tuxpaint.org/'
license=('GPL')
backup=('etc/tuxpaint/tuxpaint.conf')
depends=('python2' 'fribidi' 'sdl_ttf' 'sdl_mixer' 'sdl_image' 'sdl_pango'
         'libpaper' 'librsvg')
makedepends=('optipng' 'setconf')
optdepends=('tuxpaint-stamps: more stamps'
            'tuxpaint-conf: configuration manager')
source=("http://downloads.sourceforge.net/sourceforge/tuxpaint/$pkgname-$pkgver.tar.gz")
sha256sums=('8abd932f8e36fad795bba0290c094c9c221e424c4b9aa8c763b7fb3459cb2be3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  setconf src/tuxpaint.desktop Categories \
    'Game;KidsGame;Graphics;RasterGraphics;'

  # fullscreen
  gsed -i 's:# fu:fu:' src/tuxpaint.conf

  # python2 fix
  for f in docs/zh_tw/mkTuxpaintIM.py fonts/locale/zh_tw_docs/maketuxfont.py; do
    gsed -i '0,/on/s//on2/' $f
  done

  # libpng15 & gettext fix
  gsed -i 's:$(ARCH_LINKS):$(ARCH_LINKS) -lpng -lintl:' Makefile

  # libpng16 fix
  find . -name '*.png' -exec optipng -quiet -force -fix {} +

}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake PREFIX=/usr

}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    GNOME_PREFIX=/usr \
    X11_ICON_PREFIX="$pkgdir/usr/share/pixmaps" \
    install -j1
  chmod -R 644 "$pkgdir/usr/share/doc/tuxpaint-dev/"*
  chmod 755 "$pkgdir/usr/share/doc/tuxpaint-dev/html"
}

# vim:set ts=2 sw=2 et:
