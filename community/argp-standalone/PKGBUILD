# Contributor:

pkgname=argp-standalone
pkgver=1.3
pkgrel=2
pkgdesc='Standalone version of arguments parsing functions from GLIBC'
arch=('i386' 'amd64')
url='http://www.lysator.liu.se/~nisse/misc/'
license=('GPL')
depends=('libc')
_port_patches=(
        patch-Makefile.am
        patch-configure.ac
        patch-testsuite_Makefile.am
        )
source=("http://www.lysator.liu.se/~nisse/misc/${pkgname}-${pkgver}.tar.gz"
        "${_port_patches[@]}")
md5sums=('720704bac078d067111b32444e24ba69'
         'c487d7123fad0d951c5d6e3460aa41fa'
         '4c87275bf64afd6890f2a1fd0ea24006'
         '2d70bd5b8532725a93f14a6e9ffc1ab8')

prepare() {
	cd "${pkgname}-${pkgver}"
	for i in "${_port_patches[@]}"; do
		msg "patch $i"
		patch -p0 -i "${srcdir}/$i"
	done
	sed -i '' -e 's|AM_CONFIG_HEADER|AC_CONFIG_HEADER|' configure.ac
	sed -i '' -e 's|AM_PROG_CC_STDC|AC_PROG_CC|' configure.ac
	autoreconf --install
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir" install

	install -dm755 "$pkgdir/usr/include"
	install -m644 argp.h "$pkgdir/usr/include"
}
