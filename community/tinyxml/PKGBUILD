# $Id: PKGBUILD 88660 2013-04-21 13:06:07Z arodseth $
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>
# Contributor: Larry Hajali <larryhaja[at]gmail[dot]com>
# Contributor: Hubert Grzeskowiak <arch at nemesis13 dot de>

pkgname=tinyxml
pkgver=2.6.2
pkgrel=6
pkgdesc='Simple, small, C++ XML parser that can be easily integrated into other programs'
url="http://www.grinninglizard.com/tinyxml/"
arch=('amd64' 'i386')
license=('zlib')
depends=('gcc-libs')
makedepends=('gmake' 'gcc')
_port_patches=(
               'patch-tinyxml.h'
               )
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver//./_}.tar.gz"
        "$pkgname.pc")
sha256sums=('15bdfdcec58a7da30adc87ac2b078e4417dbe5392f3afb719f9ba6d062645593'
            '0e37a568eaebb8f0644f148a3d3efa921ac518217e1c0de46c2859d26516bcc4')

build() {
  cd "$srcdir/$pkgname"

  gmake CC=clang CXX=clang++ CXXFLAGS="${CXXFLAGS} -std=c++11 -stdlib=libc++ -fPIC"
  clang++ -fPIC "$CXXFLAGS" -shared -o "lib${pkgname}.so.0.$pkgver" \
      -Wl,-soname,"lib${pkgname}.so.0" $(ls *.o | grep -v xmltest)
}

package() {
  cd "$srcdir/$pkgname"

  install -dm 0755 "$pkgdir"/usr/{lib,include}
  install -m 0755 "lib${pkgname}.so.0.$pkgver" "$pkgdir"/usr/lib/
  install -m 0644 "$pkgname.h" tinystr.h "$pkgdir"/usr/include

  # License-files
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 readme.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # pkgconfig-file
  install -dm755 "$pkgdir/usr/lib/pkgconfig"
  install -m644 "$srcdir/$pkgname.pc" "$pkgdir/usr/lib/pkgconfig/$pkgname.pc"

  (cd "$pkgdir/usr/lib"
    ln -s "lib${pkgname}.so.0.$pkgver" "lib${pkgname}.so.0"
    ln -s "lib${pkgname}.so.0.$pkgver" "lib${pkgname}.so"
  )
}

# vim:set ts=2 sw=2 et:
