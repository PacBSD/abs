# $Id: PKGBUILD 105335 2014-02-05 13:36:53Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Alexey Yakovenko <waker@users.sourceforge.net>

pkgname=deadbeef
pkgver=0.6.2
pkgrel=9
pkgdesc='A GTK+ audio player for GNU/Linux.'
arch=('i386' 'amd64')
url='http://deadbeef.sourceforge.net'
license=('GPL2')
depends=('gtk3' 'alsa-lib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('libvorbis' 'libmad' 'flac' 'curl' 'imlib2' 'wavpack' 'libsndfile' 'libcdio' 'libcddb'
             'libx11' 'faad2' 'zlib' 'intltool' 'pkgconf' 'libpulse' 'libsamplerate'
             'yasm' 'ffmpeg' 'gnu-libiconv' 'gcc')
optdepends=('libsamplerate: for Resampler plugin'
            'libvorbis: for Ogg Vorbis playback'
            'libmad: for MP1/MP2/MP3 playback'
            'flac: for FLAC playback'
            'curl: for Last.fm scrobbler, SHOUTcast, Icecast, Podcast support'
            'imlib2: for artwork plugin'
            'wavpack: for WavPack playback'
            'libsndfile: for Wave playback'
            'libcdio: audio cd plugin'
            'libcddb: audio cd plugin'
            'faad2: for AAC/MP4 support'
            'dbus: for OSD notifications support'
            'pulseaudio: for PulseAudio output plugin'
            'libx11: for global hotkeys plugin'
            'zlib: for Audio Overload plugin'
            'ffmpeg: for ffmpeg plugin')
install='deadbeef.install'
options=('!clang')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('786f3a01fc9e03e73191d1cce43250a3')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i '' -e 's, -lsupc++,,' \
		plugins/adplug/Makefile.in \
		plugins/gme/Makefile.in \
		plugins/sid/Makefile.in \
		plugins/supereq/Makefile.in

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --enable-gtk3 \
	--disable-alsa --disable-nullout \
	--disable-vfs-zip \
	--with-libiconv-prefix=/usr/local LIBS="-lexecinfo -lintl"
  gmake
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake prefix="${pkgdir}/usr" install
}
