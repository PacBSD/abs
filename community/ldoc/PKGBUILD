# $Id: PKGBUILD 101737 2013-11-30 16:22:11Z seblu $
# Maintainer: Anthony donnelly <Amzo@PacBSD.org>

pkgname=ldoc
pkgver=1.4.3
pkgrel=3
pkgdesc='A LuaDoc-compatible documentation generation system'
arch=('any')
url='http://stevedonovan.github.com/ldoc/'
license=('custom')
depends=('lua' 'lua-penlight')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stevedonovan/ldoc/archive/$pkgver.tar.gz")
md5sums=('eda6879140dce9435f5a264ef970a205')

package() {
  cd LDoc-$pkgver
  # install library
  install -dm755 "$pkgdir/usr/share/lua/5.3"
  cp -R ldoc "$pkgdir/usr/share/lua/5.3"

  # install binaries
  install -dm755 "$pkgdir/usr/bin"
  install -m755 ldoc.lua "$pkgdir/usr/bin/ldoc.lua"
  ln -s ldoc.lua "$pkgdir/usr/bin/ldoc"

  # copy docs
  install -dm755 $pkgdir/usr/share/{licenses,doc}/$pkgname
  install -m644 readme.md "$pkgdir/usr/share/doc/$pkgname/readme.md"
  install -m644 doc/doc.md "$pkgdir/usr/share/doc/$pkgname/doc.md"
  # copy license
  install -m644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
