# $Id: PKGBUILD 92159 2013-06-01 21:53:18Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Eddie Lozon <almostlucky@attbi.com>

pkgname=pekwm
pkgver=0.1.17
pkgrel=11
pkgdesc='Small, fast, functional, and flexible window manager'
arch=('i386' 'amd64')
license=('GPL')
url='https://www.pekwm.org/projects/pekwm'
depends=('gcc-libs' 'libjpeg' 'libpng' 'libxft' 'libxinerama' 'libxpm' 'libxrandr')
makedepends=('gendesk')
optdepends=('lxappearance: feature-rich GTK+ theme switcher'
            'nitrogen: background browser and setter'
            'tint2: basic, good-looking task manager')
backup=(etc/pekwm/autoproperties
        etc/pekwm/autoproperties_typerules
        etc/pekwm/config
        etc/pekwm/config_system
        etc/pekwm/keys
        etc/pekwm/menu
        etc/pekwm/mouse
        etc/pekwm/mouse_click
        etc/pekwm/mouse_sloppy
        etc/pekwm/mouse_system
        etc/pekwm/start
        etc/pekwm/vars)
source=("http://www.pekwm.org/projects/pekwm/files/$pkgname-$pkgver.tar.bz2")
sha256sums=('8a1fd3bf9f38e8c7bb2b2864c090f986b60cec2281ecf1bba462d120fb327d00')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  gendesk  -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --name PekWM
  gendesk  --pkgname "${pkgname}_kdm" --exec "$pkgname" --name PekWM

  CFLAGS+=" -DICONV_CONST"
  LDFGLAGS+=" -liconv"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --prefix=/usr --sysconfdir=/etc
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake DESTDIR="${pkgdir}" install
  install -dm755 \
      "${pkgdir}/usr/share/xsessions"
  install -m644 "$pkgname.desktop" \
    "${pkgdir}/usr/share/xsessions/$pkgname.desktop"
  install -dm755 \
     "${pkgdir}/usr/share/apps/kdm/sessions"
  install -m644 "${pkgname}_kdm.desktop" \
    "${pkgdir}/usr/share/apps/kdm/sessions/${pkgname}.desktop"
}

# vim: ts=2 sw=2 et
