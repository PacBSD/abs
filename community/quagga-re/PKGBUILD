# $Id: PKGBUILD 113569 2014-06-25 19:41:28Z seblu $

pkgname=quagga-RE
pkgver=0.99
pkgrel=4
pkgdesc='BGP/OSPF/ISIS/RIP/RIPNG routing daemon suite'
arch=('i386' 'amd64')
url='https://github.com/Quagga-RE/quagga-RE/tree/RE-stable-0.99'
license=('GPL2')
depends=('net-snmp' 'readline' 'ncurses' 'perl')
options=('!buildflags')
install=quagga.install
source=("$pkgname-$pkgver.zip::https://github.com/Quagga-RE/quagga-RE/archive/RE-stable-0.99.zip"
        "quagga.in"
        "watchquagga.in")
conflicts=('quagga')

build() {
  cd $pkgname-RE-stable-$pkgver
  autoreconf -fvi
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/quagga \
    --localstatedir=/run/quagga \
    --enable-exampledir=/usr/share/doc/quagga/examples \
    --enable-vtysh \
    --enable-isisd \
    --enable-isis-topology \
    --enable-netlink \
    --enable-snmp \
    --enable-tcp-zebra \
    --enable-pcreposix \
    --enable-multipath=64 \
    --enable-user=quagga \
    --enable-group=quagga \
    --enable-configfile-mask=0640 \
    --enable-logfile-mask=0640
  gmake
}

package() {
  # upstream install
  cd $pkgname-RE-stable-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 "${pkgdir}"/etc/rc.d
  install -m644 "${srcdir}"/quagga.in "${pkgdir}"/etc/rc.d/quagga
  install -m644 "${srcdir}"/watchquagga.in "${pkgdir}"/etc/rc.d/watchquagga
  chmod +x "${pkgdir}"/etc/rc.d/quagga
  chmod +x "${pkgdir}"/etc/rc.d/watchquagga
}

# vim:set ts=2 sw=2 et:
md5sums=('3c3f0c5841a878a31f2570000ab8b622'
         'bb5fc293d232cd74fe89c50884d45bf8'
         'eff65db4d415318e85a9025570b11a39')
