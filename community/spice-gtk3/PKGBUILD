# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk3
pkgver=0.24
pkgrel=6
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('amd64')
url="http://spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'dbus-glib' 'gtk3' 'libcacard' 'libpulse' 'usbredir' 'perl-text-csv' 'libsoup')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'spice-protocol' 'usbutils' 'vala')
source=("http://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2")
sha256sums=('6efe500845f910945670f11664f584fd13b9db494075c41137eb7bacd60d1ed1')

prepare() {
  cd "$srcdir/spice-gtk-$pkgver"
  sed -i'' -e 's,/usr/bin/env python,/usr/bin/python2,' spice-common/spice_codegen.py
}

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  find . -name aclocal.m4 | xargs sed -i '' -e 's|1.13|1.14|g' 
  find . -name configure | xargs sed -i '' -e 's|1.13|1.14|g'
  PYTHON=python2 ./configure --prefix=/usr  SSL_LIBS="-L/usr/lib -lssl -lcrypto" SSL_CFLAGS="-I/usr/include/openssl" \
    --disable-static --enable-vala --with-gtk=3.0 \
    --with-coroutine=gthread
  sed -i '' -e 's|lcelt |lcelt051 |g' $(grep -rl "-lcelt" ./)
  gmake
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  gmake DESTDIR="$pkgdir/" install
}
